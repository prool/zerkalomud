#28700
насмертьстража~
0 f 100
~
mecho Умирая, страж слабым голосом начал говорить.
mecho Страж прошептал: А-аа-а, Это Вы! Я знал, что рано или поздно Вы придете... Хочешь знать кто я?
mecho Страж прошептал: Ну что ж я расскажу... Когда-то давно я был простым кузнецом,
mecho Страж прошептал: но Великая Богиня  приблизила меня к себе. И с тех пор удача и слава
mecho Страж прошептал: были моими спутницами....  Ни в чем я не знал затруднений, 
mecho Страж прошептал: ибо Великая Богиня помогала мне - она давала   мне деньги, она указывала
mecho Страж прошептал: дорогу, она истребляла моих врагов... Но однажды   другие Боги 
mecho Страж прошептал: прогневались на мою покровительницу, за то что она нарушает запрет
mecho Страж прошептал: на невмешательства в дела смертных... Они изгнали ее из пантеона, и  
mecho Страж прошептал: в наказание наслали на мой народ орды славянских племен 
mecho Страж прошептал: откуда-то с Севера. Я знаю..., сейчас я умру, о Вы никогда не найдете
mecho Страж прошептал: тех кто оберегает Храм Падшей Богини, тех кто ушел в Ночь,
mecho Страж прошептал: тех кто стал стражем Ночи!!!
mecho Сказав это страж упал бездыханным.
~
#28701
встречаследопыт~
0 q 100
~
wait 1
msend %actor.name% Добрый день %actor.name%!
msend %actor.name% Горе у нас, украли в нашем селении самого красивого юношу.
wait 1
msend %actor.name% Следы ведут в эти странные неприятные развалины, мы их
msend %actor.name% все прочесали, но ничего не нашли. Помоги нашему горю,
msend %actor.name% найди сына нашего старейшины, освободи его и приведи к нам.
~
#28702
разобратькирп~
2 c 0
разобрать разломать~
if !(%arg.contains(кирп)%) 
  wsend  %actor% А что именно?
  return 0
  halt
end
wsend %actor% Вы стали разбирать кирпичи, удивляясь как легко вам это удается.
if %actor.sex%==1
  wechoaround %actor% %actor.name% стал разбирать кирпичную кладку.
else
  wechoaround %actor% %actor.name% стала разбирать кирпичную кладку.
end
wait 3s
wdoor 28709 down room 28749
wsend %actor% Вы открыли темный проход на север, быстро опускающийся вниз.
if %actor.sex%==1
  wechoaround %actor% %actor.name% открыл темный проход на север, быстро опускающийся вниз.
else
  wechoaround %actor% %actor.name% открыла темный проход на север, быстро опускающийся вниз.
end
~
#28703
ресет~
2 f 100
~
* if %room.down% == down
wdoor 28709 down purge
* end
~
#28704
развязатьпленник~
0 c 100
развязать~
if !(%arg.contains(пленник)%) 
  msend  %actor% А что именно?
  return 0
  halt
end
wait 1
msend %actor% Вы развязали пленника и помогли ему встать.
mechoaround %actor% %actor.name% развязал%actor.g% пленника и помог%actor.q% ему встать.
wait 3s
mecho Пленник слабо прошептал:
mecho Отведите меня к своим, я даже не знаю где нахожусь.
mecho Голова гудит, ничего не понимаю, где я, что я, зачем здесь оказался.
mload mob 28709
wait 1
mpurge %self%
~
#28705
следуй~
0 d 0
след следуй~
wait 1
mecho Пленник встряхнул головой и посмотрел на вас.
say Вспомнил, ох ..... как голова болит.
wait 1
say Кто-то вечером в деревне набросил сзади на меня петлю, все в глазах померкло.
say Думал степняки напали на поселение, ан нет притащили в это дурное место.
say У нас и раньше пропадали юноши и девушки - и вот надо же со мной
say приключилось такое. Ведь знал надо быть осторожным, но чтобы прямо
say посередине поселения такое случилось.
дум
say Помоги выбраться отсюда, я даже думаю меня ищут где-то рядом.
say У самого нет сил искать дорогу.
calcuid nastor 28705 mob
detach 28701 %nastor.id%
calcuid mesto 28763 room
attach 28708 %mesto.id%
встать
follow .%actor.name%
detach 28705 %self.id%
~
#28706
смертьприспупленника~
0 f 100
~
if (%world.curmobs(28710)%==1) && (%world.curmobs(28711)%==0)
  mecho Когда умер последний из приспешников вы заметили что-то у стены.
  mload mob 28711
end
~
#28707
смертьследопыта~
0 f 100
~
if %world.curobjs(28704)%==0
  mecho Что-то блеснуло на шее умирающего следопыта.
  mload obj 28704
end
if %world.curmobs(28709)%==1
  mecho Пленник удивленно посмотрел по сторонам.
  mecho Пленник сказал: кошмар, что творится, извини что нечем отблагодарить,
  mecho Пленник сказал: но я пойду домой.
  mecho Пленник быстро скрылся с глаз.
  calcuid hmob 28709 mob
  mpurge %hmob%
end
~
#28708
встречаследопыт2~
2 g 100
~
if %actor.vnum% == 28709
  wait 2s
  if %world.curmobs(28705)% ==1
    wait 2s
    wecho Следопыт увидел пленника и встал как вкопанный.
    wait 1s
    wecho Потом вдруг вздрогнул и на его лице появилась вымученная улыбка.
    wecho Он бросился к пленнику, обнял.
    wait 1s
    wecho День добрый, Павлуша, как рад тебя найти, просто нет слов.
    wecho Мы тут все обыскали и не чаяли тебя живым увидеть.
    wait 1s
    wecho С этими словами он вдруг что-то схватил под рубахой очень похожее
    wecho на амулет, вздрогнул, изменился в лице и вдруг расхохотался.
    wait 1s
    wecho Ты думаешь мы тут случайно? Тебя ищем? Как бы не так, это я тебя
    wecho скрутил за твоим домом и это мои люди тебя принесли сюда.
    wecho Но смотрю тебя освободили, значит придется разобраться со всеми
    wecho и отнести тебя к нашей богине - ей нужна свежая кровь.
    calcuid nastor 28705 mob
    wait 1
    wpurge %nastor%
    wload mob 28712
    detach 28708 %self.id%
  end
end
~
#28709
проходв288~
2 e 100
~
wait 1
if %world.curobjs(28704)%==0
  halt
end
if %actor.haveobj(28704)%
  calcuid hobj 28704 obj
  wpurge %hobj%
  wsend %actor% Вдруг кровавый знак выскочил из поклажи, обжег вас и полетел
  wechoaround %actor% Вдруг из поклажи %actor.rname% выскочил кровавый знак и полетел
  wecho по направлению к северной стене. Там он попал прямо в углубление по
  wecho середине стены. Запахло черной магией.
  wait 1s
  wecho Как только он коснулся поверхности стены, стена вдруг исчезла,
  wecho и на ее месте открылся проход на север.
  wdoor 28767 north room 28800
end
~
#28710
ресетпроход~
2 f 100
~
* if %room.north% == north
wdoor 28767 north purge
* end
if %exist.mob(28808)%
  calcuid target 28808 mob
  exec 28810 %target.id%
end
~
$~
