#25700
КТО ТО ПРИШЕЛ В КЛЕТКУ И ЧЕГО ТО ХОЧЕТ:)~
0 r 100
~
wait 1
say Приветствую тебя путник!
mecho _-  Много лет я провел в изгнании за свои магические эксперименты.
дум
if %actor.name% == Свентовит
  wait 1s
  mecho _-  Но сейчас у меня появилась одна дорогостоящая идея и мне не хватает денег 
  mecho  -  чтобы осуществить ее. Я могу продать тебе кое-какие заклинания изученные мною
  mecho _-  и изложенные в книгах.
  mecho _-  Групповое исцеление  - 550.000 кун
  mecho _-  Массовое молчание    - 300.000 кун
  mecho _-  Массовое проклятие   - 400.000 кун
  mecho _-  Массовая слепота     - 500.000 кун
  mecho _-  Запечатать комнату   - 180.000 кун
  mecho _-  Огненный шар         - 150.000 кун
  mecho _-  Длительное оцепенение- 100.000 кун
  mecho _-  Критическое исцеление- 90.000 кун
  wait 1s
  mecho _-  Решайся быстрее времени мало-скоро новолуние! 
  wait 3s
end
mecho  -  Да чуть не забыл -- если твоя душа не умерла, и жаждет приключений, то за 100 кун
mecho  -  переправлю тебя туда, где силы твои найдут достойное применение.
attach 25701 %self.id%
~
#25701
Дали деньги Чародею.~
0 m 100
~
wait 1s             
set i 0
eval rem %random.30%
eval cha %random.20%
if %amount% > 100
  set i 1
  if %rem% < %actor.remort% && %amount% > 80000
    say Такой умный а людям наслово веришь! А они обманывать умеют!     
    wait 1s
    say Может в следующий  раз ты мне понравишься больше!
    mecho %self.name% исчез в яркой вспышке,  вместе с вашими деньгами!
    wait 1
    mpurge %self%
  end
  if %cha% > %actor.cha% && %amount% > 80000
    say Спасибо тебе за деньги, но не нравишься ты мне!
    say И я тебе ничего не дам!
    wait 1s
    say Может в следующий раз ты мне понравишься больше!
    mecho %self.name% исчез в яркой вспышке,  вместе с вашими деньгами!
    wait 1
    mpurge %self%
  end
end 
switch %amount%
  case 100
    mecho Чародей поднял руки к небу и с небес ударила золотистая молния.
    mechoaround %actor% %actor.name% исчез в воздушном круговороте.
    msend %actor% Вас завертело в воздушном урагане и  Вы оказались в другом месте.
    mteleport %actor% 25700
    halt
  break
  case 90000
    mload obj 513
    give книг %actor.name%
  break
  case 100000
    mload obj 515
    give книг %actor.name%
  break
  case 150000
    mload obj 571
    give книг %actor.name%
  break
  case 180000
    mload obj 555
    give книг %actor.name%
  break
  case 500000
    mload obj 567
    give книг %actor.name%
  break
  case 400000
    mload obj 566
    give книг %actor.name%
  break
  case 300000
    mload obj 569
    give книг %actor.name%
  break
  case 550000
    mload obj 532
    give книг %actor.name%
  break
  default
    say И чего же ты за эти деньги хочешь?
    say Решайся быстрее -- время дорого, еще столько надо успеть сделать!
    give %amount% кун %actor.name%
  done 
  wait 1s          
  if %i%           
    say А теперь прощай!
    mecho %self.name% исчез в яркой вспышке!
    mpurge %self%
  end
~
#25702
Зашли к старику, охраняющему темницу.~
0 r 100
~
wait 1
mecho Старик сказал тебе:
mecho  - Много лет я живу в этом доме. Под ним находится темница для нечестивых
mecho _- колдунов и жрецов которых полонили доблестные витязи.
mecho _- Сами княжеские войны сдавали мне под расписку этих осужденных негодяев.
mecho  - Не так давно произошла в темнице сильнейшая битва. Но я не могу покинуть
mecho  - свой пост и посмотреть, что там случилось. И самое плохое заключается
mecho  - в том, что Зло заворочалось там внизу, а я как назло забыл древнее
mecho  - заклинание, позволяющее запечатывать помещения навечно. Помоги мне
mecho  - пожалуйста чужеземец посмотри, что же там сейчас происходит и постарайся
mecho _- найти древнее заклинание.
mecho _- Поможешь?
вопр
attach 25703 %self.id%
~
#25703
Поможет!:)~
0 d 1
помогу~
wait 2
say Спасибо тебе странник.
say Вот возьми ключик и быстрее возвращайся назад!
mload obj 25734
give ключ %actor.name%
detach 25702 %self.id%
detach 25703 %self.id%
~
#25704
У БЕЛОГО МАГА-УЗНИКА~
0 r 100
~
wait 1s
say Давно я не видел здесь кого-либо живого...
дум
say Но я рад что ты пришел сюда. Я могу рассказать много чего интересного.
вопр
say Хотя подожди, я кажется понял для чего ты сюда пришел-тебя послали узнать что же здесь
say произошло!
say А было здесь вот что: некоторое время назад темные колдуны решили бежать из темницы
say собрав все свои силы они разрушили охранные заклинания, но так как силы их были на
say исходе -- они не выдержали бы схватки с хранителем темницы. Они решили пробираться под
say землей за пределы тюрьмы. Они пробили ужасными заклинаниями проход вглубь земли, а тех
say из нас кто пытался им противостоять уничтожили. Я не мог им помешать, я смог только
say защитить себя... Эх, как бы я хотел встать и выйти сейчас на волю к свету.Сам видишь,
say как здесь темно и неуютно, вот так и сижу здесь уже многие годы, ощущая, что где то
say внизу шевелится Древнее Зло.
взд
~
#25705
У ЧЕРНОГО МАГА-УЗНИКА~
0 r 100
~
wait 1s
say Давно я не видел здесь кого-либо живого...
дум
say Но я рад что ты пришел сюда. Я могу рассказать много чего интересного.
вопр
say Хотя подожди, я кажется понял для чего ты сюда пришел -- тебя послали узнать, что же здесь
say произошло!
say А было здесь вот что: некоторое время назад темные колдуны решили бежать из темницы
say собрав все свои силы они разрушили охранные заклинания, но так как силы их были на
say исходе -- они не выдержали бы схватки с хранителем темницы. Они решили пробираться под
say землей за пределы тюрьмы. Они пробили ужасными заклинаниями проход вглубь земли, а тех
say из нас кто пытался им противостоять уничтожили. Я не мог им помешать, я смог только
say защитить себя...
say Вот так и сижу здесь уже многие годы, ощущая что где то
say внизу шевелится Древнее Зло.
взд
say Хм, Я вижу ты это уже знаешь.
дум
say Я понял - тебе это уже все рассказал мой брат по несчастью...
say Но он не знает главного! Мои товарищи-колдуны потеряли остатки разума под действием
say чьей то Воли. А принадлежит она очень страшному существу которое не остановится ни
say перед чем. По сути своей мои друзья погибля в тот момент когда это существо поработило
say их. И сейчас Его слуги, что то хотят выкопать там внизу, но что именно я не знаю
печал
say Но я догадываюсь, что скорее всего они хотят откопать Его из толши земли.
say Если им это удастся мы все умрем!
say Эх, как бы я хотел попробовать сейчас мясо, но к сожалению здесь нечем
say поддерживать огонь.
плак
say Иди не буду тебя задерживать -- тебе еще многое надо сделать!
взд
~
#25706
Дали светящийся шарик белому узнику.~
0 j 100
~
*а лоад100% и ограничение по количеству в мире нафиг типа не нада ?
wait 1s
if (%object.vnum% ==  126)
  wait 1s
  say Спасибо тебе большое и прощай -- время моего заключения закончилось!
  if %random.5% == 1 && %world.curobjs(25732)% < 3
    mload obj 25732                                 
    бро посох
  end
  wait 1
  mpurge %self%
end
~
#25707
Дали вяленое мясо черному узнику.~
0 j 100
~
*а лоад100% и ограничение по количеству в мире нафиг типа не нада ?
wait 1s
if (%object.name% == вяленое мясо)
  wait 1s
  say Спасибо тебе большое и прощай -- время моего заключения закончилось!
  if %random.5% == 1 && %world.curobjs(25733)% < 3
    есть мяс
    mload obj 25733                                 
    бро все
  end
  wait 1
  mpurge %self%
end    
wait 1s 
бро %object%
~
#25708
Телепорт к тайной комнате.~
2 c 1
вдавить повернуть надавить нажать~
if !(%arg.contains(булыжник)%) 
  wsend       %actor% Что вы хотите вдавить???
  return 0
  halt
end
wsend       %actor% Вы вдавили булыжник в стену.
wechoaround %actor% Поднатужившись, %actor.name% вдавил%actor.g% булыжник в стену.
wait 1s
wsend %actor% .- Вы появились в тайной комнате.
wteleport %actor% 25757
wecho %actor.name% исчез%actor.q%!
~
#25709
Телепорт из тайной комнаты.~
2 d 1
назад~
wteleport %actor% 25756
wechoaround %actor% %actor.name% появил%actor.u% здесь.
~
#25710
Толкаем валун.~
2 c 1
толкнуть сдвинуть  нажать~
if !(%arg.contains(валун)%) 
  wsend       %actor% Что вы хотите толкнуть???
  return 0
  halt
end
wsend       %actor% Поднатужившись, вы сдвинули валун с места.
wechoaround %actor% Поднатужившись, %actor.name% сдвинул%actor.g% валун в сторону.
wait 1s
wsend %actor% .- Вы сдвинули валун в сторону.
wload obj 25704
detach 25710 %self.id%
end
~
#25711
Квест главы наемников.~
0 r 100
~
wait 1s
say Здравствуй путник. Вижу ты сильный воин. Помоги нам вот уже несколько месяцев за нами
say идут по следам искуснейшие убийцы. Помоги нам-убей их и принеси мне охранную грамоту
say которая позволяет им проходить сквозь любые охранные посты.
взд
~
#25712
Убили убийцу~
0 f 100
~
if ( %world.curobjs(25735)% == 0 )
  mecho Убийца умер!
  mload obj 25735
end
~
#25713
Дали главе наемников грамоту.~
0 j 100
~
wait 1s
if (%object.vnum% != 25735 ) 
  дум
  брос %object.name%
  halt
end
wait 1
mpurge %object%
if %random.100% < 41
  say Спасибо тебе большое! Возьми это все! Дудочка не простая если в нее подуть она поможет
  say в твоем самом главном сражении.
  mload obj 25736
  дать все %actor.name%
else
  say Вот несколько монет, возьми их в награду за помощь.
  set buffer %actor.gold(+2200)%
end
~
#25714
Бросили сферу-появился проход!~
1 h 100
~
if (%self.room% == 25729 )
  *странно, каким же образом wecho wpurge wdoor тут сработают?
  wait 1s
  oecho Огромный взрыв озарил все вокруг ярким светом.
  oecho За стеной оказался широкий высокий проход, построенный не человеком и не для человека.
  odoor 25729 south room 25739
  odoor 25739 north room 25729 
  wait 1s               
  *а стирать двери при репопе  забыл         
  calcuid rum 25729 room
  attach 25722 %rum.id%
  oecho Ну что же пора двигаться дальше!
  wait 1  
  opurge %self%
end
~
#25715
Смерть ожившего духа.~
0 f 100
~
*прикольно в качестве квестовых предметов использовать книги  :-)
mecho Древний дух успокоился навечно.
if ( %world.curobjs(555)% == 0 && %random.7% == 1)
  mload obj 555
end
mload obj 25737
~
#25716
Сдача квеста старику-сторожу.~
0 j 100
~
wait 1s
if ( %object.vnum% == 25737 )  || ( %object.vnum% == 555 )
  wait 1
  mpurge %object%
  *дают один предмет а пуржится 2, к чему это приведет ? а потом вороны исчезают..
  *mpurge череп
  *mpurge книга
  say Спасибо тебе большое! Да это очень похожее заклинание, но мне нужно будет его изменить.
  mecho Внезапно старик преобразился и засветился внутренним огнем. ПОД ЗЕМЛЕЙ ПРОШЕЛ ГУЛ
  say Извини я не могу отпустить тебя, ты слишком много знаешь...
  mtransform 25730
  wait 1
  атак %actor.name%
  attach 25717 %self.id%
  detach 25730 %self.id%
end
~
#25717
Молодой колдун умер.~
0 f 100
~
*камнепад.. счас.. может тут еще круг пустоты,суд,Зб,МО положить ?
*mload obj 579
if (%world.curobjs(551)%  < 1 ) && (%random.100% < 10 )
  mload obj 551
end
~
#25718
Дудочка призывает героев:)~
1 c 2
свистеть~
if !(%arg.contains(дудочка)%) 
  oecho Куда вы хотите свистнуть?
  return 0
  halt
end
oecho Поднатужившись, вы свистнули в дудочку.
oload mob 25710
oload mob 25711
oload mob 25712
wait 1
opurge %self%
end
~
#25719
Агро триг на оживний дух(для вызванных дудочкой героев).~
0 n 100
~
*и какого рожна этот триг должен делать?
атак .дух
~
#25720
Отковыриваем дощечку.~
2 c 1
отодрать отколоть отломать~
if !%arg.contains(дощечк)%
  wsend       %actor% Что вы хотите отколоть???
  return 0
  halt
end
wait 1    
*ну прально.. пришел отколол десяток дощечек.. и оделся в +200 мема       
if %random.25% == 7 && %world.curobjs(25722)% < 4
  wsend %actor% Поднатужившись, вы откололи дощечку.
  wload obj 25722                                   
else
  wsend %actor% __Неосторожным  ударом вы разбили дощечку вдребезги!
end                      
*и как потом этот триггер после репопа тут появится ? ребутом ?
attach 25723 %self.id% 
detach 25720 %self.id%
~
#25721
Триггер на 500 кун.~
0 m 1
~
*не ну юморист.. типа 500 кун чтобы моб отдал что на нем залоадилось ??
wait 1s
поклон .%actor.name%
*if ( %amount% == 500 )
*wecho Хорошо вот возьми.
*дать все %actor.name%
*end
~
#25722
пурж прохода при репопе~
2 f 100
~
wdoor 25729 south purge
wdoor 25739 north purge
~
#25723
пурж прохода при репопе~
2 f 100
~
attach 25720 %self.id%
detach 25723 %self.id%
~
#25724
бой со скелетом~
0 f 100
~
return 0
mecho Скелет рассыпался кучей изломанных костей.
mload obj 25738
~
#25725
лоад скелета~
1 n 70
~
eval pause 10+%random.15%
wait %pause%s
if %world.curmobs(25731)% < 15
  oecho Куча костей зашевелилась и превратилась в грязный изломанный скелет.
  oload mob 25731
  wait 1
  opurge %self%
end
~
#25726
Репоп зоны~
2 f 100
~
*calcuid conj 25700 mob
*detach 25701 %conj.id%
detach 25701 %world.mob(25700)%
*calcuid old 25702 mob
*detach 25703 %old.id%
detach 25703 %world.mob(25702)%
wdoor 25729 south purge
wdoor 25739 north purge
attach 25710 %world.room(25772)%
~
#25727
загрузка злого скелета~
0 n 100
~
eval target %random.pc%
mkill %target%
n
e
s
s
w
w
n
e
~
#25728
Перебили убийц~
0 f 100
~
if (%world.curmobs(25728)% < 2 ) && (%world.curobjs(25735)% < 1 )
  mload obj 25735
end
~
#25729
Дракон убит~
0 f 100
~
if %world.curobjs(3355)% < 1 && %world.curobjs(3354)% < 1  && %random.100% < 3
  mload obj  3354
end
if ( %random.100% < 20 ) && ( %world.curobjs(25739)% < 5 )
  mload obj 25739
end
~
#25730
Тощщой пытается надеть панцирь~
1 j 100
~
if %actor.con% < 20
  osend %actor% Как вы собираетесь воевать в этой броне, коли туда можно запихать пятерых таких как вы?
  oechoaround %actor% %actor.name% попытал%actor.u% примерить панцирь из шкуры дракона.
  oechoaround %actor% Весьма похоже на пугало из оглобли, облаченное в старый кафтан.
  return 0
  halt
end
~
$~
