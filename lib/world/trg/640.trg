#64000
Тригер подмастерья гончара~
0 q 100
~
wait 2 
эм окинул Вас взглядом и вернулся к прерванной работе
~
#64001
Зашли на коновязь~
0 r 100
~
wait 1s
say Здраве буде!
wait 1s
улыб
wait 1s
say За лошадкой, мил человек?
~
#64002
Горшки в ауте~
2 e 100
~
if %exist.mob(64043)%
  * * удаляем сына если он вдруг сотался
  calcuid nMob2 64043 mob
  wpurge  %nMob2.id%
end
*детачим триг помогу жене гончара
calcuid nMob3 64042 mob
detach 64024 %nMob3.id%
if %random.100% < 41
  *wecho exist.mob(64003) %exist.mob(64003)%
  if !%exist.mob(64003)%
    wait 1
    wecho Вы прошли вдоль полок, разглядывая удивительные горшки и кувшины.
    wait 2
    wecho Вдруг откуда не возьмись прямо перед вами выскочил паршивец-мальчишка!
    wload mob 64003
    wecho Вы испуганно отшатнулись и краем плаща зацепились за торчащий из полки гвоздь.
    wecho Дернувшись в сторону, вы неловко повернулись и уронили полку!
    wait 1s
    wecho &YБААААААААААХ! КРААААААХ! ТЫДЫЩЬ!&n
    wait 1s
    wecho Все горшки теперь просто гора осколков!
    wait 1s
    wecho Схватившись за сердце, вы невольно покосились на мастера-гончара за дверью.
    wait 1s
    * атач трига вымогает деньги ученик
    calcuid nMob4 64003 mob
    *attach 64003 %nMob4.id%
    *выполнить триг 
    exec 64003 %nMob4.id% 
  end
  *детачим триг
  *detach 64003 %nMob4.id% 
end 
~
#64003
Ученик гончара разводит на бабки~
0 z 100
~
wait 3
if %actor.sex% == 2
  say Посмотрите, тетя, что вы натворили!
elseif %actor.sex% == 1
  say Посмотрите, дяденька, что вы натворили!
end
mecho Ученик всплеснул руками и нагнулся к разбитым горшкам.
wait 1
say Нууу, если Вы мне дадите 20 кун, я попробую вас выручить...
say Убрать тут все, горшки переставить, чтобы никто ничего не заметил.
emot засунул руки в карманы и рассеянно засвител, невинно поглядывая по сторонам.
wait 1s
say А то сейчас позову мастера! Уж он вам спуску не даст!
~
#64004
Все таки развел~
0 m 1
~
if (%amount% < 20)
  wait 1
  say Вы что, за дурачка меня держите?
  mecho Ученик гончара гневно положил кучку кун себе в карман.
  wait 1
  say Ну смотрите, вот сейчас точно мастера позову, и Вам не поздоровится!
  msend %actor% Маленький вымогатель выжидающе посмотрел на Вас.
  mechoaround %actor% Ученик гончара выжидающе посмотрел на %actor.vname%
else 
  say Ну вот, теперь идите, ни о чем не беспокойтесь, я все приберу!
  wait 1s
  mecho Отвернувшись, маленький пройдоха пробурчал : "Все равно тут один брак был!"
  хи 
  миг %actor.name% 
  mecho Мальчик метнулся за какой-то шкаф и моментально скрылся с глаз.
  mpurge %self.id%
end
~
#64005
Хлопоты жены корчмаря~
0 b 10
~
wait 1
switch %random.8%
  case 1
    mecho  Жена корчмаря наклонилась и поправила сбившийся ковер.
  break
  case 2
    mecho  Жена корчмаря подошла к шкафчику в коридоре и заглянула внутрь.
    mecho  Жена корчмаря наклонила голову и пробормотала : 'Не забыть бы пригласить мастера'
    mecho  Жена корчмаря сказала :  'Совсем дверцы расшатались...'
  break
  case 3
    mecho  Жена корчмаря постучала в дверь одной из комнат.
    mecho  Жена корчмаря громко сказала : 'Пане просили разбудить!'
  break
done
end
wait 5s
~
#64006
Тригер дозорного на башне~
0 r 100
~
wait 1 
mecho Дозорный подозрительно оглядел вас с ног до головы.
наст 
wait 2s
say Кто вы такой? А ну не шатайтесь где не положено!
~
#64007
Тригер священника~
0 r 100
~
wait 1 
mecho Отец Алексий, довольный ходом строительства, вознес хвалу Господу и перекрестился.
~
#64008
Тригер каменщика~
0 r 100
~
wait 1
switch %random.8%
  case 1
    mecho  Каменщик довольно оглядел дело своих рук.
  break
  case 2
    миг %actor.name%
  break
done
end
~
#64009
Тригер иконописца~
0 r 100
~
wait 1
switch %random.8%
  case 1
    mecho  Сдернув шапку с головы, иконописец бросил ее на землю и притопнул ногой.
    if %random.100% < 10 && %world.curobjs(64004)% < 3
      mload obj 64004
    end
    mecho  &YМастер-иконописец восторженно вскричал : 'Великий получится Собор!'&n
  break
  case 2
    mecho  Мастер-иконописец восторженно огляделся и присвистнул.
  break
done
end
~
#64010
Пробираемся в подземелье~
2 c 0
двигать сдвинуть~
*неграмотный тригер имхо, не вижу смысла, при передвижении бочки, перемещать персонажа
*по идее должен просто проход открыться 
if !%arg.contains(бочка)%
  wsend %actor% Что двигаем?  
else
  wait 1
  wsend %actor% Отодвинув старую бочку, Вы обнаружили узкий вход!
  wechoaround %actor% %actor.name% осторожно отодвину%actor.q% бочку.
  wechoaround %actor% Внезапно неведомая сила утащила %actor.rname% в неизвестность!
  *osend %actor% Осторожно держась за края ямы, вы начали спускаться вниз.
  *osend %actor% Вы оказались в тайном подземелье!
  wteleport %actor% 64043
end
~
#64011
Лошадка везет груженную повозку~
0 n 100
~
wait 2
восток
wait 5s
юг
wait 7s
юг
wait 10s
юг
wait 8s
юг
wait 15s
юг
wait 8s
юг
wait 24s
юг
wait 10s
юг
wait 30s
attach 64012
mpurge %self%
~
#64012
Лошадка везет пустую повозку~
2 z 100
~
*из какого тригера вызывается я так и не понял )
*сделал что 011  триг атачет перед пуржем
oload mob 64015
wait 15s
север  
wait 15s
север
wait 6s
север
wait 6s
север
wait 6s
север
wait 6s
север
wait 6s
запад
wait 6s
~
#64013
Триггер дворняжки~
0 r 100
~
wait 1s
switch %random.3%
  case 3
    эм робко махнула хвостом
  break
  case 2
    наст
    mecho Робкая дворняжка посмотрела на Вас и негромко тяфкнула.
  break
  case 1
    follow %actor.name%
  break
done
~
#64014
Реакция корчмаря на дворняжку~
0 h 100
~
*проверяем при входе всех в том числе мобы
if (%actor.vnum% == 64016)
  *halt
  *корчмарь обнаружил что вошла дворняжка*
  wait 1 
  mecho _Корчмарь Федул взмахнул полотенцем и закричал : 'Ах ты, паршивая собачонка! Вон, вон отсюда!' 
  masound _Из корчмы донесся громкий крик : 'Ах ты, паршивая собачонка! Вон, вон отсюда!' 
  mecho _Собаченка, взвизгнув, умчалась за дверь, поджав хвост. 
  mpurge %actor.id%
  *wait 1
  *на соседней клетке появляется испуганная дворняжка*
  calcuid r2 64033 room
  attach 64015 %r2.id% 
  exec 64015 %r2.id%
end
~
#64015
Выскочившая за дверь дворняга~
2 z 100
~
wait 1s
wecho _Вдруг из корчмы с визгом выскочила испуганная собачка!
wload mob 64017
wait 1s
wforce дворняжка эм прижалась к земле и начала истошно лаять в сторону корчмы
detach 64015 %self.id%
*отдаю приказ дворняги) т.к триг для комнаты
*oforce дворняжка восток
*calcuid dvorn 64017 mob
*opurge %dvorn.id%
*нахер дворняге идти на восток потом ее пуржить?)
~
#64016
Дьякон дает квест~
0 d 0
помогу~
*Активизировать при подключении зоны Никольская горка*
~
#64017
Дьякон вручает награду~
0  0
~
*Активизировать при подключении зоны Никольская горка*
~
#64018
Седой паромщик встречает~
0 r 100
~
say Подобру, странник!
say Паром вот-вот отправлится. Если хотите, отвезу вас на тот берег Сейма.
say Кун 100, положите, однако.
~
#64019
Дали седому паромщику куны~
0 m 100
~
if %actor.vnum% != -1
  halt
end
if %amount% < 100
  дать %amount% кун %actor.name%
  say Ну за такие куны попробуй-ка сам переправится!
else
  mecho Внимание! Паром отправляется!
  wait 2
  msend %actor% Паромщик взялся за веревку и паром медленно заскользил по водной глади.
  mechoaround %actor% Паромщик взялся за веревку и %actor.name% медленно отбы%actor.q% на пароме.
  mteleport %actor% 64062 
  if %exist.mob(62808)%
    *телепорт обоих видов лошадок из квеста 3031
    calcuid lmob 62808 mob
    if %lmob.realroom% == 64086  
      mteleport %lmob% 64062
    end
  end
  if %exist.mob(62807)%
    calcuid lmob1 62807 mob
    if  %lmob1.realroom% == 64086
      mteleport %lmob1% 64062
    end
  end
  *horse
  *четакое хорс нипонял ( 
  mat %actor.realroom% mecho Седой паромщик произнес: 'Ну, бывай странник!'
end
~
#64020
внучок паромщика встречает~
0 r 100
~
say Подобру, странник!
say Ежели хотите к Путивлю добраться, то только нашим паромом через Сейм-батюшку.
say А следующая переправа ниже по Сейму сто верст будет. И дорога плохая туда.
say За 100 кун нынче возим. А как иначе.
emot шмыгнул носом, вытерев его рукавом рубахи
wait 1s
say Давайте пока куны, а я деда покличу.
~
#64021
дали куны внучку паромщика~
0 m 100
~
if %actor.vnum% != -1
  halt
end
if %amount% < 100
  дать %amount% кун %actor.name%
  say А ну топайте отсюда, ишь чего удумали!
else
  mecho Мальчишка сложил руки у рта и во всю мочь заорал: Деедуууушкааа! Плыви сюда!
  wait 2s
  mecho С той стороны к Вам медленно поплыл паром.
  msend %actor% Паромщик взялся за веревку и паром медленно заскользил по водной глади.
  mechoaround %actor% Паромщик взялся за веревку и %actor.name% медленно отбыл%actor.g% на пароме.
  mteleport %actor% 64086
  *телепорт обоих видов лошадок из квеста 3031
  if %exist.mob(62808)%
    calcuid lmob 62808 mob
    if %lmob.realroom% == 64062  
      mteleport %lmob% 64086
    end
  end
  if %exist.mob(62807)%
    calcuid lmob1 62807 mob
    if  %lmob1.realroom% == 64062
      mteleport %lmob1% 64086
    end
  end
  mat %actor.realroom% mecho Седой паромщик произнес: 'Ну, бывай странник!'
end
~
#64022
КвестерВошелКВедунье~
0 r 100
~
*если вошел квестер 1
if !%actor.quested(640102)% && %actor.quested(640101)%
  wait 1s
  say Да, прослышала я уже о беде сестры моей
  wait 1s
  mecho - Так вот слушай и запоминай:
  wait 1s
  mecho - Живет леший в дремучем, непроходимом лесу.
  mecho - Бродят по тропам лесным не люди и не звери, а страшные волколаки!
  mecho - У входа в самое логово застыл черным изваянием могучий дуб-стражник.
  mecho - Коли хватит у тебя смелости и силы духа
  mecho - Сможешь ты вызволить малыша из лап чудища лесного.
  mecho - Ну а коли нет, то и суда нет
  mecho - Я все равно больше ничем помочь не могу
  взд
  %actor.setquest(640102)%
  halt
end
*если вошел квестер 2
if !%actor.quested(640202)% && %actor.quested(640201)%
  wait 1s
  кив
  wait 1s
  say Знаю я все, знаю, можешь не рассказывать - на то я и ведунья.
  wait 1s
  mecho - Лучше слушай и запоминай что надо сделать:
  mecho - Нужно взять самую дорогую белую атласную ткань,
  mecho - И нож, украшенный изображениями Семаргла
  mecho - Как добудешь это, сразу езжай на то место где повстречал боярин Белую бабу.
  mecho - Да смотри, никому их не давай!
  wait 1s
  mecho - Как доедешь до места, возьми нож и начинай в клочья ткань кромсать.
  mecho - Ну а дальше...
  mecho - Богу молись ради грехов своих...
  wait 1s
  mecho - А коли справишься с ней - незабудь вырвать клок волос и отдать боярину!
  mecho - Тот клок волос и будет великим оберегом его дочке.
  wait 1s
  mecho - Еще знаю, что ткань ту белоснежную раз в год привозит купец из Ладоги
  mecho - Говорит он, есть там у них три сестрицы, вот у них и берет
  mecho - Хоть и очень дорого, но лучше атласа вовек не сыскать!
  wait 1s
  mecho - А нож тот, что нужен тебе, недавно подарил своей дочке брянский боярин.
  mecho - Уж незнаю, верно ли то...
  mecho - Правда ли, кривда ли...
  mecho - Но то так ведаю!
  mecho - А теперь иди!
  mecho - Я все равно больше ничем помочь не могу
  взд
  %actor.setquest(640202)%
  halt
end 
*если вошел квестер 3
if !%actor.quested(640302)% && %actor.quested(640301)%
  wait 1s
  кив
  say Знала я, что все равно ко мне придете!
  mecho - Сколько на этот раз людей не досчитались?
  mecho - А впрочем неважно...
  эм на миг задумалась, продолжая помешивать какое-то зелье в деревянной ступке
  wait 1s
  mecho - Вобщем так!
  wait 1s
  mecho - Дело тут нечисто...
  wait 1s
  mecho - Завелся в городе ерестун-оборотень.
  mecho - По ночам выходит он из своего человечьего тела и загрызает людей.
  mecho - Прогнать его нетрудно, 
  mecho - А распознать среди обычных людей почти невозможно.
  взд
  wait 1s
  mecho - В этом деле я так и быть тебе помогу!
  mecho - Внимательно слушай и запоминай:
  wait 1s
  mecho - Распознать ерестуна помогает трава адамова голова.
  mecho - Растет она близ воды, расцветает желтыми кувшинками.
  mecho - Я всегда хожу за ней по берегу Сейма-батюшки.
  mecho - Только учти, охораняют ее неусыпно слуги водяного.
  wait 1s
  mecho - Срывают ее через золотую или серебряную цепочку с кулоном из соколиного глаза.
  mecho - Найти такой кулон не просто, 
  mecho - Но слышала я, что совсем недавно, подарил его своей дочке Брянский боярин
  mecho - Незнаю насколько правда то - говорю что ведаю
  эм поставила на горелку емкость с каким-то зельем
  wait 1s
  mecho - Так вот...
  mecho - Когда найдешь траву адамова голова
  mecho - И сорвешь ее через цепочку, принеси мне.
  mecho - Я и скажу что делать дальше.
  mecho - Да смотри, никому их не давай!
  mecho - Времени у тебя немного! 
  mecho - Поторапливайся!
  %actor.setquest(640302)%
end 
~
#64023
ВошлиКЖенеГончара~
0 r 100
~
wait 1s
*mecho actor.quested(640101) %actor.quested(640101)% 
if !%actor.quested(640101)%
  mecho _Вдруг Вы заметили вылезающего из под скамейки маленького мальчика.
  mload mob 64043
  wait 1s
  mecho Ребенок капризно сморщил маленькое личико.
  wait 1s
  mecho &YУУУ-ААА!!! Не хочуууу!!! Не будууууууу! - закричал мальчик на всю округу.&n
  wait 1s
  mecho Ребенок с интересом посмотрел на мать, ожидая реакции.
  wait 3s
  mecho &YНееееет!!! Не будууууууу! - еще пуще кричал сын гончара&n
  wait 1s
  эм невыдержав нытья сына, всплеснула руками
  mecho _Жена гончара гневно вскричала: Да чтоб тебя леший взял!
  wait 2s 
  mecho _Тут же налетел ветер черный и унес мальчика в неведомые дали, к самому лешему.
  * удаляем сына
  if %exist.mob(64043)% 
    calcuid nMob2 64043 mob
    mpurge  %nMob2.id%
  end
  *
  wait 1s 
  эм охнула от неожиданности
  wait 1s
  mecho _И поняв наконец, что наделала - запричитала:
  mecho - Ой, ты моя кровинушка!
  mecho - Да, как же я теперь найду тебя! Ведь вынет душу из тебя леший!
  эм зарыдала пуще прежнего
  wait 1s 
  mecho - Да хоть бы кто теперь помог мне вернуть моего ребенка!!!
  всхл
  *атач трига помогу жене гончара
  attach 64024 %self.id%
else
  эм подняла заплаканные глаза
  say А где же мой сын?
end 
~
#64024
ПомогуЖенеГончара~
0 d 0
помогу найду~
wait 1s
mecho _В глазах жены гончара сверкнул лучик надежды.
эм вытерла слезы
wait 1s
say Спасибо тебе, добрый человек!
mecho - Забери моего сыночка от лютого лешего, а я в долгу не останусь!
mecho - Отдам самое дорогое, что у меня есть!
mecho - Ведь сын много дороже...
mecho _Скатилась по щеке несчастной женщины прозрачная слеза.
wait 1s
mecho - Да, чуть не забыла...
mecho - Как найти лешего спроси у моей сестры.
mecho - Она в городе зельями приторговывает, а потому все знает, все ведает.
эм отвернулась
mecho - А теперь иди, странник.
*** 
*флаг прохождения первой ступени
%actor.setquest(640101)%
detach 64023 %self.id%
detach 64024 %self.id%
~
#64025
ВошлиКБоярину~
0 r 100
~
if !%actor.quested(640201)%
  wait 1s
  say Приветсвую тебя путник, вижу не из нашего ты города.
  wait 1s
  mecho - А коли, иноземец ты, может сможешь помочь моей беде
  эм тяжело вздохнул
  wait 1s
  mecho - Ехал я недавече с ярмарки с дочкой своей.
  mecho - Вдруг, выходит из лесу на дорогу какая-то баба.
  mecho - Глаза закрыты, а из них слезы льют ручьем.
  mecho - А сама вся в белом с ног до головы.
  эм перекрестился
  wait 1s
  mecho - А дочурка моя спрашивает: "О чем ты, тетенька, плачешь?"
  mecho - Я было пытался ее одернуть, да было поздно.
  mecho - Открыла белая баба глаза и говорит:
  mecho - "Отец, купи дочери материи на белое платье" и после того исчезла.
  эм совсем опечалился
  wait 1s
  mecho - Дочка радуется, думает под венец скоро пойдет...
  mecho - А я то знаю, является сама Бела баба перед чьей-то неминуемой смертью.
  mecho - С тех пор потерял покой я...
  mecho - Одна она у нас с женой, помрет - и мы помрем.
  эм смахнул скупую слезу
  wait 1s
  mecho - Вот так-то...
  wait 1s
  mecho - Ходил я к знахарке нашей, той что торгует зельями недалеко от Широкой улицы.
  mecho - Так она говорит можно помочь беде моей, коли найдется смелый воин
  mecho - И не испугается противиться Белой бабе.
  mecho - Может ты поможешь?
  вопрос
  *атач трига помогу боярину
  calcuid nMob3 64044 mob
  attach 64026 %nMob3.id%
else
  wait 1s
  say Неужто испугался ты Белой бабы?
  эм с укором покачал головой
end
~
#64026
ПомогуБоярину~
0 d 0
помогу~
wait 1s
эм рад
mecho - Отлично! 
mecho - Век буду тебе благодарен! 
mecho - И за ценой не постою!
mecho - Что делать я незнаю, сходи к знахарке той, она все подробно раскажет
mecho - И вот еще что...
mecho - Возьми на всякий случай вот эту карту
wait 1s
эм достал перо и начал что-то быстро царапать на клочке бумаги
mecho - Вот держи! 
mecho - Это карта того самого места, где мы с дочкой встретили Белую бабу.
mecho - Надеюсь разберешься
mload obj 64021
дать все %actor.name%
mecho - Теперь можешь идти!
*флаг прохождения первой ступени
%actor.setquest(640201)%
detach 64025 %self.id%
detach 64026 %self.id%
~
#64027
ПринеслиРебенка~
0 j 100
~
if %object.vnum% == 64032
  *
  wait 1s 
  рад
  mecho _Дрожащими руками несчастная мать начала открывать берестяной короб.
  mecho _Из глубины берестяных сплетений донесся детский голосок.
  wait 1s
  if %actor.quested(640105)%
    *награда
    mecho _И через секунду счастливая мать сжимала в объятиях свое чадо. 
    *снимаем флаги
    %actor.unsetquest(640101)% 
    %actor.unsetquest(640102)% 
    %actor.unsetquest(640103)% 
    %actor.unsetquest(640104)%
    %actor.unsetquest(640105)% 
    %actor.unsetquest(640106)% 
    %actor.unsetquest(640107)% 
    %actor.unsetquest(640108)%
    *%actor.unsetquest(640110)% 
    *%actor.unsetquest(640111)% 
    *%actor.unsetquest(640112)% 
    *на данном этапе по квесту 6401 должен остаться один флаг входа на кладбище - 640109
    *который снимается внутри кладбища при входе в него 
    * и 1 из трех флагов рыбного квеста
    wait 1s
    поклон %actor.name%
    mecho - Спасибо тебе, добрый человек за то что не оставил за сердцем мое горе.
    mecho - Прими и от меня подарок в знак вечной тебе признательности!
    эм достала что-то из-за пазухи
    wait 1s
    switch %random.4%
      case 1
        if %random.100% < 40 && %world.curobjs(64051)% < 7
          mload obj 64051
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 2
        if %random.100% < 40 && %world.curobjs(64049)% < 7
          mload obj 64049
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 3
        if %random.100% < 40 && %world.curobjs(64035)% < 7
          mload obj 64035
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 4
        if %random.100% < 40 && %world.curobjs(64036)% < 7
          mload obj 64036
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      default 
      break 
    done
    %purge% %object.id%
    дать все %actor.name%
    * * * * 
  else
    say Но это не мой ребенок!
    вопрос %actor.name%
  end
end
~
#64028
ВошлиКТысяцкому~
0 r 100
~
if !%actor.quested(640301)%
  wait 1s
  эм поднял голову от бумаг
  wait 1s
  say Как раз вовремя ты пришел!
  mecho - Дело есть у меня, вот не знал кому поручить...
  mecho - А ты, вижу как раз сгодишься!
  wait 1s
  mecho - Недавече как вчера, попала ко мне бумага одна
  mecho - Говорится там, что стали пропадать люди в нашем городе.
  эм задумчиво почесал бороду
  wait 1s
  mecho - И пропадают все как один ночью темной!
  mecho - И говаривают видели их потом посреди леса в зверинах шкурах!
  эм перекрестился
  wait 1s
  mecho - Но это может мужикам со страху померещилось
  mecho - А вот то что людей мы недосчитываемся каждый день, то истинная правда!
  mecho - Вот те крест!
  эм перекрестился с еще большей страстью
  wait 1s
  mecho - Сдается мне что дело тут нечисто.
  mecho - А уж коли нечисто, то наверняка о том ведает наша ведунья
  mecho - Конечно, не любит ее народ, ну а услугами ее пользуемся.
  mecho - Что тут поделаешь?
  взд
  mecho - Разобрался бы ты с этим? 
  mecho - А у меня и других делов навалом - через край лезут!
  эм начал воинственно размахивать бумагами
  wait 1s
  mecho - Ну что, поможешь?
  вопрос
  *атач трига помогу 
  calcuid nMob3 64022 mob
  attach 64029 %nMob3.id%
else
  wait 1s
  say Неужто испугался ты?
  эм с укором покачал головой
end
~
#64029
ПомогуТысяцкому~
0 d 0
помогу~
wait 1s
кив
mecho - Отлично! 
mecho - Как справишься, сразу приходи ко мне, выдам заслуженную награду!
mecho - И за ценой не постою!
эм похлопал по толстому кошельку, висящему  на поясе
wait 1s
mecho - Начинать поиск тебе следует с ведуньи.
mecho - Глядишь поможет чем, или слово нужное скажет...
mecho - Теперь можешь идти!
*флаг прохождения первой ступени
%actor.setquest(640301)%
detach 64028 %self.id%
detach 64029 %self.id%
~
#64030
тест2~
0 j 100
~
*тестовый тригер для экстремального снятия флагов
*если подключен значит забыла убрать 
wait 1s
%actor.setquest(64001)%
%echo%  27015 %actor.quested(64001)%
%actor.unsetquest(64001)%
%echo% 270105 %actor.quested(64001)%
***
%actor.setquest(27011)%
%echo%  27015 %actor.quested(27011)%
%actor.unsetquest(27011)%
%echo% 270105 %actor.quested(27011)% 
***
%actor.setquest(27010)%
%echo%  27015 %actor.quested(27010)%
%actor.unsetquest(27010)%
%echo% 270105 %actor.quested(27010)% 
***
%actor.setquest(640101)%
%echo%  27015 %actor.quested(640101)%
%actor.unsetquest(640101)%
%echo% 270105 %actor.quested(640101)% 
*** 
%actor.setquest(640102)%
%echo%  27015 %actor.quested(640102)%
%actor.unsetquest(640102)%
%echo% 270105 %actor.quested(640102)% 
***
%actor.setquest(640103)%
%echo%  27015 %actor.quested(640103)%
%actor.unsetquest(640103)%
%echo% 270105 %actor.quested(640103)% 
***
%actor.setquest(640104)%
%echo%  27015 %actor.quested(640104)%
%actor.unsetquest(640104)%
%echo% 270105 %actor.quested(640104)% 
***
%actor.setquest(640105)%
%echo%  27015 %actor.quested(640105)%
%actor.unsetquest(640105)%
%echo% 270105 %actor.quested(640105)% 
***
%actor.setquest(640106)%
%echo%  27015 %actor.quested(640106)%
%actor.unsetquest(640106)%
%echo% 270105 %actor.quested(640106)%
***
%actor.setquest(640107)%
%echo%  27015 %actor.quested(640107)%
%actor.unsetquest(640107)%
%echo% 270105 %actor.quested(640107)%
***
%actor.setquest(640108)%
%echo%  27015 %actor.quested(640108)%
%actor.unsetquest(640105)%
%echo% 270105 %actor.quested(640108)%
***
%actor.setquest(640110)%
%echo%  27015 %actor.quested(640110)%
%actor.unsetquest(640105)%
%echo% 270105 %actor.quested(640110)%
***
%actor.setquest(640111)%
%echo%  27015 %actor.quested(640111)%
%actor.unsetquest(640105)%
%echo% 270105 %actor.quested(640111)%
***
%actor.setquest(640112)%
%echo%  27015 %actor.quested(640112)%
%actor.unsetquest(640105)%
%echo% 270105 %actor.quested(640112)%
~
#64031
ПодобралиАдамовуГолову~
1 g 100
~
if %actor.quested(640301)% 
  %send% %actor% _Вы потянулись к траве и сорвали ее протащив сквозь серебренную цепочку.
  %echoaround% %actor% %actor.rname% потянулся к траве и сорва%actor.q% ее протащив сквозь серебренную цепочку. 
  oload mob 62813
  oload mob 62813
  oload mob 62813
  if %actor.haveobj(64067)%
    *если в лапах цепочка
    %actor.setquest(640303)%
  end
  detach 64031 %self.id%
  return 1
else
  %send% %actor% _Только Вы прикоснулись к траве, как огненная стена накрыла Вас.
  %echoaround% %actor% %actor.rname% потянулся к траве и тут же с воем отскочи%actor.q% от нее
  set nHit 100
  odamage %actor% %nHit%
  return 0
end
~
#64032
ВошлиПодГремячьюБашню~
2 e 100
~
*если вошел имеюий билет на свадьбу
wait 1s 
if %actor.quested(640109)% 
  *лоад черта
  if !%exist.mob(64045)% 
    wload mob 64045
  end
  wecho _Внезапно налетел вихрь и так ж внезапно исчез, оставив вместо себя вихрь-черта.
  wait 1s
  wforce черт эм подозрительно посмотрел вокруг
  wait 1s
  wforce черт say Ты кто есть то?
  wforce черт say Уж не на свадьбу лешего намылился?
end
~
#64033
ДаЧерту~
0 d 100
да~
if %actor.quested(640109)%
  wait 1s
  дум
  wait 1s
  say Что-то не видел я раньше тебя!
  mecho - И больно уж ты на человека похож!
  mecho - Нука давай показывай копыта, а то душу выну!
  эм угрожающе выставил вперед свои рога
  wait 1s
  %send% %actor% Вдруг сами того не ожидая, Вы начали демонстрировать черту ноги.
  %send% %actor% Но какое же было Ваше изумление, когда вместо ступней Вы увидели копыта!
  %send% %actor% Теперь до Вас дошло наконец, что за заклятие совершил леший.
  %send% %actor% Конечно, открытие радовало, но одновременно и пугало.
  %send% %actor% Нехороший озноб гонял по спине мурашки...
  %echoaround% %actor% Вдруг %actor.name% нача%actor.q% демонстрировать черту копыта.
  wait 1s
  %send% %actor% Черт внимательно сомотрел Ваши копыта.
  %echoaround% %actor% Черт внимательно осмотрел копыта %actor.rname%
  wait 1s
  mecho - Теперь вижу, наш ты!
  mecho - Добро пожаловать на свадьбу Яги и Лешего!    
  эм поклнился в ноги(копыта?)
  wait 1s
  mecho - Надеюсь подарок то успел подготовить?
  mecho - А то негоже на свадьбу-то да без подарка!  
  ухм 
  wait 1s
  mecho - И смотри выйти с кладбища так просто нельзя!
  mecho - Коль колдовать не умеешь дашь эту грамоту пекленцу, он сворожит что-нить.
  mecho - Если спьяну слова не забудет!
  *сделано для того чтобы 100 проц снять флаг 640109
  wait 1s 
  хих
  mload obj 64063
  дать грамота %actor.name%
  wait 1s
  mecho - Все! Проходи! Да не задерживайся!
  эм махнул на запад   
  *открываем проход н запад на 3 секунды* 
  calcuid nRoom 64081 room
  attach 64040 %nRoom.id% 
  exec 64040 %nRoom.id% 
  wait 6s
  mpurge %self.id% 
else
  wait 1s
  say - А ты кто такой?! Смерть тебе будет наградой!
  wait 1s
  атак %actor.name%
end 
~
#64034
ЛетитЯгаНаСвадьбу~
2 e 100
~
wait 1s
wecho _&CТо ни ветрушки шумят буйные,&n
wait 1s
wecho _&CТо ни на море разыгралась непогодушка,&n
wait 1s
wecho _&CТо летит в ступе Баба-Яга Костяная Нога!&n
wait 1s
wecho _&CЛетит пестом железным погоняет!&n
wait 1s
wecho _&CПомелом след заметает!&n
wait 1s
wecho _&CЗубами звериными клацает!&n
wait 1s
wecho _&CКостьми белыми бряцает!&n
if !%exist.mob(64055)%
  wload mob 64055
end
detach 64034 %self.id%
~
#64035
ЧертСчитаетГостей~
0 r 100
~
wait 1s
эм послюнявил копыто и перевернул страницу огромной книги, которую держал в руках.
mecho - Тааааак...
mecho - Посчитаем-с, все ли гости прибыли...
switch %random.8%
  case 1
    wait 1s
    mecho - Русалки-утопленницы - 3 штуки
    mecho _Откуда-то сверху донесся переливчатый смех
    mecho - Тут они!
    эм что-то начертал в своей амбарной книге
  break
  *
  case 2
    mecho - Болотники с болотницами - 2 пары.
    mecho _Внезапно твердая земля вокруг порылась пупырышками и начала булькать
    кив
    mecho - Вижу!
    эм заскрипел сорочьим пером по бересте
  break
  *
  case 3 
    mecho - Черти! Аж 8 штук! Прям табун!
    mecho _Откуда ни возьмись появилось восемь чертей.
    mecho _Их хвосты были задранны вверх, не хуже, чем у мартовских котов.
    mecho - А ну брысь отсюда, нечистая!
    эм ткнул пером в берестяной лист
  break
  *
  case 4 
    mecho - Волколаки - 3 штуки!
    mecho _Донесшееся с запада хриплое рычание говорило само за себя.
    mecho - И эти тут - так и запишем!
    эм старательно вывел черный крестик на белой поверхности
  break
  *
  case 5 
    mecho - Колдун-чернокнижник - одна штука
    mecho - Ну этот точно придет, можно и к бабке не ходить
    эм поставил очередной крестик на бересте
  break
  *
  case 6 
    mecho - Какой-то %actor.name%!
    эм крепко задумался
    %send% %actor% _Вдруг заметил Ваше присутствие.
    %echoaround% %actor% Вдруг заметил присутствие %actor.rname%. 
    mecho - А, это ты? Тогда вычеркиваю!
    эм поставил жирный крест в почти бухгалтерской на вид книге 
  break
  *
  case 7 
    mecho - Остался Пекленец.
    эм вспомнил что Пекленец с самого утра идол Кощного Бога подпирает
    кив
    эм начертил красивый крестик рядом с именем Пекленца
  break
  *
  case 8 
    mecho - А сами молодые...
    эм оглянулся в поисках Лешего с Ягой
    mecho - Что-то не видать...
  break
  *
done 
~
#64036
РезетЗоны~
2 f 100
~
*вернуть триг летит яга
calcuid nRoomYaga 64093 room
attach 64034 %nRoomYaga.id%
*квест 6401
calcuid nMobG 64042 mob
attach 64023 %nMobG.id% 
*квест 6402
calcuid nMobB 64044 mob
attach 64025 %nMobB.id%  
*квест 6403
calcuid nMobT 64022 mob
attach 64028 %nMobT.id%  
*вернуть триг птицелова
calcuid nRoomP 64038 room
attach 64049 %nRoomP.id%
*возвращаем триг подпрка лешему (квест 6401)
calcuid nMobL 64056 mob
attach 64037 %nMobL.id% 
*восстановление тригеров видим нечисть
wait 1
calcuid nR1 64006 room
attach 64057 %nR1%
calcuid nR2 64014 room
attach 64057 %nR2%
calcuid nR3 64016 room
attach 64057 %nR3%
wait 1
calcuid nR4 64017 room
attach 64057 %nR4%
calcuid nR5 64019 room
attach 64057 %nR5%
calcuid nR6 64057 room
attach 64057 %nR6%
wait 1
calcuid nR7 64070 room
attach 64057 %nR7%
calcuid nR8 64044 room
attach 64057 %nR8%
calcuid nR9 64002 room
attach 64057 %nR9%
~
#64037
ДалиЧто-тоЛешему~
0 j 100
~
if %actor.quested(640109)%
  wait 1s
  *неважно что даем
  say Аааа... Вот и ты пришел!
  mecho - Помню, помню как ты меня выручил!
  mecho - И за подарок спасибо, что не забыл!
  ул
  wait 1s
  mecho - Теперь моя очередь тебе подарки дарить!
  хи
  wait 1s
  *награда
  switch %random.4%
    case 1
      if %random.100% < 60 && %world.curobjs(64052)% < 8
        mload obj 64052
      else 
        %self.gold(+5000)%
        дать 5000 кун %actor.name%
      end 
    break
    case 2
      if %random.100% < 90 && %world.curobjs(64053)% < 8
        mload obj 64053
      else 
        %self.gold(+5000)%
        дать 5000 кун %actor.name%
      end 
    break
    case 3
      if %random.100% < 90 && %world.curobjs(64054)% < 8
        mload obj 64054
      else 
        %self.gold(+5000)%
        дать 5000 кун %actor.name%
      end 
    break
    case 4
      if %random.100% < 690 && %world.curobjs(64055)% < 8
        mload obj 64055
      else 
        %self.gold(+5000)%
        дать 5000 кун %actor.name%
      end 
    break
    default 
    break 
  done
  %purge% %object.id%
  дать все %actor.name%
  detach 64037 %self%
  * * * * 
else
  say А ты кто таков?
  хму
end
~
#64038
ВошлиККупцу~
0 r 100
~
*если лошадка на руме значит пришел квестер 6281
*mecho ------------0 
if %exist.mob(62807)% 
  *mecho ------------1
  calcuid lmob 62807 mob 
  *сравниваем с румой что перед купцом 
  *потому что когда срабатывает триг лошадка еще не вошла на руму
  if %lmob.realroom% == 64064
    wait 1s
    *mecho ------------2
    %send% %actor% _Стоявший за прилавком купец, увидев Вас, чуть не выпрыгнул навстречу.
    %echoaround% %actor% _Стоявший за прилавком купец, чуть не выпрыгнул навстречу %actor.rname%.
    wait 1s
    mecho _Лицо черниговского купца приняло сияюще-слащавое выражение.
    wait 1s
    mecho - Здравия тебе путник и божьего расположения!
    mecho - А куда это ты рыбку везешь?
    mecho - Может продашь мне ее? 
    mecho - Али поменяемся? Много у меня вещей есть хороших!
    mecho - Ну как, хочешь?
  end
end
~
#64039
ХочуКупцу~
0 d 0
хочу да~
if %exist.mob(62807)% 
  calcuid lmob 62807 mob 
  if %lmob.realroom% == 64068
    *
    wait 1s
    эм потер ручки, хищно поглядывая в сторону повозки с рыбой
    wait 1s
    mecho - Отлично!
    switch %random.4%
      case 1
        if %random.100% < 10 && %world.curobjs(64033)% < 10
          mecho - Вот тебе за твою рыбу воротник жемчугом саженый.
          mecho - Любой бабе по душе будет!
          mload obj 64033
        else 
          mecho - Что-то рыба твоя лежалая! Глядишь вот-вот испортится!
          mecho - Вот моя цена за нее, больше не дам!
          %self.gold(+5000)%
          дать 5000 кун %actor.name%
        end 
      break
      case 2
        if %random.100% < 10 && %world.curobjs(64034)% < 10
          mecho - Вот тебе за рыбу твою поясочек шерстяной.
          mecho - В любую даже самую лютую зиму согреет!
          mload obj 64034
        else 
          mecho - Что-то рыба твоя лежалая! Глядишь вот-вот испортится!
          mecho - Вот моя цена за нее, больше не дам!
          %self.gold(+5000)%
          дать 5000 кун %actor.name%
        end 
      break
      case 3
        if %random.100% < 10 && %world.curobjs(64037)% < 7
          mecho - Вот тебе за рыбу твою кафтан заморский.
          mecho - Кто оденет - вмиг щеголем прослывет!
          mload obj 64037
        else 
          mecho - Что-то рыба твоя лежалая! Глядишь вот-вот испортится!
          mecho - Вот моя цена за нее, больше не дам!
          %self.gold(+5000)%
          дать 5000 кун %actor.name%
        end 
      break
      case 4
        if %random.100% < 10 && %world.curobjs(64040)% < 10
          mecho - Вот тебе за рыбу твою белы порты княжеские.
          mecho - Прямо с княжьего двора мне их продали!
          mload obj 64040
        else 
          mecho - Что-то рыба твоя лежалая! Глядишь вот-вот испортится!
          mecho - Вот моя цена за нее, больше не дам!
          %self.gold(+5000)%
          дать 5000 кун %actor.name%
        end 
      break
      default 
      break 
    done
    дать объекттрг39 %actor.name%
    mpurge %lmob.id%
  end
end
~
#64040
ВходНаКладбище~
2 z 0
~
wdoor 64081 west room 64082
wait 5s
wdoor 64081 west purge
~
#64041
УмерлаЯга~
0 f 0
~
if %random.100% < 9 && %world.curobjs(64056)% < 7
  mload obj 64056
end 
if %random.100% < 8 && %world.curobjs(64060)% < 10
  mload obj 64060
end 
~
#64042
УмерВихрьЧерт~
0 f 0
~
if %random.100% < 9 && %world.curobjs(64057)% < 10
  mload obj 64057
end 
~
#64043
УмерЧерт~
0 f 0
~
if %random.100% < 9 && %world.curobjs(64058)% < 10
  mload obj 64058
end 
~
#64044
УмерПекленец~
0 f 0
~
if %random.100% < 9 && %world.curobjs(64059)% < 10
  mload obj 64059
end 
~
#64045
УмерлаРусалка~
0 f 0
~
if %random.100% < 9 && %world.curobjs(64061)% < 10
  mload obj 64061
end 
~
#64046
УмерлаБолотница~
0 f 0
~
if %random.100% < 9 && %world.curobjs(64062)% < 10
  mload obj 64062
end 
~
#64047
ДалиГрамотуПекленцу~
0 j 100
~
if %object.vnum% == 64063
  *снимаем флаги
  %actor.unsetquest(640109)%
  %actor.unsetquest(640110)%
  %actor.unsetquest(640111)%
  %actor.unsetquest(640112)%
  *телепорт наружу
  wait 1s 
  mecho _Пекленец страшно наморщил лоб что-то вспоминая.
  wait 3s 
  mecho _Пекленец взмахнул своим жезлом и Вы оказались неизвестно где...
  mteleport all 64061 
  wait 3s 
end
~
#64048
РусалкаЕстРыбу~
0 r 70
~
wait 1s
mecho _Глаза русалки-утопленницы жадно разгорелись синим огнем.
mecho _Русалка схватила из корыта самого большого карпа и хищно запустила в него зубы.
wait 1s
mecho _Причмокивая от удовольствия, русалка вмиг растерзала карпа, оставив одни косточки.
wait 1s
*если мороженная рыба
if %actor.quested(640110)%
  mecho _Вдруг русалка закашлялась чем-то поперхнувшись.
  mecho _Истошно воя утопленница вытащила из пасти маленький крестик, 
  mecho _Который видимо когда-то проглотил златоперый карп. 
  if %random.100% < 90 && %world.curobjs(640614)% < 22
    mload obj 64064
  end 
  wait 1s
  %send% %actor% Вдруг русалка злобно взглянула на Вас.
  %echoaround% %actor% Вдруг русалка злобно посмотрела на %actor.rname%
  say А, это ты хотел нас отравить, подложив крест!
  mecho - Поэтому ты умрешь!
  атак %actor.name%
end
*если ловили сами
if %actor.quested(640111)% 
  mecho _Довольно облизнув губы русалка выкинула кости.
  mload obj 64065
  бро все
end 
*если ловил отец витко  
if %actor.quested(640112)% 
  mecho _Довольно облизнув губы русалка аккуратно положила кости за пазуху.
end 
detach 64048 %self.id%
~
#64049
ВошлиПодГлухуюБашню~
2 e 100
~
*если вошел квестер 3031
if %actor.quested(303101)% 
  *лоад молодого птицелова
  if !%exist.mob(64054)% 
    calcuid r1 64039 room
    attach 64050 %r1.id% 
    exec 64050 %r1.id% 
    detach 64050 %r1.id% 
  end
  wait 1s
  wecho _Внезапно тишина разорвалась нестерпимым гомоном тысячи птиц.
  wecho _Вы растерянно оглянулись в поисках источника звука.
  detach 64049 %self.id%
end
~
#64050
ЛоадМолодогоПтицелова~
2 z 100
~
wload mob 64054
~
#64051
ВошлиКПтицелову~
0 r 100
~
if %actor.quested(303101)%
  wait 1s
  эм пыхтя залез на лестницу с огромной клеткой в руках
  wait 1s
  эм кряхтя и ругаясь кое-как подвесил пищащую и свистящую клетку к потолку.
  wait 1s
  %send% %actor% Наконец птицелов заметил Ваше присутвие.
  %echoaround% %actor% Наконец птицелов заметил присутсвие %actor.rname%.
  wait 1s
  хму
  wait 1s
  say Тебе чего тут надо?
  say Я тут выполняю важное задание местного боярина!
  say Так что иди своей дорогой, покуда худа не вышло!
  wait 1s
  mecho _Огромная клеть с треском начала падать с непрочного крюка.
  wait 1s
  эм ухватил падающую клеть и пыхтя продолжил попытки подвесить ее к потолку башни.
end
~
#64052
ОткрылиКлетьСПтицами~
1 p 100
~
wait 1s
%send% %actor% Серебрянный ключ скользнул в замочную скважину, отпирая замок.
%send% %actor% Вы распахнули дверь огромной клети.
%echoaround% %actor% %actor.name% распахнул дверь отгромной клети.
wait 1s
oecho _Тут же зашелестел воздух крыльями тысяч птиц.
oecho _Все вылетали и вылетали из тесной клети птички-невелички.
oecho _Здесь были и воробьи и овсянки, поползни и клесты
wait 1s
oecho _Поднялись они стаей и улетели к своим родным краям.
%actor.setquest(303103)%
detach 64052 %self.id%
~
#64053
УмерПтицелов~
0 f 0
~
mload obj 64066
~
#64054
ПринеслиКлокВолосБоярину~
0 j 100
~
if %object.vnum% == 62804
  *
  wait 1s 
  рад
  mecho _Боярин повертел в руках клок седых волос, почесал задумчиво бороду.
  say Да, будет этот седой волос хорошим оберегом моей дочке до самой смерти.
  wait 1s
  if %actor.quested(640201)%
    *награда
    mecho - А вот и награда твоя за службу хорошую!
    *снимаем флаги
    %actor.unsetquest(640201)% 
    %actor.unsetquest(640202)%
    %actor.unsetquest(640203)%
    *3031
    %actor.unsetquest(303101)% 
    %actor.unsetquest(303103)% 
    wait 1s
    mecho - Прими и от меня подарок в знак вечной тебе признательности!
    эм достал что-то из-за пазухи
    wait 1s
    switch %random.5%
      case 1
        if %random.100% < 40 && %world.curobjs(64041)% < 10
          mload obj 64041
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 2
        if %random.100% < 40 && %world.curobjs(64042)% < 10
          mload obj 64042
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 3
        if %random.100% < 40 && %world.curobjs(64043)% < 10
          mload obj 64043
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 4
        if %random.100% < 40 && %world.curobjs(64045)% < 10
          mload obj 64045
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 5
        if %random.100% < 40 && %world.curobjs(64046)% < 10
          mload obj 64046
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      default 
      break 
    done
    %purge% %object.id%
    дать все %actor.name%
    * * * * 
  else
    say За оберег для дочки, конечно, спасибо, но ведь не ты его добыл!
    укор %actor.name%
    %purge% %object.id%
  end
end
~
#64055
ПринеслиАдамовуГолову~
0 j 100
~
if %object.vnum% == 64022
  wait 1s
  эм повертела в руках траву, понюхала.
  wait 1s
  if %actor.quested(640303)%
    say Хорошая травушка и собрана верно. Молодец!
    wait 1s
    эм начала растирать адамову траву в деревянной ступе
    wait 1s
    эм взяла с верхней полки черный порошок и высыпала в ступку
    wait 1s
    эм плюнула на четыре стороны света
    wait 1s
    %send% %actor% Внезапно ведунья дунула на порошок, который полетел Вам прямо в лицо.
    %send% %actor% От неожиданности Вы глубоко вздохнули и тут же понесла кровушка по венам адамову траву.
    %echoaround% %actor% Внезапно ведунья дунула на порошок, который полетел %actor.rname% прямо в лицо. 
    %echoaround% %actor% %actor.name% глубоко вздохнул и нача%actor.q% дышать тяжело и громко.
    wait 1s
    say Вот теперь ты будешь видеть оборотней, да всяких нечистых что между людей бродят.
    %actor.setquest(640304)%
    mecho - Учти, что они не знают что видишь ты их.
    mecho - И многих кого будешь видеть не трогай, а то беду накликаешь.
    mecho - Ищи ерестуна-оборотня - узнаешь его по волчьим клыкам.
    mecho - Как узнаешь, так сразу убей, пока он подмогу не скликал!
    mecho - Как убьешь, так вынь из него клыки волчьи и отдай тому кто просил тебя помочь.
    mecho - А то ведь не поверит, что это не человек вовсе... а зверь лютый.
    wait 1s
    mecho - Ну, теперь иди! С Богом...
    mecho - И помни, любой житель города может быть оборотнем!
  else
    say Хорошая травушка, да собрал ты ее неверно!
    say Говорила же я тебе, через серебряну цепочку срывать надо!
    взд
    wait 1s
    say Иди вновь за травой, ничего не поделаешь...
  end
  %purge% %object.id%
end
~
#64056
ПревращениеВНечистуСилу~
0 r 100
~
*превращаемся в нечистую силу
if %actor.quested(640304)% 
  switch %random.2%
    case 1
      if %random.100% < 30 
        *двоедушник
        mtransform 64061
      end
    break
    case 2
      if %random.100% < 30 
        *ерестун-оборотень
        mtransform 64062
        *атач трига лоада клыков
        attach 64059 %self.id%
      end
    break
    default 
    break 
  done
end
~
#64057
ВидимНечисть~
2 e 100
~
if %actor.quested(640304)% 
  switch %random.3%
    case 1
      if %random.100% < 30 
        wload mob 64058
      end
    break
    case 2
      if %random.100% < 30 
        wload mob 64059
      end
    break
    case 3
      if %random.100% < 30 
        wload mob 64060
      end
    break
    default 
    break 
  done
  detach 64057 %self%
end
~
#64058
ПринеслиВолчьюЧелюсть~
0 j 100
~
if %object.vnum% == 64068
  if %actor.quested(640304)%
    wait 1s
    эм взял волчью челюсть в руки - дивится...
    wait 1s
    say Как же удалось тебе такую зверюгу-то убить?
    эм покачал головой
    *снимаем флаги
    %actor.unsetquest(640301)% 
    %actor.unsetquest(640302)%
    %actor.unsetquest(640303)%
    %actor.unsetquest(640304)%
    wait 1s
    mecho - Ведь так и знал, что нечисто тут дело!
    mecho - Ну да ладно!
    эм положил челюсть на дальнюю полку
    wait 1s
    mecho - Хорошую службу сослужи%actor.q% ты, не грех и наградить!
    эм полез в какой-то ящик
    wait 1s
    switch %random.6%
      case 1
        if %random.100% < 50 && %world.curobjs(64038)% < 10
          mload obj 64038
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 2
        if %random.100% < 50 && %world.curobjs(64039)% < 10
          mload obj 64039
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 3
        if %random.100% < 50 && %world.curobjs(64044)% < 10
          mload obj 64044
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 4
        if %random.100% < 50 && %world.curobjs(64048)% < 10
          mload obj 64048
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 5
        if %random.100% < 50 && %world.curobjs(64047)% < 10
          mload obj 64047
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      case 6
        if %random.100% < 50 && %world.curobjs(64050)% < 10
          mload obj 64050
        else 
          %self.gold(+10000)%
          дать 10000 кун %actor.name%
        end 
      break
      default 
      break 
    done
    %purge% %object.id%
    дать все %actor.name%
  else
    wait 1s
    say Это мне зачем?
    хму
    брос все
  end
end
~
#64059
УмерЕрестун~
0 f 0
~
mload obj 64068
~
#64060
СамоуничтожениеПредметов24123~
1 b 100
~
*тригер сделан для удаления квестовых объектов, которые используются но мобам не отдаются
*во избежании повтороного их использования игроками
*В виду того что так и не получилось удалить именно нужную мне копию предмета (а не первую
*попавшуюся котрую выдает calcuid/makeuid) поэтому сделана постоянная проверка на наличие флагов у владельца предмета
*как только персонаж проходит определенную ступень, объект пуржится примерно в течении 13 секунд 
*триг мне не нравится тем что неоправдано грузит проверками каждые 13 сек своего существования
*но с другой стороны если его не будет, то проходя квест повторно квестовые предметы добытые
*потом и кровью в зонах будет использованы повторно соотв. без пота и крови..) непорядок
*а пуржить их как подобает с репопом зоны нельзя, потому что квесты глобальные 
*вобщем пока иного выхода не вижу
* 
* смотрим владельца
if %self.carried_by%
  *если есть владелец получаем UID
  set i %self.carried_by%
  if %i.quested(640203)%
    *если видим флаг 640203 (разрезали ножом ткань) однозначно удяляем и плевать на остльные флаги
    %send% %i% _Оставшиеся клочья белого атласа вспыхнули у Вас в руках белым пламенем, и исчезли.&n
    %echoaround% %i% &WОставшиеся клочья белого атласа вспыхнули у %i.rname% в руках белым пламенем, и исчезли&n 
    %purge% %self.id%
    halt
  end
  ***
  *Если владелец моб не делаем ничего!
  if %i.vnum% != -1
    halt
  end
  if !%i.quested(303103)% && !%i.quested(640106)%
    *если не выполняется в нужных местах квести 6401 и 3031 - удаляем
    *квест 6402  тут не учитывать! если выполняется этот квест у игрока все равно 
    * будет стоять флаг 303103 пока либо не сдаст яге, либо не сдаст квест боярину.
    *3031 часть 6402! * 
    %send% %i% _Вдруг, появилась возмущенная мышь и отобрала у Вас нечестно-добытую ткань.&n
    %echoaround% %i% &WВдруг, появилась возмущенная мышь и отобрала у %i.rname% нечестно-добытую ткань.&n
    *%echo% Вдруг, появилась возмущенная мышь и отобрала у Вас нечестно-добытую ткань.
    %purge% %self.id%
  end
else
  *если нет владельца удаляем
  %echo% _&WВдруг, откуда не возьмись, появилась мышка и утащила ненужную никому ткань к себе в норку.&n
  %purge% %self.id%
end
* безуспешные попытки сделать список из UID разных копий одного и того же предмета 
* с разными владельцами
*set z %world.curobjs(64068)% 
*calcuid r1 64068 obj
*while %z%>0
*makeuid u %r1.id%
**eval u %world.obj(64068)%
*set m %u.carried_by%
*%echo% -- %z% --- %u.name% +++ %m.name%
*eval z %z%-1
*done 
*eval f %pc%
*foreach p %self.%
*%echo% %p.name%
*done
~
#64061
ВходимКНечисти~
0 r 100
~
if !%actor.quested(640304)%
  *
  wait 1s
  %purge% %self.id%
end
~
#64062
СамоуничтожениеПредметов40029~
1 b 100
~
* смотрим владельца
if %self.carried_by%
  *если есть владелец получаем UID
  set i %self.carried_by%
  if %i.vnum% != -1
    halt
  end
  if %i.quested(640203)%
    *если видим флаг 640203 (разрезали ножом ткань) однозначно удяляем 
    %send% %i% _Нож с изображением Семаргла вспыхнул у Вас в руках белым пламенем, и исчез.&n
    %echoaround% %i% &_Нож с изображением Семаргла вспыхнул у %i.rname% в руках белым пламенем, и исчез.&n 
    %purge% %self.id%
    halt
  end
  *если не выполняется квест 6402 - удаляем
  if !%i.quested(640201)% 
    %send% %i% _Нож с изображением Семаргла вдруг нагрелся и исчез.&n
    %echoaround% %i% &WНож с изображением Семаргла вдруг нагрелся у %i.rname% в руках, и исчез.&n 
    %purge% %self.id%
  end
else
  *если нет владельца удаляем
  %echo% _Вдруг, откуда не возьмись, появилась мышка и утащила ненужный никому нож к себе в норку.&n
  %purge% %self.id%
end
~
#64063
СамоуничтожениеПредметов64067~
1 b 100
~
* смотрим владельца
if %self.carried_by%
  *если есть владелец получаем UID
  set i %self.carried_by%
  if %i.vnum% != -1
    halt
  end
  if %i.quested(640303)%
    *если видим флаг 64303 сорвали траву через цепочку
    %send% %i% _Серебряная цепочка с кулоном вспыхнула у Вас в руках белым пламенем, и исчезла.&n
    %echoaround% %actor% &WНСеребряная цепочка с кулоном вспыхнула у %i.rname% в руках белым пламенем, и исчезла&n 
    %purge% %self.id%
    halt
  end
  *если не выполняется квест 6403
  if !%i.quested(640301)% 
    %send% %i% _Серебрянная цепочка вдруг нагрелась и исчезла.&n
    %echoaround% %i% _Серебрянная цепочка вдруг нагрелась у %i.rname% в руках, и исчезла.&n 
    %purge% %self.id%
  end 
else
  *если нет владельца удаляем
  %echo% _Вдруг, откуда не возьмись, появилась мышка и утащила ненужную никому цепочку к себе в норку.&n
  %purge% %self.id%
end
~
#64064
СамоуничтожениеПредметов30319~
1 b 100
~
* смотрим владельца
if %self.carried_by%
  *если есть владелец получаем UID
  set i %self.carried_by%
  if %i.vnum% != -1
    halt
  end
  if %i.quested(303103)%
    *если видим флаг 303103 открывали клеть
    %send% %i% _Серебряный ключ вспыхнул у Вас в руках белым пламенем, и исчез.&n
    %echoaround% %i% &WСеребряный ключ вспыхнул у %i.rname% в руках белым пламенем, и исчез&n 
    %purge% %self.id%
    halt
  end
  *если 3031 не ывполняется
  if !%i.quested(303101)% 
    %send% %i% _Серебряный ключ вдруг нагрелся и исчез.&n
    %echoaround% %i% _Серебряный ключ вдруг нагрелся у %i.rname% в руках, и исчез.&n 
    %purge% %self.id%
  end 
else
  *если нет владельца удаляем
  %echo% _Вдруг, откуда не возьмись, появилась мышка и утащила ненужный никому ключ к себе в норку.&n
  %purge% %self.id%
end
~
#64065
СнимаемФлагиКвеста6401~
2 e 100
~
*снимаем флаги
%actor.unsetquest(640109)%
%actor.unsetquest(640110)%
%actor.unsetquest(640111)%
%actor.unsetquest(640112)%
~
$~
