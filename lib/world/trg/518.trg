#51800
приветствие добродушного старичка~
0 q 100
~
wait 1s
say Здрово, храбрец.
wait 1s
кашл
wait 1s
say Беда у меня приключилась не ахти какая... Только вот самому справиться мочи нет.
say Поможешь ли ты старику ? Смотри только на смех не подыми от случая со мною...
~
#51801
помогаем добродушному старичку~
0 d 1
помогу~
wait 1s
say Спасибо тебе, ну слушай же что со мною приключилось.
wait 1s
кашл
wait 1s
шепт .%actor.iname% Старенький я уже, еле на ногах держусь, но подсказали мне люди
шепт .%actor.iname% К святому отцу Димитрию обратиться, чтобы смастерил он мне посох 
шепт .%actor.iname% дорожный. И правда, опираюсь на посох этот и сразу будто на двадцать
шепт .%actor.iname% зим моложе стал.
wait 1s
шепт .%actor.iname% Так вот, частенько я бываю на полянке этой, спокойно тут, но вот
шепт .%actor.iname% сегодня беда со мною приключилась. Задремал я слегка, а как очнулся
шепт .%actor.iname% - нету посоха, все глаза изглядел, а все равно нету.
wait 1s
шепт .%actor.iname% И тут слышу хихикает тихонько кто-то...
шепт .%actor.iname% Никак малышня у меня сонного посох выкрала да и припрятала.. я бы
шепт .%actor.iname% и сам их поймал, да стар я и слаб уже... помоги ты мне, сделай милость.
wait 1s
шепт .%actor.iname% Слыхал я шалаш они где-то в лесу соорудили, небось там и прячутся.
wait 1s
шепт .%actor.iname% Да, только ты им оружием грозить не взумай, а ежели надобно будет, так
шепт .%actor.iname% сорви деревце молоденькое, да и отшлепай их посильней, чтоб неповадно было!
wait 1s
шепт .%actor.iname% Дети они еще, что с них взять.
~
#51802
мальчики и девочки собирают ягодки~
0 b 30
~
if %self.fighting%
  halt
end
switch %random.10%
  case 1
    mecho %self.iname% ловко нагнул%self.u% и вытянул%self.g% из травы ягоду земляники.
    ул
    mecho Немного повертев ягодку в руках %self.iname% ее проглотил%self.g%.
  break
  case 2
    mecho %self.iname% ловко нагнул%self.u% и вытянул%self.g% из травы ягоду черники.
    ул
    mecho Немного повертев ягодку в руках %self.iname% ее проглотил%self.g%.
  break
  case 3
    mecho %self.iname% ловко нагнул%self.u% и вытянул%self.g% из травы красивый цветочек.
    ул
    mecho %self.iname% понюхал%self.g% цветок, и, похоже, потерял%self.g% к нему всякий интерес.
  done
~
#51803
сорвать ягодку с куста земляники~
1 c 4
сорвать~
switch %random.3%
  case 1
    osend %actor% Вы наклонились и сорвали ягодку с куста земляники. 
    oload obj 51804
    oechoaround %actor% %actor.iname% наклонил%actor.u% и сорвал%actor.g% ягодку с куста земляники.
  break
  case 2
    osend %actor% Вы наклонились и сорвали две ягодки с куста земляники. 
    oload obj 51804
    oload obj 51804
    oechoaround %actor% %actor.iname% наклонил%actor.u% и сорвал%actor.g% две ягодки с куста земляники.
  break
  case 3
    osend %actor% Вы наклонились и сорвали три ягодки с куста земляники. 
    oload obj 51804
    oload obj 51804
    oload obj 51804
    oechoaround %actor% %actor.iname% наклонил%actor.u% и сорвал%actor.g% три ягодки с куста земляники.
  break 
done
wait 1
opurge %self%
~
#51804
сорвать ягодку с куста черники~
1 c 4
сорвать~
switch %random.3%
  case 1
    osend %actor% Вы наклонились и сорвали ягодку с куста черники. 
    oload obj 51805
    oechoaround %actor% %actor.iname% наклонил%actor.u% и сорвал%actor.g% ягодку с куста черники.
  break
  case 2
    osend %actor% Вы наклонились и сорвали две ягодки с куста черники. 
    oload obj 51805
    oload obj 51805
    oechoaround %actor% %actor.iname% наклонил%actor.u% и сорвал%actor.g% две ягодки с куста черники.
  break
  case 3
    osend %actor% Вы наклонились и сорвали три ягодки с куста черники. 
    oload obj 51805
    oload obj 51805
    oload obj 51805
    oechoaround %actor% %actor.iname% наклонил%actor.u% и сорвал%actor.g% три ягодки с куста черники.
  break 
done
wait 1
opurge %self%
~
#51805
пытаются залезть в шалаш~
2 c 1
лезть залезть~
wait 1
wsend %actor% Вы уже слишком большой чтобы шалаш выдержал Вас.
wechoaround %actor% %actor.iname% скептически поглядел%actor.g% на шалаш, и передумал%actor.g% туда лезть.
~
#51806
зашли в руму шалаша~
2 e 100
~
wait 1s
wecho Откуда-то сверху донесся "грозный" детский голос:
wait 1s
wecho -Я Соловей-разбойник, а ты кто ??? 
wecho -Убирайся отсюда, или твоя голова украсит мое жилище!
wait 1s
detach 51806 %self.id%
~
#51807
репоп~
2 f 100
~
calcuid shal 51860 room
detach 51806 %shal.id%
detach 51808 %shal.id%
attach 51806 %shal.id%
attach 51808 %shal.id%
calcuid star 51802 mob
detach 51800 %star.id%
detach 51801 %star.id%
attach 51800 %star.id%
attach 51801 %star.id%
calcuid kol 51885 room
detach 51822 %kol.id%
attach 51822 %kol.id%
calcuid krest 51803 mob
detach 51811 %krest.id%
attach 51811 %krest.id%
calcuid tier 51818 mob
wteleport %tier% 51891
calcuid zvon 51816 mob
exec 51826 %zvon.id%
~
#51808
трясти березку~
2 c 1
трясти ударить~
wait 1
wsend %actor% Вы изо всей силы растрясли березку и ...
wechoaround %actor% %actor.iname% изо всей силы растряс%actor.q% березку и...
wecho сверху, дико визжа, свалился озорной карапуз.
wait 1s
wload mob 51804
wecho Озорной карапуз сказал : 'я тебя не боюсь!'
detach 51808 %self.id%
~
#51809
шлепаем карапуза~
0 c 1
шлепать наказать отшлепать~
wait 1s
if !%arg.contains(карапуза)%
  msend %actor% И кого-же вы собрались наказать ?
  halt
end
if !%actor.haveobj(51806)%
  msend %actor% Вы бы с радостью, но хоть бы веточку какую нашли...
  halt
end
msend %actor% Вы ловко схватили мальчишку и изо всех сил три раза ударили его осиновой веткой!
mechoaround %actor% %actor.iname% ловко схватил%actor.a% мальчишку и изо всех сил три раза ударил%actor.a% его осиновой веткой!
wait 1s
mecho Карапуз заревел во всю мочь, быстро вырвался, бросив на землю посох.
mload obj 51807
брос посох
з
wait 2
крич Ты злой! Я все маме расскажу!
wait 1
mpurge %self%
~
#51810
сдали посох старичку~
0 j 100
~
wait 1
if %object.vnum% != 51807
  дум
  say Это мне зачем еще?        
  бросить %object.name%
  halt
end
wait 1
mpurge %object%
log &CСдали посох старику - %actor.name%&n
wait 1s
say О, мой посох! Спасибо огромное, но помочь мне тебе особенно нечем, вот, возьми немного денег.
%actor.gold(+2000)%
if %random.4% == 1 
  say Ах да, держи вот еще записку к святому отцу Димитрию...
  say Вдруг и тебе такой посох понадобится...
  mload obj 51808
  дать запи .%actor.name%
  брос запи
end
detach 51800 %self.id%
detach 51801 %self.id%
~
#51811
зашли к сердитому крестьянину~
0 q 100
~
wait 1s
хму
wait 1s
mecho Сердитый крестьянин, бормоча что-то про себя полез в кусты и выломал небольшую веточку.
wait 1s
say Ну я ему сейчас покажу!
wait 1s
say Ах, вот ты где!!!
mecho Сердитый крестьянин схватил ветку и бросился на Вас!
mload obj 51806
воор вет
атак .%actor.iname%
detach 51811 %self.id%
~
#51812
подпоясались крепкой веревкой~
1 j 100
~
oechoaround %actor% %actor.iname% подпоясал%actor.u% крепкой веревкой.
osend %actor% Вы подпоясались крепкой веревкой.
~
#51813
зашли к отцу Димитрию~
0 q 100
~
wait 1s
say Здорово, %actor.iname%.
wait 1s
say Бедна стала наша церковь, мало нам князь черниговский выделяет.
say Совсем так скоро зачахнем, да и колокольню починить не на что...
wait 1s
say Помоги нам, ежели есть чем.
say А коли нет, так не всем же богатыми быть, сохрани веру в сердце своем.
~
#51814
сдали записку отцу Димитрию~
0 j 100
~
wait 1
if %object.vnum% != 51808
  say Зачем мне это, лучше бы кун немного... колоколенку починить...
  брос все
  halt
end
wait 1
mpurge %object%
wait 1s
mecho Святой отец быстро пробежал записку глазами и задумчиво погладил бороду.
if %world.curobjs(51812)% < 7
  mload obj 51812
  дать посох .%actor.iname%
  брос посох
else
  say Извини, %actor.name%, нет у меня сейчас того, что тебе требуется.
end
~
#51815
дали пожертвование отцу Димитрию~
0 m 1
~
if !%self.fighting%
  wait 1s
  *if %amount% < 100
  say Спасибо тебе, сын мой, хоть и немного, зато от чистого сердца пожертвование твое.
  *halt
  *end
  halt
  if %amount% < 500
    dg_cast 'освя' %actor.iname%
    msend %actor.name% Святой отец Димитрий слегка подмигнул Вам и Вы почувствовали его благословление
    say Спасибо тебе, сын мой, хоть и немного, зато от чистого сердца пожертвование твое.
    halt
  elseif %amount% > 499
    dg_cast 'освя' %actor.iname%
    msend %actor.name% Святой отец Димитрий слегка подмигнул Вам и Вы почувствовали его благословление
    dg_cast 'исц' %actor.iname%
    msend %actor.name% Святой отец Димитрий выпрямился, развел руки - и Вы почувствовали себя снова здоровым.
    say Спасибо тебе, сын мой, Да хранит тебя Господь.
    halt
  end
end
~
#51816
нечто зашло к леснику~
0 h 100
~
wait 1s
say Ходют.. тут всякие
wait 1s
хму
wait 5s
~
#51817
зашли к почтенному купцу~
0 q 100
~
wait 1s
шля .%actor.iname%
wait 5s
~
#51818
зашли к глуховатому звонарю~
0 q 100
~
wait 1
if %direction% == west
  wait 5s
  встать
  mload obj 51813
  закр две з
  запер две з
  mpurge ключ
  halt
end
wait 1s
скуч
wait 1s
say Эх-х-х... Теперь никому я и не нужен уже... сижу от безделья будто на 20 лет постарел.
wait 1s
say Ежели есть тебе чем помочь - зайди в церковь, святому отцу Димитрию пожертвование вручи.
say Ведает он магию святую, не скупись, и отблагодарит он тебя чем сумеет.
wait 1s
say А в колокольне сейчас и делать нечего, лестница сломана...
say Хотя, если хочешь могу пустить тебя, погляди на позорище.
~
#51819
просим пустить в колокольню~
0 d 1
хочу можно да согласен~
wait 1s
switch %random.7%
  case 1
    msend %actor% Звонарь наклонился к Вам поближе, пытаясь расслышать, что же Вы говорите такое.
    mechoaround %actor% Звонарь наклонился к %actor.dname% поближе, пытаясь расслышать, что же он%actor.g% говорит такое.
  break
  case 2
    say Кхе-Кхе, Вы уж меня извините ради бога, старый стал, ничегошеньки не слышу.
  break
  case 3
    say Чаво говорите ?
  break
  case 4
    дум
    say Да ничего поживаю, спасибочки, а Вы как ?
  break
  case 5
    дум
    say Ну не хотите - как хотите
  break
  case 6
    mecho %self.iname% безразлично смотрит по сторонам, посвистывая
  break
  case 7
    say Ну проходи, только наверх не лезь, упадешь - никто твоих костей не соберет!
    wait 1s
    встать
    mload obj 51813
    отпер две з
    mpurge ключ
    откр две з
    *wait 120s
    *закр две з
    *wait 1s
    *запер две з
  done
~
#51820
падаем со сломанной лестницы колокольни~
2 g 100
~
wait 1
if %random.7% == 1
  wechoaround %actor% %actor.name% поскользнул%actor.u% и камнем полетел%actor.g% вниз.
  wsend %actor% Вы поскользнулись и камнем рухнули на голый дощатый пол колокольни.
  wteleport %actor% 51883
  wechoaround %actor% %actor.name% с криком упал%actor.u% на дощатый пол прямо перед Вами..
  eval    dam  %actor.hitp%
  wsend %actor% Глаза застилает кровь... Вы не в силах пошевелиться, а можете только ждать когда вернутся силы..
  wdamage %actor% %dam%
end
~
#51821
приветствие монаха-отшельника~
0 q 100
~
wait 1s
дум
wait 1s
mecho %self.iname% Зачерпнул воды из лесного ручейка и задумчиво пропустил ее между пальцев.
if %actor.level% > 22
  say Наверное знаешь ты легенду о Чёрном Кургане, да Черниговском Звере...
  wait 1s
  say Не врет молва, просыпается он... не может найти покоя... В такое то время и
  say Проливается кровь невиновных, кто на том кургане побывает...
  wait 1s
  say Долго размышлял я, что же заставляет его выползать на Свет Божий
  say Не переносит он звука колоколов... Слава богу, давно уже замолкли колокола Ильинской церкви
  wait 1s
  say И покоится он с миром, ибо не сыскалось еще воина, способного его победить...
  wait 30s
else
  say Размышляю вот сижу, но молод%actor.g% ты еще, чтобы со мной мои заботы разделить...
  wait 10s
end
~
#51822
пытаемся звонить в колокола~
2 c 1
звонить позвонить~
wait 1s
wsend %actor% Вы попытались позвонить в колокола, но запутались в веревках и, пытаясь выпутаться, породили ужасающий грохот.
wechoaround %actor% %actor.iname% попылал%actor.u% позвонить в колокол, но запутал%actor.u% в веревках и, пытаясь выпутаться, породил%actor.g% ужасающий грохот.
%actor.wait(5)%
wait 1s
wzoneecho 51800 &W"Бум-Бом-Бом" - донесся до Вас нестройный звон колоколов Ильинской церкви
wait 1s
wzoneecho 51800 &W"Бум-Бом-Бом" - донесся до Вас нестройный звон колоколов Ильинской церкви
wait 1s
wzoneecho 51800 &W"Бум-Бом-Бом" - донесся до Вас нестройный звон колоколов Ильинской церкви
wsend %actor% Наконец-то Вы выпутались и ужасающий звон прекратился.
calcuid tier 51818 mob
wteleport %tier% 51889
wechoaround %tier% Со страшным ревом из под земли вылезло чудище со змеиным хвостом.
detach 51822 %self.id%
~
#51823
черниговский зверь машет хвостом~
0 k 20
~
wait 1
%mecho%      &RЧерниговский зверь широко взмахнул хвостом, пытаясь Вас напугать.&n
~
#51824
умер черниговский зверь~
0 f 20
~
mload obj 51814
switch %random.10%
  case 1
    mload obj 1755
  break
  default
  done
~
#51825
сдали зуб отшельнику~
0 j 100
~
wait 1
if %object.vnum% != 51814
  say Я всего лишь бедный отшельник, мне это не нужно.
  брос все
  halt
end
wait 1
mpurge %object%
wait 1s
say Да-а-а... не ожидал я, что простой смертный сможет убить этакое чудище
say Я беден, могу только передать тебе часть своих знаний...
%actor.exp(+70000)%
msend %actor% %self.iname% что-то шепнул Вам на ушко, вы даже и не запомнили толком, но почувствовали себя намного опытнее...
if ((%random.100% < 11) && (%world.curobjs(3334)% < 2))
  wait 1s
  mload obj  3334
  say Да и возьми еще это колечко, и откуда оно у меня взялось? Пригодится тебе наверное.
  дать коль .%actor.name%
end
~
#51826
лоад ключа~
0 z 100
~
mload obj 51813
~
#51827
Нельзя снять сапоги-скороходы~
1 l 100
~
return 0
osend %actor% Во дают люди - на бегу сапоги снимают!
~
#51828
Сапоги ресторят мувы~
1 b 100
~
set target %self.worn_by%
if %target%
  set buf %target.move(%target.maxmove%)%
end
~
#51829
Надели сапоги скороходы~
1 j 100
~
wait 1
osend %actor% Вы надели сапоги-скороходы. Эх, хорошо!
osend %actor% Вдруг ваши ноги сами собой понесли вас бегом!!!
oechoaround %actor% %actor.name% надел%actor.g% сапоги-скороходы и понесся вдаль!
set victim %actor%
global victim
attach 51830 %self.id%
run 51830 %self.id%
~
#51830
Побежаааали!~
1 z 100
~
if !%victim%
  halt
end
set target %self.worn_by%
if !%target%
  halt
end
if (%target.position% < 5)
  oforce %target% wake
end
if (%target.position% < 7)
  oforce %target% stand
end
set i 0
while 1
  if (%i% == 49)
    set i 0
    wait 4s
    oechoaround %target% %target.name% ????c%target.u% ?????!
    osend %target% ??????-c???????? c??? c???? ????c?? ??c ??????!
  end
  eval i %i%+1
  switch %random.6%
    case 1
      oforce %target% up
    break
    case 2
      oforce %target% down
    break
    case 3
      oforce %target% north
    break
    case 4
      oforce %target% east
    break
    case 5
      oforce %target% south
    break
    case 6
      oforce %target% west
    break
  done
done
~
$~
