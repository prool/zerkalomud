#63700
Старый воин приветствует~
0 q 100
~
if %questor637.name% != %nil%
  halt
end 
wait 1s
вопрос %actor.iname%
wait 1s
say Что тебе тут нужно?
say Проваливай подобру-поздорову, покуда цел%actor.g%!
wait 10s
вздох
say Ладно.
say Слышь, как тебя... постой не уходи.
emot прикоснулся к повязке на руке и поморщился
wait 2s
say Беда в городе нашем приключилась - мор черный идет.
say Никого не щадит. Будь ты стар иль мал - все одно.
say Уж и хоронить не успеваем.
say Там городок-то - за лесом заболоченным да речкой.
emot махнул рукой куда-то на север
wait 3s
say Раньше мы здесь жили - недалече, пока не завелась тут нечисть лихая.
say Кладбище тут было священное, и склеп, где когда-то похоронили великого князя, что и мой род основал, и город.
say Предание есть у нас, что в годину лихую любой из его рода может придти и совета просить у его духа.
say И может статься - получит и совет и помощь...
wait 3s 
взд
say Вот и пошел я сюда - последний я в роду этом остался...
say Да не осилил и полдороги - сам видишь, покалечился... а дальше и вовсе лихо будет.
say Вот и сижу теперь... И дальше идти силы нет, и домой вернуться - того страшней.
say Как в глаза-то родичам и соплеменникам гляну?
say А потом лишь и останется, что сидеть да кончины ждать от черной хвори.
wait 2s
emot обхватил голову руками и глухо застонал в отчаянии
*прямой подсказки нету - не захочет помогать - не надо :) 
attach 63701 %self.id% 
~
#63701
Старый воин дает задание~
0 d 100
помогу~
if (%quest637.name% = %nil%)
  eval quest637 %actor%
  global quest637
  remote quest637 %world.mob(63701)%
  wait 1s
  emot поднял голову и посмотрел на Вас
  wait 1s
  г Поможешь... Чем? Сам%actor.g% туда отправишься?
  г Думаешь, предки мои слушать тебя станут? 
  дум
  wait 2s
  say Вот что.... Дам я тебе свой амулет.
  say В давние времена сделал его колдун - тоже из нашего рода выходец.
  say Амулет этот хранит от наговоров и магии злой.
  wait 2s
  say Может и признают тебя...
  wait 3
  mload obj 63726
  give амулет %actor.name%%
  wait 1s
  say Вот дорога, по которой дальше идти надобно
  stand
  emot раскидал кучу хлама в дальнем углу хижины
  mecho _Вы увидели небольшую дверцу, ведущую на восток.
  mdoor 63704 e room 63771
  wait 3
  rest
  say Удачи тебе...
  ждать
  detach 63700 %self.id%
  attach 63720 %self.id%
  detach 63701 %self.id% 
else
  say Приходили тут и до тебя люди... ушли и не вернулись.
  say Боюсь - сгинули... не могу я еще и тебя об этом просить.
end
~
#63702
Старый воин принимает задание (2)~
0 d 0
снадобье лекарство зелье ~
wait 1
tell Пандора %actor.name% вызвал триг в дороге предков!
tell свентовит %actor.name% вызвал триг в дороге предков!
if (%actor.id% != %quest637.id%)
  halt
end
wait 5
emot Внимательно посмотрел на мешок с травами и подозрительно принюхался.
say Значит, эти травы могут исцелить тот недуг, что обрушился на наш город?
wait 1s
say Спасибо тебе....
поклон %actor.name%
say Как мне отблагодарить тебя?
switch %random.20%
break
case 1
  mecho _Старый воин достал из ножен меч.
  wait 1s
  г Вот возьми, этот меч достался еще мне от моего деда, а ему от его деда.
  г Ты наверно знаешь, что даже за пределами Руси, русские мечи славятся своей прочностью и красотой.
  wait 1s
  mload obj 63702
  дать меч %actor.iname%
break
case 2
  mecho _Старый воин снял с себя прекрасные золоченые латы.
  wait 1s
  mload obj 63703
  дать латы %actor.iname%
  г Эти прекрасные латы, спасут тебя, от мечей да топоров врагов твоих.
break
case 3
  mecho _Старый воин снял с пальцо кольцо.
  wait 1s
  mload obj 63704
  дать коль %actor.iname%
  г Это старинное кольцо, досталось мне за подвиги ратные да дела великие, возьми его в награду, не откажи. 
break
case 4
  mecho _Старый воин снял с себя амулет.
  wait 1s
  mload obj 63705
  дать амулет %actor.iname%
  г Это старинный амулет, достался мне за подвиги ратные да дела великие, возьми его в награду, не откажи. 
break
case 5
  mecho _Старый воин снял с себя прекрасный шлем.
  wait 1s
  mload obj 63706
  дать шлем %actor.iname%
  г Это старинный русский шлем, не одна стрела не потревожит тебя с ним.
break
case 6
  mecho _Старый воин достал из-за плеч огромную секиру.
  wait 1s
  mload obj 63707
  дать секи %actor.iname%
  г Это старинная русская секира, да не одну вражью голову срубила она на своем веку.
break
default 
  mecho _Старый воин достал небольшой мешок денег.
  wait 1s
  mload obj 63701
  дать дене  %actor.iname%
  г Это все, что скопил я за свою жизнь, возьми, тебе пригодится. 
break 
done
detach 63702 %self.id%
~
#63703
Верховный предок приветствует~
0 r 100
~
if (%actor.vnum% != -1)
  halt
end
wait 1s               
гнев
г Как ты смог%actor.q% попаcть суда ?
г Как ты прош%actor.y% через мою стражу ?
хму
if (%actor.id% != %quest637.id%)
  halt
end
wait 1s
msend %actor% _Вы достали амулет из черного камня и показали его призраку.
mechoaround %actor% _%actor.name% достал%actor.g% амулет и показал%actor.g% его призраку.
mecho _Призрак слегка подался назад, отстранившись от амулета.
wait 3s
г Понятно...
г Пришел, значит, помощи просить... у предков...
wait 1s
г А какие такие заслуги твои, чтобы я тебе помогал?
хох
wait 1s
say Вот что - я тебя испытаю... Выполнишь три моих приказа.
say Справишься - помогу я тебе.
say Ну а коли не справишься - так с покойника не спрашивают, да и помощи они редко просят.
ухм
wait 1s
say Ну что - готов%actor.g%?
calcuid delete_trig 63701 mob
detach 63703 %delete_trig.id%
~
#63704
Верховный предок дает 1 задание~
0 d 1
готов готова да согласен согласна~
wait 1s  
г Вот тебе первое испытание.
г Есть тут в лесу место странное
г Живут там непонятные даже мне существа, и правит ими неведомо кто.
wait 2s
г Убей их всех, да что нибудь в доказательство принеси.
г Сейчас я перенесу тебя в их обиталище, да смотри с пустыми руками не возвращайся!
wait 3s 
mecho _Призрак взмахнул рукой и вас перенесло неведомо куда.
mteleport all 63720
calcuid delete_trig 63701 mob
detach 63704 %delete_trig.id% 
~
#63705
Предок награда за 1 задание и дает 2 задание~
0 j 100
~
if (%object.vnum% !=  63708)
  say Что это ты мне принес?
  halt
end
wait 1s
mpurge %object%
г Да, ты справился с этим заданием.
г Долго эти твари нам жизнь портили.
хих
wait 1s
г Ну...  помощь ты пока не заслужил.. а вот награду небольшую - это да.
ухм
wait 2s
г За то, что освободил меня от мерзости этой, на получи в подарок.
switch %random.10%
break
case 1
  mecho _Призрак взмахнул рукой и перед вами, вдруг, появились прекрасного вида латы.
  wait 1s
  г Вот возьми, за свою помощь.
  wait 1s
  mload obj 63709
  дать лат %actor.iname%
break
case 2
  mecho _призрак взмахнул рукой и перед вами, вдруг, появился прекрасного вида амулет.
  wait 1s
  mload obj 63710
  дать амул %actor.iname%
  г Вот возьми, за свою помощь.
break
case 3
  mecho _Призрак взмахнул рукой и перед вами, вдруг, появилось прекрасного вида кольцо.
  wait 1s
  mload obj 63711
  дать коль %actor.iname%
  г Вот возьми, за свою помощь.
break
default 
  mecho _Призрак взмахнул рукой и перед вами появился небольшой сундучек, полностью набитый драгоценностями.
  wait 1s
  mload obj 63712
  дать сун  %actor.iname%
  г Вот возьми, за свою помощь. 
break 
done
wait 3s  
г С первым заданием ты справился, вот тебе второе. 
wait 1 
г Я знаю есть леса далекие, а в них болота глубокие.
wait 1
г И живут там звери лютые, звери страшные, звери ужасные.
wait 1
г И прошла молва, что в лесах тех растет дерево, а на дерево плод прекрасный.
wait 1
г Вот принеси мне его, а там видно будет, может и помогу тебе.
wait 4s 
mecho _Призрак взмахнул рукой и вас перенесло неведомо куда.
mteleport all 63737
detach 63705 %self.id%
end
~
#63706
Телепорт из пустоши к предку~
2 e 100
~
wait 1
wportal 63769 2
wecho Яркая вспышка ослепила Вас и в дальнем углу возник большой портал.
~
#63707
Умер верховный семекрыл~
0 f 100
~
msend %actor.name% Верховный семикрыл, что-то пискнул на прощание и, выронив из лап странный камень, отправился в дальний путь.
mechoaround %actor.name% %actor.iname% порубал эту тварь на маленькие кусочки.
mload obj 63708
~
#63708
Телепорт из леса к предку~
2 e 100
~
wait 1
wportal 63769 2
wecho Яркая вспышка ослепила Вас и в дальнем углу возник большой портал.
~
#63709
Срываем плод~
1 c 100
сорвать~
if  !%arg.contains(плод)%
  osend %actor.iname% _Что вы хотите сорвать?!
  halt
end                                              
oechoaround %actor.iname% _%actor.iname% подпрыгнул и сбил прекрасный плод с ветки.
osend %actor.iname% _Вы подпрыгнули и сбили плод с ветки.
oload obj 63716
oforce %actor% взять плод
opurge %self%       
~
#63710
Предок награда за 2 задание и дает 3 задание~
0 j 100
~
if (%object.vnum% == 63716) 
  wait 1s
  mpurge %object.name%
  г Хм... справил%actor.u%, значит?
  wait 1s
  г Ну молодец...
  ухм
  wait 2s
  г Но, к твоему сожалению, я решил, что ты не получишь то, что просишь.
  wait 1s
  say Потому как ошиб%actor.u% ты немного.
  emot злобно ощерился
  say Мор этот, защиты от которого ты явил%actor.u% просить я же и вызвал!
  mecho _-Ибо мой род всегда с твоим враждовал.. да и сгинул во времена давние в этой вражде...
  mecho _-А теперь и вам я погибель принесу, не пощажу никого!
  mecho _-А твои кости сгниют - и никто не узнает, где ты смерть наш%actor.q%!
  хохот
  mecho _-Призрак взмахнул рукой и вас перенесло неведомо куда!
  mteleport all 63705 horse
  wait 1
  calcuid delete_trig 63701 mob
  mload mob 63712
  mpurge %self%
  detach 63710 %delete_trig.id%
end
~
#63711
Телепорт из залов предков к старику~
2 z 100
~
wait 1
wportal 63703 2
wecho В воздухе возникла яркая вспышка и в дальнем углу возник большой портал.
~
#63712
Умер темный призрак~
0 f 100
~
msend %actor.name% _Призрак умер уже окончательно и бесповоротно.
mechoaround %actor.name% %actor.iname% добил умирающий призрак.
mecho _Тьма в комнате слегка рассеялась. 
mecho _Вы вдруг заметили скрытую за троном маленькую арку, ведущую в темноту.
mdoor 63769 e room 63770
mdoor 63770 w room 63769
~
#63713
Репоп~
2 f 100
~
*detach 63701 %deletetrig.id% 
*detach 63702 %deletetrig.id%
*detach 63720 %deletetrig.id%
*attach 63700 %deletetrig.id%
wdoor 63769 e purge
wdoor 63770 w purge
wdoor 63792 n purge
*calcuid deletetrig 63701 mob
*attach 63703 %deletetrig.id%
*attach 63704 %deletetrig.id%
*attach 63705 %deletetrig.id%
*attach 63710 %deletetrig.id%
*calcuid deletetrig 63704 mob  
*attach 63707 %deletetrig.id%
*calcuid delete_trig 63712 mob
*attach 63712 %deletetrig.id%
*calcuid deletetrig 63715 obj
*attach 63709 %deletetrig.id%
calcuid crypt 63761 room
detach 63721 %crypt.id%
attach 63721 %crypt.id%
calcuid monstrum 63718 mob
detach 63729 %monstrum.id%
detach 63731 %monstrum.id%
attach 63729 %monstrum.id%
~
#63714
вооружаемся головой духа~
1 j 0
~
osend %actor% _Вы ухватили голову болотного духа за волосья и пару раз взмахнули ею.
oechoaround %actor% _%actor.name% ухватил%actor.g% голову болотного духа за волосы и пару раз со свистом взмахнул%actor.g% ею.
~
#63715
берут голову духа~
1 g 0
~
if %actor.int% < 21 
  return 0
  osend %actor% _Вы попытались взять голову болотного духа.
  oechoaround %actor% _%actor.name% попытал%actor.u% взять голову болотного духа.
  oecho _Внезапно голова открыла глаза и щелкнула зубами!
  osend %actor% _Вы едва успели отдернуть руку.
  oechoaround %actor% _%actor.name% резко отдернул%actor.g% руку, попятил%actor.u% и едва не шлепнул%actor.u% наземь.
end
~
#63716
убил болотного духа~
0 f 100
~
if ((%world.curobjs(63717)% < 4) && (%random.1000% <= 40))
  mload obj 63717
end
~
#63717
надели глаз лесного духа~
1 j 0
~
oecho _Глаз лесного духа вспыхнул багровым сиянием.
oechoaround %actor% _Вы почувствовали, как этот странный свет завроживает Вас и отнимает волю.
~
#63718
валим народ с обрыва~
2 e 100
~
wait 1
if (%direction% != west)
  halt
end
wait 1
wecho _Вы тихонько крадетесь по тропе... 
wecho ...конечно, ведь вы бывалый воин, и знаете, что не стоит зря шуметь...
if (%actor.affect(полет)% == 1)
  wsend %actor% _Толстая ветка хлестнула вас по лицу и вы шлепнулись на землю!
  wechoaround %actor% _Толстая ветка хлестнула по %actor.name% лицу и он%actor.g% шлепнул%actor.u% на землю!
end
wsend %actor% _Вы неловко шагнули и ваша нога подвернулась на камнях.
wechoaround %actor% _%actor.name% неловко шагнул%actor.g%, и покатил вниз по каменной осыпи.
wecho _Вы покачнулись, пытаясь сохранить равновесие...
wecho            ....покатитилсиь по камням куда-то в темноту!
wteleport all 63776
wat 63776 wecho Грох... !
wteleport all 63776
wat 63776 wecho Бум... !
wteleport all 63776
wat 63776 wecho Тарарах... !
wat 63776 wecho Ой, мама... !!!
wait 4
run 63719 %world.room(63776)%
~
#63719
сообщение упавшим с обрыва~
2 z 0
~
wecho Мда... Будем надеятся, никто этого не видел и не слышал...
wecho А то засмеют...
wecho До смерти...
~
#63720
дали мешок воину~
0 j 100
~
if (%actor.id% != %quest637.id%)
  say Это что еще? Зачем ты мне это принес%actor.g%?
  eval objectname %object.name%
  halt
end
say Хм... это еще что?
say Чем мне ЭТО помочь может?
вопр %actor.name%
if (%object.vnum% == 63700)
  attach 63702 %self.id%
  detach 63720 %self.id%
  mpurge %object.name%
end
drop %objectname.car%.%objectname.cdr%
~
#63721
входят в склеп~
2 e 100
~
return 0
wsend %actor% Вы шагнули к входу в подземелье.
wechoaround %actor% _%actor.name% шагнул%actor.g% ко входу в подземелье.
wechoaround %actor% _Внезапно статуи воинов зашевелились и отделились от стен!
wechoaround %actor% _Мрачные воины в полном боевом облачении предстали перед Вами.
wsend %actor% _Внезапно статуи воинов зашевелились и отделились от стен!
wsend %actor% _Мрачные воины в полном боевом облачениии предстали перед Вами.
wload mob 63716
wload mob 63716
wteleport каменный.страж 63775
wteleport каменный.страж 63775
detach 63721 %self.id%
~
#63722
убили каменного стража~
0 f 100
*~
if (%world.curobjs(63730)% < 5) && (%random.10% <= 2)
  mload obj 63730
end
if (%world.curobjs(63731)% < 5) && (%random.10% <= 2)
  mload obj 63731
end
~
#63723
ткач создает туманных воинов~
0 b 5
~
halt
if ((%self.position% == 7) || (%self.position% == 1))
  halt
end
if (%random.2% == 1)
  if (%world.curmob(63702)% < 5)
    mecho _~~%self.name% вдруг быстро зашевелил лапами...
    mecho _...~~%self.name% создал туманного воина!
    mload mob 63702
  end
else
  if (%world.curmob(63703)% < 5)
    mecho _~~%self.name% вдруг быстро зашевелил лапами...
    mecho _...~~%self.name% создал туманного стража!
    mload mob 63703
  end
end
~
#63725
загрузочный у статуй~
0 z 100
~
mkill %random.pc%
~
#63727
загрузка призрака ╧2~
0 n 100
~
if ((%random.100% < 5) && (world.curobjs(63720) < 4))
  mload obj 63720
end
if ((%random.100% < 5) && (world.curobjs(63721) < 4))
  mload obj 63721
end
if ((%random.100% < 5) && (world.curobjs(63722) < 4))
  mload obj 63722
end
if ((%random.100% < 5) && (world.curobjs(63723) < 4))
  mload obj 63723
end
if ((%random.100% < 5) && (world.curobjs(63724) < 4))
  mload obj 63724
end
if ((%random.100% < 5) && (world.curobjs(63725) < 4))
  mload obj 63725
end
одеть все
~
#63728
у призрака ╧2~
0 q 100
~
say Опять ты? Ну что ж.. значит сдохнешь тут! А из твоих костей я сделаю дудки! 
detach 63728 %self.id%
~
#63729
вошли к чудо-юду~
0 q 100
~
wait 10s
if (%self.position% == 7)
  halt
end
say Ну хоть кто-то в драку не лезет... 
wait 1s
say А то все норовят - трип, стаб, баш, оглуш, килл....
say Или даже длит холдом не разобравшись... ироды! Я, может, в Красную Книгу занесено!
emot задумчиво посмотрело на развешанные по деревьям связки черепов.
wait 1s
say Хоть бы кто догадался водички поднести, напоить...
emot отвернулось и стало угрюмо рассматривать окружающие деревья
attach 63731 %self.id%
~
#63730
напали на лешего~
0 p 100
~
mecho Леший заорал: ТАК ТЫ ДРАТЬСЯ?!
mecho - А  НУ - ПОШЛИ ВСЕ ВОН ИЗ МОЕГО ЛЕСА!!!!
foreach char %self.pc%
  dg_cast 'слов возв' %char.name% 
done
~
#63731
у чуда-юда ╧2~
0 c 1
поить напоить~
if !%arg.contains(чудо-юдо)%
  msend %actor% _Кого поим-то?
  halt
end
if ((%time.month% > 11) || (%time.month% < 3)) 
  msend %actor% _Вы разбили лед, покрывающий поверхность ручья.
  mechoaround %actor% _%actor.name% разбил %actor.g% лед, покрывающий поверхность ручья.
end
msend %actor% _Вы напоили лесное чудо-юдо.
mechoaround %actor% _%actor.name% напоил%actor.g% лесное чудо-юдо.
say Вот спасибо!
улы
say Прям-таки всем спасибам спасибо...
рад %actor.name%
лиза %actor.name%
eval friend %actor.id%
calcuid leshak 63719 mob
attach 63732 %leshak%
remote friend %leshak%
detach 63731 %self%
~
#63732
вошли к лешему~
0 q 100
~
if (%actor.id% != %friend%)
  хих %actor.name%
end
wait 1s 
say Здоров%actor.g% будь, %actor.name%!
say Ты, никак, из леса выбраться не можешь?
say Ты, видать, человек добрый... помогу я тебе.
emot махнул рукой куда-то на северо-восток.
say Вон туда тебе дороженька...
улы
mecho Деревья расступились и выстроились по сторонам тропки на северо-восток.
mdoor 63792 n room 63775
detach 63732 %self.id%
~
#63733
purge objects~
1 z 0
~
opurge %self%
~
#63734
лоадим камень~
0 f 100
~
mload obj 63733
~
#63736
положить амулет на алтарь~
1 c 2
пожертвовать~
if (%actor.realroom% != 63770)
  return 0
  halt
end
if !%arg.contains(амулет)%
  osend %actor% _Что вы хотите пожертвовать?
  halt
end
if !%arg.contains(алтарь)%
  osend %actor% _Куда вы хотите это положить?
  halt
end
osend %actor% _Вы положили амулет из черного камня на резной алтарь.
oechoaround %actor% _%actor.name% положил%actor.g% амулет из черного камня на резной алтарь.
oecho _Амулет вспыхнул и рассыпался на осколки.
wait 1
oecho _Вы почувствовали чье-то странное присутствие.
wait 3s
oecho _Голос произнес:
oecho - Я приветствую тебя... Да, я знаю , зачем ты явился. 
wait 1
oecho - И хотя ты не моей крови, но ты прош%actor.q% через великие опасности, чтобы помочь моему роду.
oecho - Поэтому я дам тебе ответ. 
wait 1s
oecho - Возьми эти травы из мест, что лежат за пределами нашего мира
oecho - Они способны изгнать проклятую хворь.
wait 1s
oecho - Прощай, и да будет с тобой удача...
oload obj 63700
exec 63711 %world.room(63770)%
wait 1
opurge %self%
~
#63737
бросили камень~
1 h 100
~
if (%actor.realroom% != 63719)
  halt
end
osend %actor% _Вы аккуратно пристроили камень на надлежащее место в пентаграмме.
oechoaround %actor% _%actor.name% швырну%actor.q% здоровую каменюку в пентаграмму.
oecho _Внезапно пентаграмма вспыхнула ослепительным голубым огнем.
exec 63733 %world.obj(63732)%
exec 63738 %world.room(63719)%
wait 1
opurge %self%
~
#63738
портал из тумана в залы предков~
2 z 0
~
wportal 63765 2
~
#63739
load set on master of clouds~
0 f 100
~
mload obj 63733
~
#63740
load set on great sevenwings~
0 f 100
~
if %world.curobjs(3371)% < 2 && %random.10000% < 100
  mload obj 3371
end
~
$~
