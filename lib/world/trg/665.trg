#66500
Староста приветствует~
0 r 100
~
if %questor665.name% != %nil%
  halt
end
wait 1s
г Что, привело тебя к нам, что ты хочешь найти тут ?
вопрос %actor.iname%
г Не чего то у нас нет...
г Была кузня одна на все село, да и то с кузнецом что-то случилось...
г За молот браться не может, видать заболел он хворью невиданной...
wait 2s
дум
wait 1s
say Слушай, раз уж ты здесь, помоги излечить кузнеца.
г А то ведь не дело, село и без кузни.
~
#66501
Староста дает задание~
0 d 100
помогу~
if (%quest665.name% = %nil%)
  set quest665 %actor%
  global quest665
  wait 1s
  взд
  wait 1s
  г Ну так вот, заболел наш кузнец хворью невиданной, ковать ничего не может.
  г Излечи его, помоги нам. 
  wait 2s 
  дум
  г Я знаю, что жил у нас в селе знахарь один, да ушел в лес, и живет там сейчас.
  г Сходи к нему, может, что и подскажет.
  wait 1s
  calcuid for_mb 66500 mob
  detach 66500 %for_mb.id%
  detach 66501 %for_mb.id%
end
~
#66502
У отшельника~
0 r 100
~
wait 1s
усмех
г Зачем ты пришел сюда ?
г Зачем ты отвлекаешь меня от дел ?
хму
wait 2s 
г А я догадываюсь, послал тебя староста ко мне, ведь кузнец у них хворает.
г Наверно хочет, что бы я кузнецу их помог ?
wait 2s     
г Он глуп, не может понять простые вещи...
дум
г А ты заметил, что село опустело? Люди бегут из этих мест!
г А кто еще не убежал, все заболеют, как и кузнец.
wait 6
г Кузнецу я помочь смогу, но всей беды мне не отвести от селения
г Принеси мне травки волшебной, что растет у ручья.
г А ручей тот в лесу где то находится.
г Принесешь, так и быть, сварю я отвар один, он и поможет кузнецу.
wait 8
г А вот как снять проклятье со всей округи я могу лишь догадываться
г Говорят, что где-то в лесу бродит девушка-оборотень,
г Это она насылает на ближайшие деревни болезни и мор.
дум
wait 10
г Если принести ей лекарство, которое избавит ее от превращения в рысь каждую ночь
г То скорее всего она снимет свои заклятья и люди смогут вновь жить здесь. 
дум
wait 10
г Не жди от меня точного ответа - я не знаю, какое лекарство ей поможет.
г Людская молва говорит, что оборотней лечат чесноком... но это лишь слухи.
~
#66503
Отшельник дает отвар~
0 j 100
~
if (%object.vnum% == 66500)
  wait 1
  mpurge %object%
  wait 1s
  г Да это то что надо.
  улыб
  wait 2s
  г Вот...Этот отвар поможет кузнецу.
  усмех
  mload obj 66501
  дать отвар .%actor.name%
  if (%random.1000% <= 150)
    wait 1s
    mecho Отшельник случайно обронил какую-то книгу.
    mload obj 1735
    Брос книг
  end
  detach 66503 %self.id% 
  detach 66502 %self.id%
end
end
~
#66504
Староста принимает задание~
0 j 100
~
if (%actor.name% == %quest665.name%)
  if %object.vnum% != 66501
    wait 1
    drop %object.name%
    halt
  end
  wait 1
  mpurge %object%
  г Ты смог договориться с отшельником.
  г Спасибо тебе %actor.iname%, теперь кузнец наш снова будет при деле.
  wait 1s
  г Но у нас село не богатое, сам видишь...
  г Чем бы тебя отблагодарить ?
  дум 
  wait 1s
  г Вот возьми, у меня есть кусок железа да мешочек денег.
  г Отнеси железо кузнецу, он что нибудь да выкует для тебя.
  г А деньги оставь себе, за помощь.
  wait 1s
  mload obj 66502
  mload obj 66502
  mload obj 66502
  mload obj 66503
  дать кусок .%actor.name%
  дать кусок .%actor.name%
  дать кусок .%actor.name%
  дать мешок .%actor.name%
end
~
#66505
Кузнец делает вещи~
0 j 100
~
wait 1
if %object.vnum% != 66502 
  г На кой мне это?
  wait 2
  брос все
  halt
end
wait 1
mpurge %object%
г Эх, помог ты мне излечиться, а я уж постараюсь выковать для тебя что-нибудь!
switch %random.4%
  case 1
    mecho Кузнец взял кусок железа и начал что-то выковывать из него.
    if ( %world.curobjs(66504)% < 5 ) && ( %random.10% > 7 )
      mload obj 66504
      дать браслетик %actor.iname%
      г Вот, держи. Тебе это пригодится!
    else
      г Эх не вышло.
      взд
      halt
    end
  break
  case 2
    mecho Кузнец взял кусок железа и начал что-то выковывать из него.
    if ( %world.curobjs(66505)% < 5 ) && ( %random.10% > 6 )
      mload obj 66505
      дать меч %actor.iname%
      г Вот, держи. Тебе это пригодится!
    else
      г Эх не вышло.
      взд
      halt
    end
  break
  case 3
    mecho Кузнец взял кусок железа и начал что-то выковывать из него.
    if ( %world.curobjs(66506)% < 5 ) && ( %random.10% > 6 )
      mload obj 66506
      дать нагр %actor.iname%
      г Вот, держи. Тебе это пригодится!
    else
      г Эх не вышло.
      взд
      halt
    end
  break
  default
    mecho Кузнец взял кусок железа и начал что-то выковывать из него.
    wait 1s
    г Эх не вышло.
    взд
    halt
  break
done
~
#66506
Репоп~
2 f 100
~
set quest665 %nil%
global quest665
calcuid delete_trig 66500 mob
attach 66500 %delete_trig.id% 
attach 66501 %delete_trig.id%
attach 66504 %delete_trig.id%
calcuid delete_trig 66501 mob
attach 66502 %delete_trig.id%
attach 66503 %delete_trig.id%
calcuid delete_trig 66502 mob
attach 66505 %delete_trig.id%
~
#66507
дали девушке чеснок~
0 j 100
~
wait 1
if %self.fighting%
  halt
end       
If (%object.vnum%==66508)
  Say Ой! Чеснок! Я так долго его искала!
  эмоц приложила чеснок к ранке на плече и истошный вопль вырвался из ее груди.    
  кри Я свободна! Я здорова! я прощаю людей!
else
  Say Мне это не нужно! Ты издеваешься надо мной!
  kill %actor.name%
  halt
End
mecho Девушка заметно преобразилась, в ее глазах появилась радость.
mpurge чеснок
wait 10
if %self.fighting%
  halt
end
танц
wait 10
if %self.fighting%
  halt
end
say За свое исцеление я вас отблагодарю!  
wait 1
switch %random.5%
  case 1
    mecho _Девушка взяла что-то из дорожной сумы.
    if (%world.curobjs(66509)% > 7)
      mload obj 66510
      дать мешок %actor.iname%
      г Вот, держи. Думала оберег тебе дать, да потеряла.
    else
      mload obj 66509
      дать нить %actor.iname%
      г Вот, держи. Тебе это пригодится!
    end
  break
  case 2
    mecho  Девушка пошарила в дорожной суме.
    г Извини, у меня нет ничего!
  break
  case 3
    mecho _Девушка что-то взяла из дорожной сумы.
    if (%world.curmobs(66507)% > 5) 
      mload obj 66510
      дать мешок %actor.iname%
      г Вот, держи. Думала оберег тебе дать, да потеряла.
    else
      mload obj 66507
      дать когот %actor.iname%
      г Вот, держи. Тебе это пригодится! 
      г Пусть он напоминает тебе о том, что зла людям причинять не нужно!
    end
  break
  default 
    mecho _Девушка что-то взяла из дорожной сумы.
    wait 1s
    mload obj 66510
    дать мешок %actor.iname%
    взд         
  break 
done     
wait 5
if %self.fighting%
  halt
end     
г А теперь прощайте! Мне надо идти..
mpurge девушка
~
#66508
выпадение чеснока~
0 f 100
~
if ((%world.curobjs(66508)% < 1) && (%random.100% <= 20))
  mload obj 66508
end
~
$~
