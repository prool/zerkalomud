#58500
дергаем рычаг~
2 c 0
повернуть дернуть нажать двигать толкать тянуть~
* [триг в комнате недалеко от ворот в лагерь]
* Триг позвляет попасть через ворота в лагерь
wait 1
if %actor.vnum% != -1
  halt
end
if !%arg.contains(рыч)%
  wsend %actor% Что вы хотите повернуть?
  halt
end
wsend %actor% Вы с трудом повернули порядком заржавевший рычаг.
wechoaround %actor.name% %actor.name% повернул%actor.g% заросший рычаг.
wait 1s
wecho Где-то недалеко послышался звук отворяемых ворот.
* создаем сквозной проход между: #58518 () и #58519 ()
wdoor 58518 w room 58519
wdoor 58519 e room 58518
~
#58501
приветствие новобранца~
0 q 100
~
* [триг на мобе, у самого входа в зону]
* Приветственный триг, он аттачит новый триг на моба
wait 1
г А ну стой! Дальше хода нет! Поворачивай, да иди туда откуда пришел! Стой, говорю!
* аттачим триг, позволяющий сказать "г стою":
attach 58502 %self.id%
* детачим текущий триг:
detach 58501 %self.id%
~
#58502
новобранец пугает~
0 d 0
стою~
* [триг на мобе, у самого входа в зону]
* Если сказать "г стою" - моб приаттачит на себя следующий триг, ответив на который откроется вход в зону 
wait 1
г То-то же, а то прет, аки слепой.
wait 1s
г Али не видишь, кто перед тобой стоит? Оказать воинское приветствие нужно.
ворч
wait 2s
г Всему вас желторотиков учить надо.
важн
wait 3s
г Ну что притих? Струхнул поди? Ишь как я тебя испужал!
wait 2s
г Ну да ладно, расслабься, я то, если по-честному не совсем воин...в смысле уже почти...
г осталось, всего несколько испытаний пройти...
wait 2s
г Кстати, коли уж занесла тебя нелегкая в эти края, можешь мне услугу оказать, а я тебя
г отблагодарю, чем смогу.
г Дельце то несложное, одна нога тут - другая там. Сходишь по быстрому и обратно.
wait 2s
г Ну так что? Сходишь?
* аттачим триг, позволяющий сказать "г схожу"
attach 58503 %self.id%
* детачим текущий триг:
detach 58502 %self.id%
~
#58503
новобранец дает квест~
0 d 100
схожу~
* [триг на мобе, у самого входа в зону]
* Если сказать "г схожу" - откроется вход в зону
wait 1
if %actor.vnum% != -1
  halt
end
г Ну и чудненько!
эм деловито потер руки
wait 2s
г Ну ближе к делу!
г Есть в нашем мире враг, которого невозможно победить. Имя этому врагу - время.
г Время не спит и не ест - оно молча ведет свою борьбу, с каждой минутой приближая
г свою победу. Так или иначе, смерть получит тебя, как не убегай.
wait 3s
г Долгое время волшебники Наемной Дружины бились с решением этой вечной задачи,
г но все напрасно. Но все тщетно... ход времени не повернуть, однако, определенные
г результаты были достигнуты.
г Я, конечно, не знаю КАК. Но им удалось создать определенную зону, где время
г остановилось и стрелки обратного отсчета замерли на месте.
wait 3s
г И теперь путник, ты как раз и стоишь на дороге к этой зоне, а точнее к небольшому
г поселению, куда со временем уходят все старые ратники Наемной Дружины.
г Вход туда чужим строго запрещен! И я стою здесь, чтобы это правило выполнялось.
г Это одно из моих испытаний. Если пройду все как положено - быть мне ратником НД.
wait 3s
г Ну а теперь о главном.
г Для того, чтоб гарантировано в Наемную Дружину попасть, необходимо поручительство
г одного из ратников.
г А я тут человек новый, никого не знаю... так кто же за меня поручится?
wait 3s
г Вот то-то и оно - никто.
г Так я, кое-чего придумал. Ступай в лагерь отшельников к главе их, да попроси за меня!
г Всего-лишь &Wскажи &nему о &Wпоручительстве&n
г Ему то что? Что ему? Жалко? Он уже поди и с постели не встает! Авось и вовсе не
г поймет о чем просьба твоя. Поставит тебе галку под поручительством, а мне того и надо!
wait 3s
г С таким то поручителем, мне испытания и проходить не нужно!
г Ну шагай давай, а я отвернусь, вроде как и не встречал тебя, совесть мучает понимаешь ли.
wait 1s
г Тьфу ты, чуть не забыл! Чтоб ворота отпереть, нужно секретный рычаг &Wповернуть&n, где,
г точно не знаю, слыхал, будто где-то недалеко от ворот, авось на месте разберешься.
mecho Молодой охранник открыл путь на юг.
* соединяем две комнаты #58502 () и #58503 ()
mdoor 58502 south room 58503
mdoor 58503 north room 58502
* аттачим триг на сдачу квеста (дать мобу предмет 58500)
attach 58504 %self.id%
* детачим текущий триг
detach 58503 %self.id%
~
#58504
новобранец сдача квеста~
0 j 100
~
* [триг на мобе, у самого входа в зону]
* Если дать мобу предмет 58500, то это будет завершением квеста,
* моб выдаст награду и спуржится.
wait 1
if %object.vnum% == 58500
  say У тебя получилось!? Вот уж не ожидал!!!
  wait 2s
  mecho Молодой охранник с увлечением стал вчитываться в содержание поручительства.
  mpurge поручительст
  wait 7s
  г Лучше и не придумать! Теперь то мне точно не откажут!
  if %world.curobjs(58501)% <3
    if %random.100% < 50
      mload obj 58501
      г Возьми это в награду мне это больше не пригодится!
      дать нашивк .%actor.name%
      г Ну все! Прощай, побегу скорее к воеводе нашему!
    end
  else
    mload obj 58520
    г В благодарность прими немного денег!
    дать все .%actor.name%
    г Ну все! Прощай, побегу скорее к воеводе нашему!
  end
  * пуржим себя (т.е. детачим все триги попутно на мобе)
  mpurge %self%
end
~
#58505
леший варит~
0 j 100
~
* [триг на мобе пьяный колдун, что сидит в трактире за одним из столов]
* Мобу необходимо сдать коготь ягуара, и получить адскую настойку
wait 1
if %object.vnum% == 58519
  mpurge ягуар
  ухм
  say Что? Тоже попробывать хочешь?
  ул
  say Так и быть, сегодня я добрый. На пробу сделаю, но больше не подходи.
  wait 1s
  mecho Волшебник отвернулся и стал, что-то шептать над чаркой с водкой.
  wait 1s
  mecho Наконец волшебник закончил свой обряд и вновь повернулся к Вам лицом.
  wait 1s
  г Вроде как пропорции соблюдены, держи да не злоупотребляй.
  mload obj 58509
  дать все .%actor.name%
else
  г Мне это не пригодиться.
  брос все
end
~
#58506
глава поселения встречает~
0 q 100
~
* [триг на мобе-квестере в лагере, находится в своем личном доме]
* Приветственный триг, аттачит на себя следующий триг
wait 1
if %actor.clan% == нд
  покл
  г Всегда рад приветствовать в своем доме братьев по крови!
  эм лихо сбегал в подвал за четвертиной водки.
  облиз
  г Чем помочь могу?
else
  хму
  г Кто ты путник?
  г Что привело тебя в наши края? Неужто охранять наш лагерь перестали?
  г Нельзя тебе здесь находиться, али не знал?
  гнев
  wait 2s
  г Что молчишь? Говори уж раз пришел, что ищешь?
end
* аттачим триг "г поручительство"
attach 58508 %self.id%
* детач текущего трига перенесем в следующий
* детачим текущий триг
* detach 58506 %self.id%
~
#58507
репоп~
2 f 100
~
* [триг на репоп зоны]
* Очищаем глобальные переменные и закрываем открытые выходы
wait 1
* моб молодой охранник:
if %exist.mob(58500)%
  calcuid molodoy 58500 mob
  attach 58501 %molodoy%
  detach 58502 %molodoy%
  detach 58503 %molodoy%
  detach 58504 %molodoy%
end
* моб глава лагеря:
if %exist.mob(58502)%
  calcuid glava 58502 mob
  rdelete questor585 %glava.id%
  attach 58506 %glava%
  detach 58508 %glava%
  detach 58521 %glava%
end
* моб призрак невинного:
if %exist.mob(58514)%
  calcuid prizrak 58514 mob
  attach 58522 %prizrak%
  detach 58523 %prizrak%
  detach 58524 %prizrak%
  rdelete questor585 %prizrak.id%
end
* моб хранитель трофеев:
if %exist.mob(58501)%
  calcuid hranitel 58501 mob
  attach 58525 %hranitel%
  detach 58526 %hranitel%
  rdelete questor585 %hranitel.id%
end
* моб трактирщик:
if %exist.mob(58504)%
  calcuid traktir 58504 mob
  attach 58527 %traktir%
  rdelete questor585 %traktir.id%
end
* моб богатырь:
if %exist.mob(58503)%
  calcuid btr 58503 mob
  attach 58529 %btr%
  rdelete questor585 %btr.id%
end
* чиним открытые выходы:
wdoor 58502 south purge
wdoor 58503 north purge
wdoor 58518 west purge
wdoor 58519 east purge
* удаляем глобальные переменные
unset questor585
~
#58508
глава отвечает по поручительству~
0 d 100
поручительство~
* [триг на мобе-квестере в лагере, находится в своем личном доме]
* Если сказать "г поручительство" - приаттачится триг для сдачи квеста варианта "убить всех призраков"
wait 1
* детачим триг 58506 (на приветствие)
detach 58506 %self.id%
хох
г ПОРУЧИТЕЛЬСТВО????
г Неужто дорога длинная повернула тебе мозги путник?
г Поручаться за неизвестного мне человека, ну насмешил!!!
хох
wait 3s
г Ну насмешил, чуть было не лопнул!
хох
эм плюхнулся на стул и звучно захохотал
wait 5s
mecho Прошло несколько минут прежде чем старый вояка успокоился.
wait 2s
эм вытер глаза от слез и стал тереть рукой в области сердца
г Вот вражина, что наделал, старого человека чуть в могилу не свел.
г Из-за тебя вот, сердце прихватило.
тоск
wait 4s
mecho Старик продолжил что-то бубнить себе под нос, не обращая на Вас никакого внимания.
wait 4s
г .......и сердце вот опять шалит........раньше же нужно было....говорил я им....
wait 3s
г давно уже пора...давно....все из-за них дьявольское отродье....
wait 3s
mecho Внезапно старик замер и уставился в вашу сторону.
wait 2s
г Ты кто? Что ты делаешь в моем доме?
хму
wait 3s
mecho Ловко прыгнув к стене старик сорвал со стены огромный меч и направился к Вам.
wait 3s
mecho И вот он уже в трех шагах от Вас, но силы его стали сдавать и меч его тянет к земле.
mecho Тяжело дыша, старик вновь посмотрел Вам в глаза.
wait 5s
г И что это на меня нашло...я..ведь...ну...тьфу ты. Все эти проклятые бессоные ночи.
г Окончательно меня в гроб загонят. Мне бы выспаться, а не могу...понимаешь?
г Не понимаешь. Ладно, объясню.
wait 3s
взд
г Недалеко от лагеря, был когда то горный прииск. Да и не прииск, а так...хобби.
г Да хобби далеко завело нас, видно глубоко мы копнули, накопали, что называется
г на свою голову.
wait 6s
г Сначала в пещерах находиться тяжело стало, вроде как и заходить туда непросто, а внутри
г совсем житья не стало, виски давит, на ум мысли идут непонятные.
г Дальше - больше. Стали видения чудные видиться всем. Несколько человек за виденьями ушли
г да так и не вернулись.
г Решили мы, с нашим хобби завязывать. Перестали в пешеры ходить. С неделю все хорошо было.
г А потом еще хуже, стали те видения, да страх животный преследовать нас по ночам уже
г внутри лагеря.
взд
wait 6s
г И ничего мы с этим поделать не можем, одно спасение, водку пить. Когда выпьешь много
г другие видения видятся, до зла не доводят. Вот и спиваемся тут почуть. Пройди по улице,
г вокруг никого, все в кабаке душу лечат.
г Хранитель трофеев наш, вычитал в книгах, будто души убиенных нами врагов не дают нам
г спокойно век дожить... чего таить, таких много у всех нас поднакопилось...
г А победить уже не сможем их в бою... сам понимаешь возраст...руки не те...
wait 4s
эм сделал несколько глотков из кружки стоящей на столе
wait 3s
г О чем это я?
дум
г Ах да... души окаянные.
г Что ты там говорил то, когда пришел? Поручительство тебе?
ул
г Давай так поступим, поможешь нам в нашей беде, а я помогу тебе.
г Принесешь покой в наше селение, подпишу тебе бумажку эту. Авось там наши ребята сами
г разберуться - достоин али нет вояка тот в дружине быть.
wait 6s
г Ну, тебе решать. Пойдешь в пещеры или до дома повернешь, Как решишь так и будет.
г А теперь иди, отдохнуть мне нужно...
* аттачим триг на сдачу квеста (нужен предмет 58521)
attach 58521 %self.id%
* детачим текущий триг
detach 58508 %self.id%
~
#58509
умерла душа невинного~
0 f 0
~
* [триг на смерть моба #58514 (призрак невинно убиенного)]
* награда: прозрачная субстанция
mload obj 58521
~
#58510
умер призрак лжеца~
0 f 100
~
* [триг на смерть моба #58513 (призрак лжеца)]
* награда: полупрозрачный плащ с кровавым пятном
if %world.curobjs(58514)% < 2 && %random.100% <= 33
  mload obj 58514
end
~
#58511
умер призрак подстрекателя~
0 f 100
~
* [триг на смерть моба #58512 (призрак подстрекателя)]
* награда: полупрозрачные перчатки с кровавым пятном
if %world.curobjs(58515)% < 2 && %random.100% <= 33
  mload obj 58515
end
~
#58512
умер призрак агрессора~
0 f 100
~
* [триг на смерть моба #58511 (призрак агрессора)]
* награда: полупрозрачный медальон
if %world.curobjs(58516)% < 2 && %random.100% <= 33
  mload obj 58516
end
~
#58513
умер глава~
0 f 100
~
* [триг на смерть моба #58502 (глава поселения)]
* награда: перстень с изображением сколопендры
if %world.curobjs(58505)% < 2 && %random.100% <= 33
  mload obj 58505
end
~
#58514
умер бтр~
0 f 100
~
* [триг на смерть моба #58503 (могучий старец)]
* награда: наручи с изображением сколопендры (33%)
if %world.curobjs(58503)% < 1
  * засохший язык:
  mload obj 58503
end
if %world.curobjs(58506)% < 2 && %random.100% <= 33
  * наручи с изображением сколопендры (33%):
  mload obj 58506
end
~
#58515
умер охотник~
0 f 100
~
* [триг на смерть моба #58506 (подвыпивший охотник)]
* награда: сапожищи с изображением сколопендры
if %world.curobjs(58507)% < 2 && %random.100% <= 33
  mload obj 58507
end
~
#58516
умер хранитель~
0 f 100
~
* [триг на смерть моба #58501 (хранитель трофеев)]
* награда: ремень с изображением сколопендры (33%)
if %world.curobjs(58502)% < 1
  * банка с глазами:
  mload obj 58502
end
if %world.curobjs(58508)% < 2 && %random.100% <= 33
  * ремень с изображением сколоендры (33%)
  mload obj 58508
end
end
~
#58517
умер волшебник~
0 f 100
~
* [триг на смерть моба #58505 (пьяный волшебник)]
* награда: перчатки пьяницы
if %world.curobjs(58510)% < 2 && %random.100% <= 33
  mload obj 58510
end
~
#58518
умер витязь~
0 f 100
~
* [триг на смерть моба #58507 (хитрый витяь)]
* награда: тяжелый щит с изображением сколопендры
if %world.curobjs(58511)% < 2 && %random.100% <= 33
  mload obj 58511
end
~
#58519
умер колдун~
0 f 100
~
* [триг на смерть моба #58508 (тощий колдун)]
* награда: капюшон с изображением сколопендры (33%)
if %world.curobjs(58519)% < 1
  * коготь леопарда
  mload obj 58519
else
  if %world.curobjs(58512)% < 2 && %random.100% <= 33
    капюшон с изображением сколопендры (33%)
    mload obj 58512
  end
end
~
#58520
умер трактирщик~
0 f 0
~
* [триг на смерть моба #58504 (здоровяк трактирщик)]
* награда: костяной нагрудник с ликом сколопендры
if %world.curobjs(58513)% < 2 && %random.100% <= 33
  mload obj 58513
end
~
#58521
сдача квеста главе~
0 j 100
~
* [триг на мобе-квестере в лагере, находится в своем личном доме]
* Триг на сдачу предмета #58521 (прозрачная субстанция) завершение квеста (ветки где "убивали призраков")
wait 1
if %object.vnum% == 58521
  if %exist.mob(58501)% && %exist.mob(58502)% && %exist.mob(58503)% && %exist.mob(58504)% && %exist.mob(58505)% && %exist.mob(58506)% && %exist.mob(58507)% && %exist.mob(58508)%
    ул
    say Ну чтож, уговор дороже денег. Смелым и отважным всегда попутный ветер.
    say Ты свое обещание сдержал - сдержу и я. Забирай поручительство мое.
    wait 2s
    mpurge субстанци
    wait 2s
    mload obj 58500
    дать поручит .%actor.name%
    detach 58521 %self.id%
  else
    гнев
    say Ах ты супостат проклятый! Посмел пролить кровь моих содружинников.. Умри!
    attack .%actor.name%
  end
else
  г Я хоть и стар уже, но со зрением у меня все в порядке. Оставь себе эту безделушку.
end
~
#58522
призрак невинного привет~
0 q 100
~
* [триг на призраке невинно убиенного, находится в глубине пещер]
* Приветственный триг, позволяет открыть квестовые ветки: "убийство всех в лагере", или "мирное решение" квеста
wait 1
mecho Призрак печально посмотрел на Вас.
wait 1s
say Не часто у нас бывают гости, каким ветром тебя занесло путник?
wait 2s
say Ты не похож на тех людей, что не дают нам спокойно уйти в иной мир.
wait 2s
say И на рудокопа ты не похож, кто же ты? Что тебе нужно в этом забытом месте?
дум
wait 3s
say Подожди, кажется, я начинаю понимать. Ты здесь, чтобы уничтожить нас?
ул
wait 2s
say Хочу тебя огорчить путник, нас невозможно убить. Нечто большее чем смерть, не пускает
say нас в иной мир. Имя этому кровная клятва.
wait 4s
say Видно у тех людей, что сверху живут, свое мнение на этот счет, но послушай и нас тоже.
wait 3s
say Все те призраки, которых ты можешь найти здесь, при жизни так или иначе перешли дорогу
say Наемной Дружине, за что и были зверско убиты.
дум
wait 1s
say Все за исключением меня, пожалуй. Я был убит из за того, что оказался не в нужное время
say не в нужном месте.
wait 1s
mecho Призрак мрачно улыбнулся.
wait 1s
say Так вот, перед смертью, все мы поклялись не видеть покоя после смерти, пока не отомстим
say тем людям за наши мучения.
wait 2s
say Так-то вот...замкнутый круг. Пока клятва наша не исполнится, мы будем оставаться здесь.
хих
say Поверь мы уже далеко продвинулись к своей цели.
say И даже если ты сейчас убьешь нас всех, то не обольщайся, уже следующей ночью, мы опять
say будем живы.
хох
wait 3s
say Скоро мы победим, я чувствую это!
say Но именно ты можешь максимально приблизить нашу победу, если только захочешь нам помочь.
вопр
* аттач трига на "г помогу" (ТМ), согласие на ветку квеста "убить всех в лагере" или "мирное решение"
attach 58523 %self.id%
* детач текущего трига
detach 58522 %self.id%
~
#58523
квест невинной~
0 d 0
помогу~
* [триг на призраке невинно убиенного, находится в глубине пещер]
* если сказать "г помогу" то откроется возможность выполнить ветки квеста "убить всех в лагере" или "мирное решение"
wait 1
if %actor.vnum% != -1
  halt
end
* помечаем персонажа, как взявшего квест у призрака
makeuid questor585 %actor.id%
global questor585
* даем хранителю троффев знать кто взял квест у призрака
calcuid hranitel 58501 mob
remote questor585 %hranitel.id%
* даем трактирщику  знать кто взял квест у призрака
calcuid traktir 58504 mob
remote questor585 %traktir.id%
* даем богатырю знать кто взял квест у призрака
calcuid btr 58503 mob
remote questor585 %btr.id%
г Хорошо, слушай, свою миссию.
г Нужно всего лишь, убить всех, кому мы поклялись отомстить.
ухм
wait 2s
г Как тебе, а? Не сильно запутано?
ул
г А чтобы, убедиться, что задание выполнено, принесешь мне части моих коллег по несчастью.
г Они должны быть у живых в качестве трофеев.
сплю
wait 1s
г Все, как говорится, жду с нетерпением.
ул
* аттачим триг, позволяющий дсать 3 предмета, полученных от мобов в лагере, этому призраку
attach 58524 %self.id%
* детачим текущий триг
detach 58523 %self.id%
~
#58524
сдача квеста невинному~
0 j 100
~
* [триг на призраке невинно убиенного, находится в глубине пещер]
* нужно принести три предмета, которые дадут: хранитель трофеев, трактирщик, богатырь
* Если дать все три предмета - призрак обретает свободу и пуржится.
wait 1
if %actor.id% == %questor585.id%
  if %object.vnum% == 58502
    ул
    say Молодец! Ты добыл глаза подстрекателя! Ишь как уставились, отвратительное зрелище!
    say Скоро мы обретем покой!
    mpurge глаза
    set glaza585 1
    global glaza585
  elseif %object.vnum% == 58503
    ул
    say Молодец! Ты добыл язык лжеца! Вот так язычище, как такой во рту помещался, непонятно!
    say Скоро мы обретем покой!
    mpurge язык
    set yazik585 1
    global yazik585
  elseif %object.vnum% == 58504
    ул
    say Молодец! Ты добыл руки и ноги агрессора! Неплохие культяпки, жаль пропадали столько.
    say Скоро мы обретем покой!
    mpurge мешок
    set rukinogi585 1
    global rukinogi585
  end
  if %glaza585% == 1 && %yazik585% == 1 && %rukinogi585% == 1
    * мы получили весь набор шмоток, обнуляем переменные и выдаем награду:
    set glaza585 0
    global glaza585
    set yazik585 0
    global yazik585
    set rukinogi585 0
    global rukinogi585
    set questready585 1
    global questready585
    * определяем, сделал лирок "мирный квест", "убил всех в лагере" или "убил призраков":
    * Живы: призрак агрессора, призрак подстрекателя, призрак лжеца: 
    if %exist.mob(58511)% && %exist.mob(58512)% && %exist.mob(58513)%
      * Живы: хранитель трофеев, глава лагеря, богатырь, трактирщик, колдун, волшебник, охотник:
      if %exist.mob(58501)% && %exist.mob(58502)% && %exist.mob(58503)% && %exist.mob(58504)% && %exist.mob(58505)% && %exist.mob(58506)% && %exist.mob(58507)% && %exist.mob(58508)%
        г Вижу, не стал ты устраивать резни, и убивать наших заклятых врагов.
        г Но ты принес, что обещал, и теперь мы с миром сможем уйти на покой. 
        mload obj 58520
        * аттачим на хранителя трофеев триг, означающий, что игрок проходит "мирный квест" 
        calcuid hranitel 58501 mob
        attach 58526 %hranitel.id%
      else
        г Неужели ты все достал??? Не могу поверить!
        г Сделал дело - гуляй смело! Собственно забирай награду и гуляй!
        if %world.curobjs(58518)% < 2 && %random.100% <= 33
          mload obj 58518
        else
          mload obj 58520
        end
      end
      дать все .%actor.name%
      г Прощай!
      ухм
      * пуржим агрессора
      calcuid agressor 58511 mob
      mpurge %agressor.id%
      * пуржим подстрекателя
      calcuid podstrek 58512 mob
      mpurge %podstrek.id%
      * пуржим лжеца
      calcuid lzetch 58513 mob
      mpurge %lzetch.id%
      wait 1s
      mecho Призрак растворился в воздухе.
      mpurge %self%
    else
      г Ты принес части моих друзей, но похоже кто-то снова их убил, уж не ты ли?!
      atack .%actor.name%
    end
  end
end
~
#58525
мирный квест~
0 q 100
~
* [триг на хранителе трофеев, моб находится в здании, на севере лагеря]
* Приветственный триг, если перед мобом тот кто взял квест - он получает
* один из трех необходимых призраку предметов, а также подсказку, где
* взять остальные два предмета, а также намек что существует "мирное решение" квеста.
wait 1
шля
if %actor.id% == %questor585.id%
  if %world.curobjs(58502)% < 1
    г Приветствую в своей скоромной обители!
    wait 1s
    г А я тебя уже давно жду. Не удивляйся, мне многое известно.
    г Поселение у нас небольшое, все на виду.
    г И о твоем уговоре с призраком, тоже мне ведомо.
    взд
    wait 4s
    г Чтож, у меня только один вариант решения есть. Хватит уже крови на мой век. Отдам тебе
    г добровольно часть от призраков тех. Не придется тебе убивать ратников наших.
    wait 4s
    г Забирай, да скажи им, что выполнил свой уговор! Авось с радости проверять не станут,
    г сгинут навечно и оставят нас в покое.
    г Коли получится, заходи ко мне, награду получишь. Ну а не получится, придется нам с тобой
    г силушкой померяться. А там уже посмотрим, кто чьи части тела собирать будет.
    ухм
    mload obj 58502
    дать банк .%actor.name%
  end
  г Остальных частей у меня нет, спроси у трактирщика нашего.
  г Уж он то должен знать!
  * детачим текущий триг
  detach 58525 %self.id%
else
  г Приветствую в своей скоромной обители!
end
~
#58526
награда хранителя~
0 q 100
~
* [триг аттачится призраком, на хранителя ценностей, если происходит "мирное решение" квеста]
wait 1
if %actor.id% == %questor585.id%
  * сбрасываем переменную о выполнении квеста
  set questready585 0
  global questready585
  ул
  * определяем, сделал ли игрок "мирный квест"
  * Живы: хранитель трофеев, глава лагеря, богатырь, трактирщик, колдун, волшебник, охотник:
  if %exist.mob(58501)% && %exist.mob(58502)% && %exist.mob(58503)% && %exist.mob(58504)% && %exist.mob(58505)% && %exist.mob(58506)% && %exist.mob(58507)% && %exist.mob(58508)%
    г Поздравляю, тебе удалось обвести вокруг пальца этих надоедливых призраков!
    if %world.curobjs(58517)% < 2 && %random.100% <= 33
      г Уговор - дороже денег!
      г Вот моя награда, пусть сослужит тебе добрую службу.
      mload obj 58517
      дать кинжал .%actor.name%
    end
  else
    г Значить не получилось у тебя по-хорошему...
    г Получай за пролитие крови моих друзей!
    заколоть .%actor.name%
    atack .%actor.name%
  end
  * детачим текущий триг
  detach 58526 %self.id%
end
~
#58527
трактирщик говорит~
0 q 100
~
* [триг аттачится призраком, во время выдачи квеста на "убийство всех в лагере" или "мирное решение"
* моб подсказывает что чтобы достать нужные ингридиенты нужно спуститься вниз и убить монстра
wait 1
if %actor.id% == %questor585.id%
  г Привет, привет! Может кружечку пива знатного? А? Прохладного, с пеночкой?
  wait 2s
  mecho Трактирщик поднял большой, наполненный свежим пивом бокал на свет.
  wait 2s
  г Смотри как золотистый цвет играет?
  облиз
  г Не хочешь? Как хочешь! А вот я с удовольствием!
  mecho Трактирщик залпом опустошил бокал до половины.
  рыг
  wait 2s
  г Отменный вкус! А теперь закусить ржаным хлебушком сушеным!
  mecho Трактирщик закинул себе в рот пригоршню мелких сухарей и звучно захрумкал.
  wait 3s
  ул
  г Жизнь - хороша! Пожалуй, по случаю хорошего настроения, дам перекусить и Алехе нашему.
  ухм
  wait 2s
  mecho Собрав со стола очередную порцию объедков, трактирщик открыл люк в полу.
  mecho Затем небрежно высыпал объедки с тарелки в зияющий проем.
  wait 3s
  г Алеха - не то человек, не то животное, сами не знаем. Живет у нас в погребе, объедки да помои
  г жрет. Больно интересно, что из него вырастет.
  г Одно пока ясно, людей непереносит, пытался я недавно посветить в погреб чтоб рассмотреть
  г его получше, так он сволочь такая, чуть ногу мне не оттяпал.
  wait 2s
  mecho Трактирщик демонстративно задрал штанину и показал Вам огромный рубец от раны.
  wait 2s
  г Кстати! Я слыхал ты кое-чего ищешь?
  ул
  г Тогда прямая дорога тебе к нашему Алехе, поскольку мешок с отрублеными трофеями я ему на
  г корм кинул. Нужны если? Иди и отними!
  г Да! С богатырем нашим поговори еще, авось, что подкинет тебе!
  * детачим текущий триг
  detach 58527 %self.id%
else
  г Приветствую! Свободных мест, хоть отбавляй! Располагайся!
end
~
#58528
умерло чудище~
0 f 0
~
* [триг на чудище, которое сидит в подвале трактира]
* загружается один из трех необходимых предметов, для сдачи квеста призраку.
if %world.curobjs(58504)% < 1
  mload obj 58504
end
~
#58529
бтр привет~
0 q 100
~
* [триг на мобе богатыре - живет в соседнем доме, от главы лагеря]
* приветственный триг, если пришедший - взявший квест - то дает необходимый предмет,
* который необходимо передать призраку.
wait 1
if %actor.id% == %questor585.id%
  г Привет, привет! Слыхал я о твоем задании, чтож, вариант неплохой. Попытка - не пытка.
  wait 1s
  г Забирай и мой трофей. Надеюсь, поможет. Иного выбора у тебя нет, придешь злом задание
  г делать, отведаешь мою плюху.
  wait 1s
  кул
  wait 1s
  ухм
  if %world.curobjs(58503)% < 1
    mload obj 58503
    дать все .%actor.name%
    detach 58529 %self.id%
  end
elseif %actor.clan%==нд
  приве .%actor.name%
else
  хму
  г Проходи мимо бродяга!
end
~
$~
