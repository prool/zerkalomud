#72200
входкданиле~
0 q 100
~
wait 1s
вздох
say Я помню...
г Я делал каменный цветок каждый день, испробовал весь малахит со всей округи.
г Но каждый раз, когда цветок был почти готов, на следующий день он разваливался.
г Не иначе, это было колдовство хозяйки.
wait 1s
дум
detach 72200 %self.id%
~
#72201
приветствие 1~
0 q 100
~
wait 1
mecho Старик медленно поднял голову и посмотрел на Вас.
mecho Рассмотрев Вас своими подслеповатыми глазами он улыбнулся Вам.
if !%questor7221%
  say - Здраве будь %actor.name%!
  mecho _- Думал не увижу на своем веку уж больше живого человека.
  mecho _- Помоги мне вещь найти одну, а я уж тебе все расскажу...
  attach 72202 %self.id%
end
~
#72202
выдача квеста1~
0 d 1
какую согласен пойду помогу принесу~
wait 1
if %actor.vnum% != -1
  halt
end
if !%questor7221%
  say В деревушке нашей жил мастер один, по камню резал.
  mecho _- Данилой его звали... Сделал он каменный цветок...
  mecho _- Хочу на него взглянуть перед смертью. Найди его пожалуйста
  mecho _- и принеси мне благодарен буду.
  set questor7221 %actor%
  global questor7221
  attach 72204 %self.id%
  detach 72201 %self.id%
  detach 72202 %self.id%
end
~
#72203
данила-мастер ~
0 d 100
почему~
wait 1s
эмоц поднял на Вас взгляд и сказал:
wait 1
mecho _-Каждый раз, как почти сделаю цветок, ломается он в моих руках.
mecho _-Говорят, потому как только с согласия хозяйки горы можно завершить эту работу.
detach 72203 %self.id%
~
#72204
оплата квеста 1~
0 j 100
~
wait 1
if %object.vnum% != 72252
  say - Зачем мне это?
  брос %object.name%
  halt
end
wait 1s
mjunk all
say - Спасибо тебе, %actor.name%!
say - Утешил%actor.g% старика напоследок! 
unset questor7221
if (%world.curobjs(72205)% < 5) && (%random.100% < 25 )
  wait 3
  say - Тут у меня одна вещица завалялась, мне она уже не надобна,
  say - А тебе может пригодиться...
  mload obj 72205
  дать чекан %actor.name%
else
  %self.gold(+500)%
  give 500 кун .%actor.name%
end
wait 1s
say - Также открою тебе тайну нашей деревни...
вздох
эмоц смахнул дрожащей рукой слезу.
wait 2s
say Угнала в полон всех жителей хозяйка медной горы. Чтоб не скучно
mecho _- было Даниле-мастеру, который сам к ней и ушел. Оставалась правда
mecho _- девица красная Катя, что любила Данилу. Но ушла она к Змеинной горе
mecho _- да и не вернулась.
вздох
wait 1s
mecho _- Видно нашла вход в рудник - царство хозяйки медной горы.
mecho _- Может и поможет чем тебе моя история...
calcuid enter 72297 room
attach 72208 %enter.id%
detach 72204 %self.id%
~
#72205
приветствие 2~
0 r 100
~
wait 1
msend %actor% Приветствую Вас!
msend %actor% Вы находитесь во владениях Хозяйки Медной горы
msend %actor% Жаль, но покинуть Вы их не сможете...
хихи
~
#72206
убийство ключника~
0 f 0
~
mecho _Отхаркивая кровью, ключник приподнялся на одной руке в луже крови
say _- Наконец-то я свободен от службы Хозяйке медной горы.
mecho __- Но перед смертью, я хочу поблагодорить тебя...
mecho __- На севере, в темнице, - Ключник закашлялся кровью.
mecho Несколько капель его крови попало Вам на одежду...
say _- Заточены жители деревушки, через которую ты прошел.
mecho __- Освободи их. Они не служат хозяйке - они ее пленники...
mecho _Ключник рухнул на пол, но когда Вы кинулись к нему, то поняли, что он мертв.
mload obj 72257
~
#72207
запрет 1~
0 r 100
~
wait 1
say - Стой! Кто идет?
say - Идите ка Вы все погулять в другое место!
say - Дальше для Вас дороги нет!
эмоц угрожающе посмотрел на Вас.
detach 72207 %self.id%
~
#72208
создание двери~
2 c 1
сдвинуть ~
if !%arg.contains(камень)%
  %send% %actor% _Что вы хотите сдвинуть?
  halt
end
wait 2
wsend %actor% Вы сдвинули камень. И почуствовали что-то неладное...
wechoaround %actor% %actor.name% сдвинул%actor.g% камень в сторону.
wait 1
wecho От скрежета камней, раздающегося в далеке Ваша душа
wecho уходит в пятки, а волосы встают дыбом.
wait 1
wdoor 72221 s room 72222
wecho Тут Вы замечаете, что камень на полу приходит в движение
wecho Образуя какой-то замысловатый узор...
wload mob 72257
wait 1
set target %random.pc%
wforce stonesnake722 shou Кто посмел нарушить мой покой???!!!
wforce stonesnake722 shou Ты умрешь за это!!!
wforce stonesnake722 kill %target.name%
detach 72208 %self.id%
~
#72209
Начальник охраны убит~
0 f 100
~
calcuid arsenal 72256 room
attach 72216 %arsenal.id%
~
#72210
взяли чашу~
1 g 100
~
wait 1
oecho Вы почуствовали странное дуновение воздуха и чье-то присутствие рядом.
wait 1
oload mob 72256
wait 1
oforce ghost722 say Ах, ты, выползыш змеиный!!!
oforce ghost722 say Кто позволил чужое брать?!!!
oforce ghost722 kill %actor.name%
detach 72210 %self.id%
~
#72211
старый узник 1~
0 c 1
освободить расковать~
if %actor.vnum% != -1
  halt
end
if !%arg.contains(узника)%
  %send% %actor% Кого освобождаем?
  halt
end
wait 1
эмоц поднял голову и взглянул на Вас усталыми подслеповатыми глазами
say - Спасибо тебе, %actor.name%, за то что, освободил%actor.g% нас.
mecho _- Долго мы были в заточении у Хозяйки медной горы.
mecho _- Много лет прошло, с того времени, как нас она в полон взяла
вздох
wait 1s
say Захватила она обманом и мою дочь - красавицу Катю...
mecho _- Ушла она искать возлюбленного своего Данилу, да попала в ловушку.
mecho _- Держит ее в заточении Хозяйка в своих личных покоях.
mecho _- Освободи ее, может поможет она тебе,
mecho _- Расскажет как хозяйку одолеть...
while %exist.mob(72252)%
  calcuid uznik 72252 mob
  mechoaround %uznik% _Узник отбросил в сторону инструмент, поднялся и направился к выходу из пещеры.
  %purge% %uznik%
done
mecho _Узник отбросил в сторону инструмент, поднялся и направился к выходу из пещеры.
calcuid katja 72255 mob
attach 72212 %katja.id%
wait 1
mpurge %self%
~
#72212
катя 1~
0 d 0
привет здравствуй~
wait 1
if %actor.vnum% != -1
  halt
end
set string1 %speech.cdr%
if !%string1.contains(Катя)%
  msend %actor% С кем здороваетесь?
  halt
end
if %string1.contains(дура)%
  say Это кто дура?! Я вот тебе сейчас!!!
  втстать
  kill .%actor.name%
  detach 72212 %self.id%
  halt
end
wait 1
эмоц увидав Вас, зарыдала от радости.
say Спасибо за помощь.
mecho _- Но не за себя прошу...
mecho _- Освободите возлюбленного моего - Данилу-мастера
mecho _- Показала ему Хозяйка медной горы каменный цветок
mecho _- И он попал под его чары
эмоц всхлипнула.
wait 2
say Держит его хозяйка в тайном убежище
mecho _- Что на юге большого зала находится...
mecho _- Спасите его, умоляю...
wait 3
say А чтобы он Вас узнал передайте ему мое колечко
say По нему он узнает что жива я еще и что Вы можете помочь.
mload obj 72214
дать колечко %actor.name%
wait 1
msend %actor% Дав Вам колечко, Катя убежала к себе в деревню...
mechoaround %actor% Дав %actor.dname% колечко, Катя убежала к себе в деревню...
calcuid danila 72254 mob
attach 72213 %danila.id%
wait 1
mpurge %self%
~
#72213
данила 1~
0 j 100
~
wait 1
if %object.vnum% != 72214
  say - Зачем мне это?
  дать %object.name% %actor.name%
end
mjunk all
дум
emot внимательно рассмотрел лежащее на его ладони простенькое колечко
say Что же это... знакомое кольцо... чье оно?
emot встрепенулся
say Катя! Это же ее кольцо - я сам подарил ей его!
wait 1s
say Спасибо тебе, %actor.name%!
mecho _- Рад что ты смог%actor.q% освободить возлюбленную мою. 
mecho _- Значит жива она. Расскажу я тебе секрет хозяйки...
mecho _- Стражи у нее люто злые - берегись их, а лучше ежели сможешь убей.
wait 1
say - Да и не забудь разбить каменный цветок - в нем сила хозяйки...
вздох
mecho _- Пока цел цветок - нет избыву колдовской силе хозяйки.
mecho _- Даже коли одолеешь ты ее - оживет она, и снова заберет себе людей в полон...
mecho _- Я не знаю, где укрыт потаенный каменный сад хозяйки,
mecho _- но наверное, это где-то подле ее покоев и тронного зала.
say Ну ступай с богом! Удачи тебе!
emot шагнул за дверь и пропал
calcuid tron 72265 room
attach 72219 %tron.id%
wait 1
%purge% %self%
~
#72214
убийство хозяйки~
0 f 100
~
if %random.100% <= 10
  mload obj 72268
  * пока никаких книг (Землетряс сюда, имхо)
  mload obj 72210
end
mload obj 72254
~
#72215
смерть каменной змеи~
0 f 100
~
if (%world.curobjs(72213)% < 3 ) && (%random.100% < 40 )
  msend %actor% И тут Вы неожиданно заметили, что перед смертью змея что-то уронила на землю.
  mload obj 72213
end
~
#72216
осмотрели стеллажи~
2 c 1
обшарить обыскать~
if %actor.vnum% != -1
  halt
end
if !%arg.contains(стеллажи)%
  wsend %actor% Что вы хотите обыскать?
  halt
end
wait 1
wsend %actor% - Вы начали осматривать оружие и доспехи лежащие на стеллажах.
wsend %actor% - Но оружие слишком тяжело для Вас, а доспехи похоже рассчитаны
wsend %actor% - на гигантов.
wait 1
wsend %actor% - Но что это? Пока Вы копались среди всего этого хлама, из под огромного
wsend %actor% - щита на пол выскользнуло с шумом что-то небольшое...
eval rnd %random.100%
if %rnd% < 11
  if %world.curobjs(72211)% < 5
    wload obj 72211
  else
    wload obj 72210
  end
elseif %rnd% < 18
  if %world.curobjs(72212)% < 5
    wload obj 72212
  else
    wload obj 72210
  end
else
  wload obj 72210
end
if ((%world.curobjs(3326)% < 1) && (%random.1000% < 40))
  wload obj 3326
end
detach 72216 %self.id%
~
#72217
создание цветка~
0 f 0
~
mecho Как только замерли последние содрогания тела огромного змея,
mecho Вы заметили странное движение камня на полу.
mecho Что это?!! Неужели камни умеют расти?!
mload obj 72261
calcuid gard 72257 room
attach 72253 %gard.id%
~
#72218
неуязвимость хозяйки~
0 p 100
~
return 0
mecho \&GМалахитовые стены вокруг мягко засветились, и раны хозяйки горы исчезли сами собой. \&n
eval buffer %self.hitp(%self.maxhitp%)% 
~
#72219
Сдвинуть трон~
2 c 1
сдвинуть отодвинуть подвинуть~
if %actor.vnum% != -1
  halt
end
if !%arg.contains(трон)%
  wsend %actor% Что двигаем?
  halt
end
eval val1 %actor.int%
eval val2 %actor.intadd%
eval sumint %val1%+%val2%
if %sumint% < 23
  wsend %actor% Да как вы собрались этакую тяжесть двигать?
  wsend %actor% Как бы вы сильны не были - человеку такую махину даже не пошевелить.
  halt
end
wait 1
wechoaround %actor% %actor.name% осмотрел%actor.g% трон хозяйки и задумал%actor.u%.
wsend %actor% Вы осторожно притронулись к выступу на спинке трона и тот мягко и плавно отошел в сторону.
wechoaround %actor% %actor.name% осторожно притронул%actor.u% к выступу на спинке трона и тот мягко и плавно съехал в сторону.
wecho Под троном в полу открылась узкая винтовая лесница, уходящая во тьму.
wdoor 72265 d room 72200
detach 72219 %self.id%
~
#72220
лоад ключа на волоте~
0 f 100
~
if %self.realroom% = 72230
  mload obj 72256
end 
~
#72221
рандом-агро на стражах~
0 q 60
~
wait 1
mkill %actor%
~
#72253
разбили цветок~
2 c 1
разбить расколоть расколотить~
if %actor.vnum% != -1
  halt
end
if !%arg.contains(цветок)%
  wsend %actor% Что вы хотите разбить?
  halt
end
wait 1
wechoaround %actor% %actor.name% нанес%actor.q% удар по цветку от которого тот рассыпался в пыль.
wsend %actor% Вы нанесли удар по цветку от которого тот рассыпался в пыль.
wecho _______
wecho В тот же миг гора задрожжала, откуда-то донесся стон и треск проседающей порорды, сверху посыпались камни.
wait 2s
wecho Чудовищный грохот обвала - словно целая гора рухнула в пропасть, сотряс все вокруг.
calcuid flower 72261 obj
wait 1
wpurge %flower%
if %world.curobjs(72262)% < 5
  wload obj 72262
end
calcuid hozayka1 72299 mob
detach 72218 %hozayka1.id%
detach 72253 %self.id%
~
#72254
Reset triggers in zone 72~
2 f 100
0~
calcuid room1 72291 room
detach 72208 %room1.id%
wdoor 72221 s purge
calcuid room2 72265 room
detach 72219 %room2.id%
wdoor 72265 d purge
calcuid room3 72257 room
detach 72253 %room3.id%
*if %exist.mob(72200)%
*wforce questmob7222 drop all
calcuid hozayka1 72299 mob
detach 72218 %hozayka1.id%
attach 72218 %hozayka1.id%
* *wait 1
*%purge% %hozayka1%
*wforce questmob7222 get all
*end
~
$~
