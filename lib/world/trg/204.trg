#20400
прыгаемвреку~
2 c 1
прыгнуть~
if !(%arg.contains(река)%) 
  wsend %actor% Куда это Вы хотите прыгнуть?
  return 1
  halt
else
  wait 1
  wsend %actor% Лихо перемахнув через перила, Вы очутились в воде.
  wechoaround %actor% %actor.name% лихо сиганул%actor.g% в реку.
  wteleport %actor.name% 20419
end
~
#20401
ловимрыбу~
2 c 100
ловить~
if !(%arg.contains(рыбу)%) 
  wait 4
  wsend %actor% И что вы собрались ловить?
  return 1
  halt
else
  wait 1
  eval item %actor.eq(18)%
  wait 1
  if %item.vnum% != 20400
    wsend %actor% Чем же Вы будете ее ловить?
  else
    wait 10
    wsend %actor% Вы забросили удочку и стали ждать.
    wechoaround %actor% %actor.name% забросил%actor.g% удочку и стал%actor.g% ждать.
    wait 30
    wsend %actor% Через некоторое время задергался поплавок. Вы со всей дури рванули удочку на себя, 
    wsend %actor% и вытянули огромного осетра, который в последний момент сорвался.
    wechoaround %actor% Через некоторое время задергался поплавок. %actor.name% со всей дури рванул%actor.g% удочку на себя, 
    wechoaround %actor% и вытянул%actor.g% огромного осетра, который в последний момент сорвался.
    wait 17
    wsend %actor% Падая, он зацепился хвостом за крючок и теперь не сможет уйти на дно.
    wechoaround %actor% Падая, он зацепился хвостом за крючок и теперь не сможет уйти на дно.
    wload mob 20403
    detach 20401 %self.id%
  end
~
#20402
выплываемнаберег~
2 c 100
выплыть~
if !(%arg.contains(берег)%) 
  wsend %actor% Куда это Вы собрались плыть?
  return 1
  halt
else
  wait 1
  wsend %actor% Барахтаясь в воде, Вы вскоре выплыли к берегу.
  wechoaround %actor% %actor.name% начал%actor.g% бодро работать руками и ногами и вскоре уже был на земле.
  wteleport %actor.name% 20420
end
~
#20403
глушимрыбу~
0 c 100
глушить~
if !(%arg.contains(рыбу)%) 
  msend %actor% И кого Вы хотите оглушить?
  return 1
  halt
else
  wait 1
  msend %actor% Вы с размаху шлепнули по воде пуще прежнего.
  mechoaround %actor% %actor.name% с размаху шлепнул%actor.g% по воде еще сильнее.
  wait 5
  Mecho Осетра совсем оглушило.
  mload mob 20404
  mpurge %self%
~
#20404
зашликРыбаку~
0 r 100
*~
wait 10
Эм настороженно начал рассматривать Вас.
wait 15
mecho Поняв, что Вы русич, он успокоился.
Say Эка, брат, тебя занесло.
взд
wait 5
say Видишь, лихо какое на нас навалилось. Татары как пришли, столько горя досталось нам.
Все разграбили, а кто вздумал с ними силой меряться, того и порешили, нехристи поганые.
Выть
wait 10
say Один кузнец остался из дюжих. Да и тот отощал. 
взд
wait 5
say Хотя ежели покормить его, то делов бы он натворил. 
дум
wait 20
say Слушай, а давай я тебя рыбачить научу? Авось чего да выловишь, добрый люд от голодной смерти
say спасешь, а то сам-то я уж ослабел слишком сильно.
wait 10
say Ну что, согласен?
attach 20405 %self.id%
attach 20406 %self.id%
detach 20404 %self.id%
~
#20405
зашликРыбаку2~
0 r 100
*~
эм вопросительно уставился на Вас
~
#20406
гСогласен~
0 d 100
согласен~
wait 10
mload obj 20400
дать удочк %actor.name%
say Вот, держи уду. Ловить рыбу дело хитрое, да у тебя, я вижу, смекалки хватит. 
wait 15
say Как поплавок задергается, сразу тащи что есть мочи на берег рыбу. Ну а коли сорвется, 
say глуши ее пару раз, чтоб не уплыла.
wait 10
mecho Много чего еще рассказал Вам старый рыбак про свое ремесло, да где там все упомнить.
mecho Надо бы реку найти подходящую.
wait 10
say Ну теперь ступай. 
calcuid kuz 20407 mob
attach 20407 %kuz.id%
detach 20412 %kuz.id%
detach 20405 %self.id%
detach 20406 %self.id%
~
#20407
зашликКузнецу2~
0 r 100
*~
wait 10
Осм %actor.name%
wait 10
say А ты еще кто таков?
say От кого пришел?
Вопр %actor.name%
attach 20408 %self.id%
attach 20409 %self.id%
detach 20407 %self.id%
~
#20408
готрыбака~
0 d 100
от рыбака~
wait 10
эм понимающе покивал
wait 10
say Хороший мужик, жаль, трусоватый чуток. А то если б рыбки б наловил, да покушать
say приготовил, ух, я бы тут делов наворотил!
эм захрипел от злобы
wait 10
say В первую очередь прибил бы стража, что баскака охраняет! Мою кузню ограбил, сволочь.
say Ох и противная же у него рожа!
рыч
attach 20409 %self.id%
detach 20408 %self.id%
~
#20409
даемжратькузнецу~
0 j 100
*~
if ((%object.vnum% != 20401 ) && (%object.vnum% != 20402))
  drop all
  halt
end
wait 5
mpurge %object.name%
wait 20
Рад
say Ыыыы...!!!
Эм начал быстро запихивать рыбу себе в рот и глотать, почти не прожевывая
wait 36
эм сыто рыгнул
wait 10
say Ну сейчас-то я покажу этому поганцу кузькину мать!
%actor.exp(+100000)%
calcuid doz 20402 mob
detach 20422 %doz.id%
в
ю
ю
ю
ю
з
з
с
ко !огн шар! часовой
detach 20410 %self.id%
detach 20409 %self.id%
~
#20410
зашликСтражу~
0 r 100
*~
wait 10
Кричать Мне велено никого не пускать! Топай отсюда!
wait 45
mecho Внимательно оглядев вас, дозорный зыркнул взглядом по сторонам и, наклонившись
mecho к вашему уху, быстро прошептал: "Тащи рыбки, договоримся".
миг 
attach 20414 %self.id%
detach 20410 %self.id%
~
#20411
даемжратьрыбаку~
0 j 100
*~
if ((%object.vnum% != 20401 ) && (%object.vnum% != 20402))
  drop all
  halt
end
wait 5
Рад
mpurge %object.name%
wait 20
Обнять %actor.name%
say Спасибо тебе, друг. Не дал%actor.g% старику умереть голодной смертью!
say Боги тебя не забудят!
%actor.exp(+100000)%
dg_cast 'добл' %actor.name%!
~
#20412
зашликКузнецу1~
0 r 100
*~
wait 10
Осм %actor.name%
~
#20413
режемфиле~
1 c 100
разрезать~
if !(%arg.contains(филе)%) 
  osend %actor% Что это Вы собрались резать?
  return 1
  halt
else
  wait 1
  osend %actor% Ловко взмахнув пару раз ножичком Вы располосовали рыбное филе.
  oechoaround %actor% %actor.name% ловко взмахнул%actor.g% несколько раз ножичком, расчленив филе.
  oload obj 20402
  oload obj 20402
  oload obj 20402
  oload obj 20402
  oload obj 20402
  opurge %self%
end
~
#20414
даемжратьстражу~
0 j 100
*~
if ((%object.vnum% != 20401 ) && (%object.vnum% != 20402))
  drop all
  halt
end
wait 5
ул
mpurge %object.name%
wait 20
say Гм. Нашел таки рибака. 
say Ладно, пирапущу я тибя. Тока симатри не задерживаися!
mecho Часовой провел Вас внутрь терема.
mteleport all 20489
~
#20415
зашликБаскаку~
0 r 100
*~
wait 10
say Зачем ти решился потиривожить миня?
Эм внимательно смотрит на Вас.
attach 20416 %self.id%
detach 20415 %self.id%
~
#20416
даемдоскубаскаку~
0 j 100
*~
switch %object.vnum%
  case 20335
    wait 1
    mpurge %object.name%
    wait 19
    say Гмм. Это письмо, но читобы его пирочитать мине понадобится котел, читобы разогреть ви нем воду.
    say Пошерсти по терему, наверняка где-нибудь завалялось чито-нибудь подходящае.
    attach 20417 %self.id%
    detach 20416 %self.id%
    case 20336
      wait 1
      mpurge %object.name%
      wait 19
      Эм внимательно посмотрел на доску.
      wait 25
      say Ти питался пирочитать сообщение и осмелился сюда пирийти?!
      say Умри, сабака!
      wait 5
      Убить %actor.name%
      detach 20416 %self.id%
      case 20336
        wait 1
        mpurge %object.name%
        wait 19
        say Гмм. Это письмо от темника. Он сичитает чито необходимо выслать подмогу для бистрейшего 
        say захвата городов.
        Дум
        attach 20420 %self.id%
        exec 20420 %self.id%
        detach 20416 %self.id%
~
#20417
даемчанбаскаку~
0 j 100
*~
switch %object.vnum%
  case 20403
    wait 1
    mpurge %object.name%
    say Карашо! Подйем во двор, пирочитаем чито сидесь написано.
    mteleport all 20435
    calcuid dvor 20435 room
    exec 20418 %dvor.id%
    mpurge %self%
~
#20418
лоадБаскака~
2 z 100
*~
wait 2s
wload mob 20400
wecho Баскак наполнил чан водной и подвесил над костром. 
wait 15
wecho Через несколько минут, когда вода закипела, он сунул в нее доску. 
wait 2s
wecho Вскоре воск размяк, и легко счистив его с доски, басурманин прочел послание.
calcuid bas 20400 mob
attach 20419 %bas.id%
exec 20419 %bas.id%
~
#20419
прочелмессагу~
0 z 100
*~
wait 1s
say Они хотят читобы я вислал подкирепление.
wait 20
Дум
attach 20420 %self.id%
exec 20420 %self.id%
~
#20420
решениебаскака~
0 z 100
*~
wait 1s
if %random.1000% <= 660
  say Читож, вискоре ми видвинемся к Китежу, тем более чито сийчас сюда из сердца ситепи идет замена.
  say Но на юго востоке, аккурат где заканчивается ситепь и начинается лес поселился какой-то 
  say зилобный волк, которий постоянно нападает на наши караваны. Разберись си ним и пириходи ко мне.
  attach 20431 %self.id%
else
  say Ми не ели уже несколько дней, пи ри осаде этого замка ми понесли болишие потери. 
  say У нас не хватает экипировки. Нам надо дождаться подкирипления из степи!
  wait 25
  say Пиредай это темнику, он висе поймет.
  mload obj 20405
  Дать слом.стре %actor.name% 
end
~
#20421
снимаемчан~
2 c 1
снять~
if !(%arg.contains(чан)%) 
  wsend %actor% И что Вы собираетесь снимать?
  return 1
  halt
else
  wait 1
  wsend %actor% Вы начали было снимать кострюлю, но поскользнувшись выронили ее из рук.
  wechoaround %actor% %actor.name% начал%actor.g% снимать со стены кострюлю, но поскользнул%actor.g%ся
  wechoaround %actor% и выронил%actor.g% ее из рук.
  wait 5
  wecho С оглушительным звоном кострюля упала на пол.
  wload obj 20403
end
~
#20422
агримСтража~
0 p 100
*~
wait 10
Кричать висем визять оружие! Защитить командира!
mload mob 20410
mload mob 20410
wait 10
ко !масс оцеп!
detach 20422 %self.id%
~
#20423
Элитаагрит~
0 n 100
*~
wait 1
ко !огне поток!
~
#20424
померосетр~
0 f 100
*~
mload obj 20401
~
#20425
сумонимпсов~
0 p 100
*~
wait 1
mecho Оглушительно свистнул и, откуда ни возьмись, появился клыкастый пес.
mload mob 20416
detach 20425 %self.id%
~
#20426
песагрит~
0 n 100
*~
wait 1
след  дегеремчин
уб %pc.name%
пом дегеремчин
~
#20427
подходимкноре~
2 c 1
подойти~
if !(%arg.contains(нора)%) 
  wsend %actor% Куда это Вы хотите подойти?
  return 1
  halt
else
  wait 1
  wecho Как только Вы приблизились к норе, из нее выпрыгнуло что-то весьма большое.
  wload mob 20420
end
detach 20427 %self.id%
~
#20428
РЕПОП~
2 f 100
*~
calcuid room1 20483 room
attach 20427 %room1.id%
*атачим триги вылова рыбы
calcuid room2 20419 room
attach 20401 %room2%
wait 1
calcuid room3 20436 room
attach 20401 %room3%
calcuid room4 20453 room
attach 20401 %room4%
wait 1
calcuid room5 20454 room
attach 20401 %room5%
calcuid room6 20455 room
attach 20401 %room6%
~
#20429
шакалагрит~
0 n 100
*~
wait 1
ко !камнепад! %pc.name%
~
#20430
шакалсдох~
0 f 100
*~
mload obj 20406
if (%world.curobjs(20418)% < 4) && (%random.1000% <= 80)
  mload obj 20418
end
~
#20431
сдаембошкушакала~
0 j 100
*~
switch %object.vnum%
  case 20406
    wait 1
    mpurge %object.name%
    wait 19
    say Пирекрасно! Типерь дорога из ситепи безопасна и ми можем начинать собираться в поход.
    say Пиредай это темнику.
    mload obj 20404
    Дать тогюрук %actor.name%
~
#20432
заходимкрусичу~
0 r 100
*~
wait 10
Эм закашлялся
wait 30
say Не думал я, что доведется перед смертью повидать земляка.
wait 5
Эм сплюнул кровь и вытер лицо ладонью
wait 15
say Не сдюжили мы город то отстоять и сейчас я уже ни на что не гожусь.
wait 5
Эм закашлялся
wait 5
mecho Лицо раненого исказилось болью. Говорить ему становится все тяжелее...
wait 30
say Командира нашего найди.
say Завалило его, в башне при осаде. 
say Коли жив, то придумает что делать.
wait 10
mecho Видимо воину стало совсем невмоготу и он, закрыв глаза, затих.
calcuid room2 20448 room
attach 20433 %room2.id%
detach 20432 %self.id%
~
#20433
лоадимдесятнега~
2 c 100
разобрать~
if !(%arg.contains(обломки)%) 
  wsend %actor% Что это Вы хотите разобрать?
  return 1
  halt
else
  wait 1
  wsend %actor% Усердно разбрасывая обломки крыши.
  wechoaround %actor% %actor.name% начал%actor.g% усердно разбрасывать обломки крыши.
  wait 5
  wecho Вскоре из-под них показалось тело еле живого командира.
  wload mob 20422
end
detach 20433 %self.id%
~
#20434
десятникговорит~
0 n 100
*~
wait 20
Эм захрипел
wait 10
say спас...
wait 10
эм закашлялся на полуслове
wait 25
say Спасибо, но моя жизнь уже ничего не стоит. 
say Единственное что еще можно сделать для города, это спасти летопись, которая велась от его основания.
wait 5
say Князь держал ее у себя в читальне. Найди ее и передай писчику в Китеж-граде.
wait 10
mecho Через мгновенье десятник потерял сознание.
calcuid room3 20492 room
attach 20435 %room3.id%
~
#20435
беремлетопись~
2 c 100
искать~
if !(%arg.contains(летопись)%) 
  wsend %actor% Что это Вы хотите найти?
  return 1
  halt
else
  wait 1
  wsend %actor% Вы начали шарить по полкам и стеллажам заполненным книгами.
  wechoaround %actor% %actor.name% начал%actor.g% лихо шарить по полкам и стеллажам, заполненными книгами.
  wait 5
  wecho Вдруг, откуда-то выкатился свиток.
  wload obj 20407
  detach 20435 %self.id%
end
~
#20436
осетрлекается~
0 k 0
*~
mecho Осетр чуть поднырнул, и, заглотнув немного лечебной для него влаги, всплыл на поверхность.
dg_cast 'исцеление' %self.name%!
~
#20437
глушимрыбу~
0 c 100
глушить~
if !(%arg.contains(рыбу)%) 
  msend %actor% И кого Вы хотите оглушить?
  return 1
  halt
else
  wait 1
  msend %actor% Вы с размаху шлепнули по воде.
  mechoaround %actor% %actor.name% с размаху шлепнул%actor.g% по воде.
  wait 5
  Mecho Осетр стал несколько более рассеянным.
  detach 20436 %self.id%
  exec 20438 %self.id%
  attach 20403 %self.id%
  detach 20437 %self.id%
~
#20438
осетр~
0 z 100
*~
wait 40s
detach 20403 %self.id%
attach 20437 %self.id%
attach 20436 %self.id%
~
#20439
баскакпомер~
0 f 0
~
eval rand %random.1000%
if ((%world.curobjs(20408)% < 4) && (%rand% <= 100))
  mload obj 20408
end
if ((%world.curobjs(20409)% < 4) && (100 <= %rand%) && (%rand% < 250))
  mload obj 20409
end
~
#20440
шаманродился~
0 n 100
*~
eval rand %random.1000%
if ((%world.curobjs(20410)% < 4) && (%rand% <= 80))
  mload obj 20410
end
if ((%world.curobjs(20416)% < 4) && (100 <= %rand%) && (%rand% < 200))
  
  mload obj 20416
end
~
#20441
басурманинродился~
0 n 100
*~
eval rand %random.1000%
if ((%world.curobjs(20412)% < 4) && (%random.1000% <= 100))
  mload obj 20412
end
if ((%world.curobjs(20415)% < 4) && (100 <= %rand%) && (%rand% < 200))
  
  mload obj 20415
end
~
#20442
вдовародился~
0 n 100
*~
if ((%world.curobjs(20411)% < 3) && (%random.1000% <= 80))
  mload obj 20411
end
~
#20443
крыспомер~
0 f 100
*~
if (%world.curobjs(20413)% < 4) && (%random.1000% <= 250)
  mload obj 20413
end
~
#20444
рыбкапомер~
0 f 100
*~
if (%world.curobjs(20414)% < 4) && (%random.1000% <= 80)
  mload obj 20414
end
~
#20445
элитародился~
0 n 100
*~
if (%world.curobjs(20417)% < 4) && (%random.1000% <= 150)
  mload obj 20417
end
~
$~
