#29500
Ходим по болоту (восток)~
2 c 100
e ea eas east  в во вос вост восто восток~
if !%actor.haveobj(29520)%
  wsend %actor% Вы сами не заметили, как заблудились.
  eval tele %random.12%
  eval tele1 %tele%+29524
  wteleport %actor% %tele1%
  halt
end
wsend %actor% Золотое яичко закрутилось, указывая Вам путь.
wforce %actor.name% восток
~
#29501
Соглашаемся с Еремеем~
0 d 0
да слушаю послушаю помогу говори~
wait 1s
г Ну что ж, слушай, коли охота.. Может, и в правду поможешь.
г Жил я счастливо с молоду, дом построил крепкий, дерево посадил, трех дочерей воспитал.
wait 1s
вздох
wait 1s
г Красавицу Настасью, сплетницу Малафью, да умницу Марью.
wait 1s
г Собрался как-то на ярмарку свой товар показать, да на людей посмотреть.
г Позвал дочерей и говорю : "Дочери мои любимые, хорошие да пригожие, что вам на ярмарке купить?"
г Старшая Настасья говорит : "Купи мне, батюшка, сарафан шелку лазоревого, да такой, чтобы соседские девки от злости сдохнули."
г Средняя Малафья говорит : "Купи мне, батюшка, бусы дорогие, да такие, чтобы подруженьки с зависти лопнули."
г А младшая молвит : "А купи мне, батюшка, аленький цветочек, да краше которого нету на белом свете."
г Подивился я, покачал головой, покачал головой, да делать нечего - поехал.
wait 1s
г На ярмарке сарафан да бусы купил я, а вот цветочек найти не мог.
г Искал, искал я его, да уж совсем отчаялся, как встретил на дороге старичка, да с цветком аленьким в руке.
г Да и краше этого цветка нет во всем мире.
г Говорю ему : "Не продашь ли мне, дедушка, цветочек аленький?"
г Ответил дедушка : "Не продажный он, а заветный, коли дочь твоя пойдет за муж за сына моего, Финиста - ясна сокола, то бери цветок."
wait 1s
вздых
г Подумал я, попечалился, а делать нечего, согласился.
г Приехал домой, рассказал все доченьке, она и молвила : " Быть тому, как суждено, выйду я замуж."
г Да с тех пор прям расцвела, ходила веселая, места себе от счастья не находила.
wait 1s
улы
wait 2s
г Но вот совсем недавно видно случилось что-то - ходит она чернее тучи, того и гляди, из дому убежит.
г Поговори ты с ней, может поведает она тебе тайну свою.
просит %actor.name% 
detach 29533 %self.id%
detach 29501 %self.id%
~
#29502
входим к Настасье~
0 q 100
~
if ( %actor.sex% == 1 )
  г Ой, заходи, добрый молодец.
  wait 5
  смущ
  wait 5
  г Гостем будешь.
  улы
  wait 5
  г А может ты свататься пришел, жениться хочешь ?
  улы
  прихор
  wait 5
  г А что, я не против.
  игра %actor.name%
  wait 1s
  г А может ты мне в подарок и камушек какой принес?
  улы
  вопр %actor.name%
elseif ( %actor.sex% == 2 ) 
  г Ты что здесь делаешь, поганка?
  руг
  wait 1s
  г Женихов моих уводишь?
  вопр %actor.name%
  wait 1s
  г Ну погоди, я тебя проучу !
  убить %actor.name%
end
~
#29503
Входим к Малафье~
0 q 100
~
г Ой, привет-привет.
улы
wait 1s
г Посплетничать хочешь ? Я тебе такого порасскажу, такого порасскажу.
хих
wait 5
switch %random.20%
  case 1
    г Хочешь, про Урика расскажу ?
    вопр
    wait 1s
    г Говорят, раньше красавец-мужчина был, всем девицам угождал.
    хих
    wait 1s
    г Даже к моей сестрице захаживал, бывало.
    wait 1s
    г А сейчас...
    wait 1s
    ужас
    wait 1s
    г Говорят колдунья злая превратила его в старуху страшную, да мечом вооружила...
    wait 1s
    г За то что дочка ее сбежала с ним из дому..
    вздых
  break
  case 2
    г Хочешь, про Ромеса тебе поведаю ?
    вопр
    wait 1s
    г Парень знатный, на берегу реки великой живет, говорят.
    wait 1s
    г С девицамм гуляет, да напитки хмельные пьет.
    wait 1s
    вздых
    г Сама бы за него замуж пошла, да найти его теперь никак нельзя.
    мечта
  break
  case 4
    г Про Игвоведа, воеводу знатного, не хочешь ли послушать ?
    wait 1s
    г Знающие люди мне поведали, что живет он в замке своем затворником, ждет своего часа.
    дум
    wait 1
    г Совсем дураком стал... А впрочем, всегда таким он был.
  break
  case 5
    г Про Кайнура тебе рассказать ?
    вопр
    wait 1s
    г Кайнур, купчина знатный, у моего отца товар часто прям из под носа уводит.
    вздых
    wait 1s
    г За то отец его не любит, да и нам общаться с ним не велит.
    вздых
    wait 1s
    г А мы не больно-то и хотим.
    дум
    wait 1s
    шепта %actor.name% Коли увидишь его, поцелуй от меня, да скажи что жду его у старого дуба.
    смущ
  break
  case 6
    г Котенка, чернокнижница темная, тебя интересует?
    вопр
    wait 1s
    г Ну что ж, и про нее расскажу.
    дум
    wait 1s
    г Говорят, необычайной красоты девица, да может волшебная та красота?
    вопр
    wait 1s
    г Ну как бы то не было, а все равно, любого парня она приворожить может.
    завид
  break
  case 7
    г Лойнис, парень знатный, тебя интересует?
    вопр
    wait 1s
    г Ничего про него люди плохого не говорят, даже странно.
    дум
    wait 1s
    г Говорят, друг хороший и верный, из любой беды выручит.
    улы
    wait 1s
    г А так оно или нет, тебе решать..
  break
  case 8
    г Про Бельбога, Стрибога и других богов тебе поведать?
    вопр
    wait 1s
    ужас
    wait 1s
    г Что-ты, как же можно ?
    wait 1s
    г Запретная то тема, зачем вспоминать всуе?
    крест
  break
  default
    г А хотя нет, не хочу я сейчас языком чесать, дел много.
    улы
  done
~
#29504
Входим к Марье~
0 q 100
~
wait 1s
плак
wait 1s
if ( %actor.sex% == 1) 
  г Зачем ты здесь, добрый молодец? 
elseif ( %actor.sex% == 2 )
  г Зачем пришла ты, красна девица?
end
wait 1s
г Ой, прости меня за неучтивость мою.
wait 1s
г Просто горе у меня случилось, не знаю что и делать.
wait 1s
вздых
~
#29505
Спрашиваем Марью~
0 d 0
расскажи говори какое~
вздых
wait 1s
г Была я замужем за Финистом - Ясным соколом.
wait 1s
г Счастливо жила, жених мой ласковый во всем мне угождал, всякое мое желание исполнял.
wait 1s
г Захочу ли на бал, в платье красивом, в карете золотой, или царевной заморской решу оборотиться - все выполнит любимый.
wait 1s
г Подарил он мне перышко из крыла своего, коли его в левую руку возьмешь, да взмахнешь им, то всякое твое желание сбудется.
плак
wait 1s
г Подсмотрели однажды сестры, за тем как я перышком взмахнула, да и зависть их черная съела.
wait 1s
г Подождали пока я усну, да понатыкали в окно ножей острых, да гвоздей ржавых.
wait 1s
г Прилетел ко мне Финист, да обрезался весь, изранился, вскрикнул нечеловеческим голосом :
г "Улетаю я от тебя, коли хочешь найти меня, износи две пары свинцовых сапог, изломай два свинцовых посоха, да два хлеба свинцовых изглодай."
плак
wait 1s
г Проснулась я, да поздно уже было, улетел мой суженный.
плак
wait 1s
г Собираюсь я в путь-дороженьку тяжкую, да боюсь отца покинуть, как же он без меня проживет-то?
плак
attach 29506 %self.id%
detach 29504 %self.id%
detach 29505 %self.id%
~
#29506
Предлагаем Марье помощь~
0 d 0
помогу~
дум
г Нет, никак нельзя тебе за меня нести ношу тяжкую.
wait 5
дум
г А хотя..
wait 1s
г Коли поможешь ты мне, награжу я тебя...
г Только верни мне Финиста.
wait 1s
г Слушай же что сделать надо.
г Говорят, где-то в лесах темных живет кузнец великий, он и скует тебе посохи, да сапоги, да хлеба свинцовые.
wait 1s
дум
г Он же тебе и поведает куда идти дальше.
г Дам я тебе перышко Финиста, возьми его в левую руку, взмахни, да пожелай помочь.
wait 1s
г Перенесет оно тебя мгновенно куда нужно.
крест %actor.name%
wait 1s
г Иди же, в добрый путь!
mload obj 29511
дать пер %actor.name%
detach 29506 %self.id%
~
#29507
Махаем пером~
1 c 1
желаю~
if (!%arg.contains(помочь)%)
  osend %actor% Не для того это перо у вас, а для того чтоб помочь Марье.
  halt
end
osend %actor% Вы взмахнули перышком, пожелали помочь Марье, и, внезапно, взлетели в воздух, потеряли сознание.
wait 10
osend %actor% Когда Вы пришли в себя, то поняли, что находитесь в незнакомом Вам лесу.
oteleport %actor% 29515
opurge %self%
~
#29508
убили лису~
0 f 0
~
if (%world.curobjs(29512)% < 6 ) && ( %random.6% < 2 )
  mload obj 29512
end
~
#29509
убили волка~
0 f 0
~
if ( %world.curobjs(29513)% < 6) && ( %random.6% < 2 )
  mload obj 29513
end
~
#29510
зашли к волку~
0 r 100
~
wait 1
if ( %random.5% == 1 )
  msend %actor% Серый волк ощерился в зверином оскале - того и гляди, бросится на вас.
end
~
#29511
зашли к лисе~
0 r 100
~
wait 1
if ( %random.5% == 1 )
  msend %actor% Лисичка, рыжая сестричка, настороженно подняла пушистый хвост, посмотрела черными глазами-бусинками, и продолжила охоту.
end
~
#29512
Заходим к кузнецу в лесу~
0 q 100
~
улы
wait 1s
if ( %actor.sex% == 1) 
  г По здорову, путник, ждал я тебя.
elseif ( %actor.sex% == 2 )
  г По здорову, красна девица, ждал я тебя.
end
wait 1s
г Напели мне сороки, что взял%actor.u% ты помочь Марье, дочери купеческой в ее беде.
г Ну что ж хорошее это дело.
улы
wait 1s
г От меня ты посохи, да сапоги, да хлеба свинцовые хочешь ?
вопр
wait 1s
г Хотя, чего я спрашиваю, и так знаю.
г Да только свинца у меня нету..
вздых
wait 1s
г Правда, за тысячу кун я может и поискал бы чего-нибудь.
хитр
~
#29513
Дали кузнецу деньги~
0 m 0
1000~
wait 1
if %amount% < 1000
  say Маловато будет!
  give %amount% кун %actor.name%
  halt
end
wait 1
ухм
г Да, пожалуй и поищу я свинца.
wait 1s
msend %actor% Кузнец ушел в свой дом, и вскоре звонкие удары молота раздались оттуда.
wait 15
г Ну вот, все готово.
wait 1s
г Дам я тебе пару советов 
г Дороженька, по которой идти тебе предстоит, начинается где-то в моем лесу.
улы
wait 1s
г Одень сапоги, в правую руку возьми посох, в левую хлебец да встань на нее.
г И не заметишь, как окажешься там, куда тебе и нужно..
wait 1s
пока %actor.name%
г Иди же, счастливого пути.
mload obj 29514
mload obj 29514
mload obj 29515
mload obj 29515
mload obj 29516
mload obj 29516
дать все.свин %actor.name%
бро все
detach 29512 %self.id%
detach 29513 %self.id%
~
#29514
вошли на первую дорожку~
2 e 100
~
eval xleb %actor.eq(17)%
eval posox %actor.eq(16)%
eval sapi %actor.eq(8)%
if ((%xleb.vnum% == 29516) && (%posox.vnum% == 29515) && (%sapi.vnum% == 29514))
  wsend %actor% Вот Вы и начали свой долгий путь.
  wait 5
  wsend %actor% Год бродили, другой ходили.
  wait 3
  wsend %actor% Много приключений пережили.
  wait 3
  wsend %actor% Но постепенно сапоги на Вас продырявились, посох сломался, хлеб изгрызли Вы.
  wait 3
  wsend %actor% И нежданно-негаданно дорожка закончилась, и вывела Вас на какое-то болото.
  wait 3
  wsend %actor% На самом краю которого стоит избушка на курьих ножках.
  wteleport %actor% 29516
  calcuid 1xleb 29516 obj
  wpurge %1xleb%
  calcuid 1sapi 29514 obj
  wpurge %1sapi%
  calcuid 1posox 29515 obj
  wpurge %1posox%
end
~
#29515
Входим в избушку~
2 c 0
войти зайти~
if !%arg.contains(в)%
  wsend %actor% Куда Вы войти пытаетесь?
  halt
end
wsend %actor% Вы закричали: Избушка-избушка повернись ко мне передом, к лесу задом, мне в тебя лезть и хлеба есть.
wsend %actor% Тут же заскрипела избушка, повернулась к Вам и вошли Вы внутрь.
wteleport %actor% 29539 
wsend %actor% А там Баба Яга лежит на печи, на девятом кирпиче.
wait 3
wsend %actor%  Баба Яга прошипела : 'Фу-фу-фу, раньше русского духу было слыхом не слыхать, видом не видать, а теперь русский дух по свету шляется, в нос бросается.'
wait 3
if (%actor.sex% == 1)
  wsend %actor% Баба Яга прошипела : 'Впрочем, вижу я, беда у тебя случилась, добрый молодец, расскажи, может и помогу тебе чем, а то стара я уж стала вредить людям, пора и помогать.'
elseif (%actor.sex% == 2) 
  wsend %actor% Баба Яга прошипела : 'Впрочем, вижу я, беда у тебя случилась, красна девица, расскажи, может и помогу тебе чем, а то стара я уж стала вредить людям, пора и помогать.'
end
~
#29516
Рассказываем старухе~
0 c 0
рассказать поведать~
msend %actor% Вы начали рассказывать старухе о горе Еремеевском
wait 1s
msend %actor% Задумалась Баба Яга, да и молвила.
wait 1s 
г Ну что ж, помогу я тебе - знаю я Еремея, хороший человек он, добрый.
улы
г За тем болотом, что на юге простирается, начинается дорога к царству дальнему, заморскому.
г Говорят люди, царевна тамошняя собралась замуж выходит за красавца писанного, Финистом которого кличут.
дум
г Быть может тот это Финист, которого ты ищешь ?
wait 1s
г Да не пустит она тебя просто так к нему.
дум
wait 1s
%echo% Баба Яга почесала голову, и начала рыться в сундуке.
wait 2s
г А вот, нашла.
%echo% Старуха достала золотое зеркальце из сундука.
wait 1s
mload obj 29518
дать все %actor.name%
г Скоро у них в королевстве будет ярмарка, так царевна обязательно там будет наряды себе выбирать.
wait 1s
г Как увидишь ее, дай вот это зеркальце.
г Захочет она его у тебя выкупить.
г Но ты не соглашайся, тверди только - Финиста увидеть, Финиста увидеть.
г Поломается она, да придется согласится.
wait 1s
г Да только может обмануть она тебя, опоить Финиста зельем колдовским, он и не проснется.
г Вот возьми слезу девичью, капни ее на Финиста, сразу он про Марью вспомнит.
mload obj 29519
д все %actor.name%
wait 1s
г А чтоб по болоту пройти без опаски, дам тебе яичко золотое.
г Ты его неси, и пропустит тебя жижа болотная, а иначе смерть верную найдешь там.
улы
mload obj 29520
д все %actor.name%
wait 1s
г Все иди же. Счастливого пути!
calcuid room 29516 room
detach 29515 %room.id%
attach 29517 %room.id%
detach 29516 %self.id%
~
#29517
Входим в избушку (квест взят)~
2 c 0
войти~
if !%arg.contains(в)%
  wsend %actor% Куда Вы войти пытаетесь?
  halt
end
wsend %actor% Вы закричали: Избушка-избушка повернись ко мне передом, к лесу задом, мне в тебя лезть и хлеба есть.
wsend %actor% Тут же заскрипела избушка, повернулась к Вам и вошли Вы внутрь.
wteleport %actor% 29539 
wsend %actor% А там Баба Яга лежит на печи, на девятом кирпиче
~
#29518
выходим из избушки~
2 c 0
выйти~
if !%arg.contains(наружу)%  
  wsend %actor% Куда Вы хотите выйти?
  halt
end
wsend %actor% Вы покинули избушку на курьих ножках.
wteleport %actor% 29516
wait 3
wsend %actor% Только вы ступили за порог, как избушка повернулась к вам задом, к лесу передом.
~
#29519
Ходим по болоту(юг)~
2 c 0
s so sou sout south ю юг~
if !%actor.haveobj(29520)%
  wsend %actor% Вы сами не заметили, как заблудились.
  eval tele %random.12%
  eval tele1 %tele%+29524
  wteleport %actor% %tele1%
  halt
end
wsend %actor% Золотое яичко закрутилось, указывая Вам путь
wforce %actor.name% юг
~
#29520
reset~
2 f 100
~
calcuid mob 29500 mob
attach 29533 %mob.id%
attach 29501 %mob.id%
calcuid mob 29506 mob
attach 29504 %mob.id%
attach 29505 %mob.id%
calcuid mob 29508 mob
attach 29512 %mob.id%
attach 29513 %mob.id%
calcuid room 29516 room
detach 29517 %room.id%
attach 29515 %room.id%
calcuid mob 29511 mob
attach 29516 %mob.id%
calcuid mob 29517 mob
attach 29526 %mob.id%
calcuid room 29558 room
attach 29528 %room.id%
attach 29529 %room.id%
~
#29521
Зашли в топь~
2 e 100
~
if %actor.haveobj(29520)%
  wsend %actor% Вы, как завороженный, начали идти вперед, в смертельную топь, но тут золотое яичко начало нагреваться и освободило вас от морока.
  halt
end
wsend %actor% Топь начала засасывать Вас. Нужно быстрее уходить отсюда.
wait 1
wteleport %actor% 29540
~
#29522
Ходим по болоту (север)~
2 c 0
с се сев севе север n no nor nort north~
if !%actor.haveobj(29520)%
  wsend %actor% Вы сами не заметили, как заблудились.
  eval tele %random.12%
  eval tele1 %tele%+29524
  wteleport %actor% %tele1%
  halt
end
wsend %actor% Золотое яичко закрутилось, указывая Вам путь.
wforce %actor.name% север
~
#29523
Ходим по болоту (запад)~
2 c 0
w we wes west з за зап запа запад~
if !%actor.haveobj(29520)%
  wsend %actor% Вы сами не заметили, как заблудились.
  eval tele %random.12%
  eval tele1 %tele%+29524
  wteleport %actor% %tele1%
  halt
end
wsend %actor% Золотое яичко закрутилось, указывая Вам путь.
wforce %actor.name% запад
~
#29524
убили кикимору~
0 f 0
~
if ( %world.curobjs(29521)% < 6) && ( %random.6% < 2 )
  mload obj 29521
end
~
#29525
убили водяника~
0 f 0
~
if ( %world.curobjs(29522)% < 4) && ( %random.6% < 2 )
  mload obj 29522
end
~
#29526
даем Иссени зеркальце~
0 j 100
~
wait 1s
if %object.vnum% != 29518
  дум
  г Нет, это меня не интересует.
  wait 7 
  морщ
  бро все
  halt
end
г Ой, какое зеркальце.
wait 7
эм запрыгала от радости
г Что ты хочешь за него ?
вопр
wait 5
attach 29527 %self.id%
~
#29527
говорим что финиста хотим увидить!~
0 d 0
увидеть~
г ЧТО ??
хмур
wait 5
г Финиста, мужа моего будущего увидеть хочешь ?
wait 5
дум
г А, хотя, согласна. 
wait 5
эм хитро улыбнулась
wait 5
г Стража, отвести к Финисту!
wait 5
mteleport %actor% 29558
detach 29526 %self.id%
detach 29527 %self.id%
~
#29528
будим финиста~
2 c 0
р ра раз разб разбу разбуд разбуди разбудит разбудить~
wsend %actor% Вы безуспешно старались разбудить Финиста - он спит колдовским сном.
~
#29529
мочим финиста слезой~
2 c 0
капнуть оросить окропить~
if (!%arg.contains(слез)%)
  wsend %actor% Чем капнуть?
  halt 
end
if !%actor.haveobj(29519)%
  wsend %actor% Так у вас же нет слезы девичьей..
  halt
end
wsend %actor% Вы капнули слезой девичьей на Финиста-ясна сокола.
wsend %actor% Сразу же встал он с постели, потянулся, да уставился на Вас.
wait 5
wsend %actor% Финист сказал : 'Ты кто?'
wait 5
wsend  %actor% Финист сказал : 'Что ты здесь делаешь? Ничегошеньки не помню я, расскажи-ка.'
if %exist.mob(29523)%
  calcuid finist 29523 mob
  wpurge %finist.id% 
  wload mob 29524
end
detach 29528 %self.id%
detach 29529 %self.id%
~
#29530
рассказываем финисту~
0 c 0
рассказать поведать~
msend %actor% Вы начали рассказывать Финисту о случившейся беде.
wait 5
msend %actor% Лицо Финиста становилось все мрачнее и мрачнее.
wait 5
msend %actor% Только Вы дошли до конца рассказа, молвил Финист :
msend %actor% 'Кого же мне выбрать, ту что столько натерпелась ради меня, или ту, что зельем колдовским опоила, силой на себе чуть не женила?'
wait 5
msend %actor% Подумал-подумал Финист и решил : 'Сейчас же возвращаемся мы к Марье - я за любовью своею, а ты за наградой положенной.'
mteleport %actor% 29508
if %exist.mob(29506)% 
  calcuid maria 29506 mob
  attach 29531 %maria.id%
end
mpurge %self.id%
~
#29531
награждает марья :)~
0 r 100
~
wait 1
г Спасибо тебе, знала я, что положиться на тебя можно.
wait 5
г  Счастлива я теперь с моим суженным.
wait 5
г  Вот тебе от меня награда.
цел %actor.name%
смущ
%actor.exp(+100000)%
%send% %actor% Вы почувствовали себя немножко опытней.
if ( %world.curobjs(29522)% < 6) && ( %random.3% < 2 )
  mload obj 29528
  г А вот от Финиста награда тебе.
  wait 5
  д наруч %actor.name%
  бро все
end
detach 29531 %self.id%
~
#29532
вошли на вторую дорожку~
2 e 100
~
eval xleb %actor.eq(17)%
eval posox %actor.eq(16)%
eval sapi %actor.eq(8)%
if ((%xleb.vnum% == 29516) && (%posox.vnum% == 29515) && (%sapi.vnum% == 29514))
  wsend %actor% Вот Вы и начали свой долгий путь.
  wait 5
  wsend %actor% Год бродили, другой ходили.
  wait 3
  wsend %actor% Много приключений пережили.
  wait 3
  wsend %actor% Но постепенно сапоги на Вас продырявились, посох сломался, хлеб изгрызли Вы.
  wait 3
  wsend %actor% И нежданно-негаданно дорожка закончилась, и вывела Вас к граду белокаменному.
  wteleport %actor% 29554
  calcuid 1xleb 29516 obj
  wpurge %1xleb%
  calcuid 1sapi 29514 obj
  wpurge %1sapi%
  calcuid 1posox 29515 obj
  wpurge %1posox%
end
~
#29533
входим к Еремею~
0 r 100
~
wait 1s
г Привет тебе.
wait 1s
улы
wait 1s
г Жил я раньше счастливо, да беда случилась, так что не до тебя сейчас мне.
wait 1s
вздых
г Да может послушаешь?
~
#29534
Даем камень~
0 j 100
~
wait 1
if %object.vnum% == 29530
  г Вот это камень так камень!
  г Всегда я о таком мечтала.
  %purge% %object%
  улы
  г Ты достоин награды.
  смущ
  цел %actor,name%
  %self.gold(+3000)%
  д 3000 кун %actor.name%
  if (%world.curobjs(29529)% < 3) && (%random.100% < 10)
    г А вот этот поясок забыл у меня воин славный.
    улы 
    смущ
    г Ну.. Когда одевался.
    хих
    mload obj 29529
    д пояс %actor.name%
  end
end
~
$~
