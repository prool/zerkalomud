#85100
ВошлиКСтряпчему~
0 q 80
~
wait 1s 
поклон
wait 1s 
say Добро пожаловать, гость дорогой!
switch %random.4%
  case 1
    mecho - На кухне горит огонь и чай еще теплый.
    mecho - Не желаете отведать?
  break
  case 2
    mecho - Хлеб у нас пекли вчера, так что отведайте свежего, лучшего во всем Ростове не делают!
  break
  case 3
    mecho - Попробуйте наш сыр!
    mecho - Изготавливаем из молока наших овец, он очень хорош, правда в этом году немного суховат.
  break
  default 
    halt
  break 
done
~
#85101
ВошлиКОнуфрию~
0 r 100
~
wait 1s
эм страстно перекрестился и очередной раз стукнулся лбом об пол
wait 1s
эм наконец поднял голову и обернулся на шум
wait 1s
if %actor.religion% == 0
  *если язычник 
  хмур %actor.name% 
  mecho _
  mecho _Схватив тяжелый посох стоявший неподалеку обеими руками, священник закричал:
  mecho __- Гореть тебе во геенне огненной, язычник!
  mecho _
  эм отвернулся и начал неистово креститься
  wait 2s
  mecho __- Защити нас, Господь, силой честной, животворящего твоего креста!
  mecho _
  %send% %actor% _Отец Онуфрий резко повернулся всем телом и гневно воззрился на Вас.
  %echoaround% %actor% Отец Онуфрий резко повернулся всем телом и гневно воззрился на %actor.rname%.
  mecho _
  mecho __- Я слуга Господа, и нет силы превыше его!!!
  mecho __- Что ты можешь мне сделать, нехристь сатанинская?!
else
  *христианин
  хмур %actor.name% 
  wait 1s
  mecho _Схватив тяжелый посох стоявший неподалеку обеими руками, священник закричал:
  wait 1s
  mecho __- Отринь пустые сомнения, %actor.name%!
  эм гневно скрестил брови, ударяя в пол тяжелым посохом
  wait 2s
  mecho __- Будь тверд в вере православной, прочь изгони страх и волнение!
  mecho __- Истинно тебе говорю, что всякий, кто призовет имя Господа, будет спасен!
  wait 1s
  mecho __- Смотри вперед смело и не убоись - Господь с нами!
  wait 1s
  mecho _Лик святого старца словно бы осветился внутренним светом. 
end
~
#85102
ВошлиКТорговцуСладостями~
0 q 30
~
wait 1s
%send% %actor% _Завидев Вас арабский торговец сладостями заметно оживился.
%echoaround% %actor% Завидев %actor.rname% арабский торговец сладостями заметно оживился.
wait 1s
mecho Вдруг на чистом русском наречии, араб весело закричал:
mecho _
if %actor.sex% == 1 
  mecho - Бери, добрый молодец, сладости - задаром отдаю, сам себя граблю!
else
  mecho - Бери, красна девица, сладости задаром отдаю, сам себя граблю!
end
mecho - За денарии, за дирхемы... могу и в ваших кунах взять...
mecho _
эм расплылся в улыбке
~
#85103
ОретБондарь~
0 q 60
~
wait 1s
mecho - Бочки, шайки! - завовопил Вам на ухо бондарь. 
wait 1s
mecho - Дны вставляем, обруча наставляем, стенки переставляем, все наново перебираем!!!
~
#85104
ОретЯрмарочныйЗазывала~
0 q 70
~
wait 1s
mecho _&YЯрмарочный зазывала закричал во весь голос:&n
mecho _
mecho - Эй, молодцы да молодицы, подходи, на меня погляди!
mecho - Городским и деревенским, местным и пришлым, ближним и дальним - наше почтение! 
поклон
wait 1s
mecho - Немцы-лекари и евреи-аптекари, люд православный и бесермены магометовы! 
mecho - Купцы, молодцы, ребята-удальцы - все сюда, на диво дивное подивиться! 
mecho - Здесь такие чудеса, что враз дыбом встанут власа, 
mecho - А вся ваша брада пропадет без следа! 
wait 1s
switch %random.4%
  case 1
    mecho - Вот ужасный амазон из земли индийской - ликом черен, нравом дик! 
    wait 1s
    mecho - Бревна подымает, огонь пожирает! 
    mecho - Рад бы и человека сожрать, да кто ж ему даст?
  break
  case 2
    mecho - Вот Ефимка Подкова - кузнец суздальский! 
    wait 1s
    mecho - Наибольший человек в целом мире! 
    mecho - Покуда круг него обходить будешь, уж вечер настанет!
  break
  case 3
    mecho - Девица Марушка, половчанка расписная! Ликом чудесна, а телом чудесней стократ √ на нем другие 
    wait 1s
    mecho - Лики намалеваны! Императоры заграничные, цареградские и латинянские! 
    mecho - Да и наши князья славные! 
    wait 1s
    mecho - За хорошую монету может и лик покойного Владимира показать!
    mecho - Аккурат на сидучем месте намалеван!
  break
  case 4
    mecho - Вот зверь ужасный, линиями покрытый, тигрусом индийским называемый!
    mecho - Усат и зубат - близко не стой, не будешь рад! 
  break
  case 5
    mecho - Вот заморский зверь-паук, без ног, без рук! 
    mecho - Не спит, не питается, а только улыбается!
  break
  default 
    halt
  break 
done
~
#85105
УченыйМедведь~
0 q 50
~
wait 1s
mecho _&YСкоморох Мирошка завопил во всю глотку:&n
mecho _&YЗаходи честной народ, куны плати - на медведя смотри!!!&n
wait 1s
mforce медведь эм начал плясать, кланяться, показывать разные трюки и шутки
wait 1s
switch %random.4%
  case 1
    say А ну-ко, Топтыга, покажи, как боярышня молодая поутру в зеркало глядится.
    wait 1s
    mforce медведь эм начал переминаться с ноги на ногу, трясти лапами перед лицом и сладко причмокивать губами
  break
  case 2
    say А покажи-ко, Топтыга, как теща зятьку блины пекла.
    wait 1s
    mecho _Это медведь показывал на настоящих блинах!
    wait 1s
    mforce медведь эм некоторое время возил блины по колоде, изображавшей печь со сковородой
    wait 1s
    mforce медведь эм вдруг свирепо заревел, затряс лапами и сожрал все блины сам
  break
  case 3
    say А покажи нам, Топтыга, как отец Онуфрий поутру в собор идет, службу служить.
    wait 1s
    mforce медведь эм вытянулся во весь рост, прижал лапы к бокам и зашагал, важно задрав подбородок
    mforce медведь эм время от времени поглядывал влево-вправо и сердито порыкивал, супя кудлатые брови
    mecho _Зрители торжествующе захохотали - строгого архиерея узнали сразу же.
  break
  case 4
    say А покажи, Топтыга, боярина Фому, после государственных дел утомившегося.
    wait 1s
    mforce медведь эм сразу же закачался из стороны в стороны, басовито загудел, словно напевая
    mforce медведь эм чуть покачался, а потом просто шлепнулся в грязь и засопел
    mecho _И снова раздался хохот - кто же не знает о горячей любви боярина и хмельной медовухи? 
    mecho _Жить друг без друга не могут!
  break 
  case 5
    say А покажи нам , Топтыга, нашего сердитого воеводу Самсона.
    wait 1s
    mforce медведь эм громогласно заревел, изображая воеводу Самсона, распекающего гридней
  break
  halt
break 
done
~
#85106
ВошлиКМальгину~
0 r 10
~
wait 1s
эм начал вереть перед Вашим носом какими-то потертыми сапогами
wait 1s
if %actor.sex% == 1 
  say Бери, добрый молодец, задаром отдаю, сам себя граблю!
else
  say Бери, красна девица, задаром отдаю, сам себя граблю!
end
wait 1s
mecho - Вот, глянь, какие сапоги - из Сираза пришли, да каблуков не стоптали! 
mecho - Железные каблуки-то, прочные! 
mecho - Красный бархат, вышивка золоченая, шнуры серебряные! 
mecho - Век носить, не сносить, для любой погоды хороши! 
mecho - Сам такие же ношу!
wait 1s
mecho _На ногах торговца действительно красовались почти такие же сапоги - только желтые.
~
#85107
ВошлиКСтарушкеТелепортеру~
0 q 100
~
*аналог ворожей в городах
wait 1s
ул %actor.name%
wait 1s
msend %actor% - Хожу я по залесью с утренней росой...
wait 1s
msend %actor% - Собираю я травы зельные,
msend %actor% - Варю травы зельные во медяной росе,
msend %actor% - Поливаю травами зельными по всем кустам, по всем полям, по всем межам.
msend %actor% - Дабы творились заклятия путь коротящие...
wait 1s
msend %actor% - Коли найдешь ингридиенты нужные,
msend %actor% - Помогу и тебе мигом добраться до желаемого русского города.
wait 1s
msend %actor% - Коли в Корсунь хочешь - принеси мне камушек и металл.
msend %actor% - Если добудешь камушек и грибы то, смогу отправить тебя в Новгород Великий.
msend %actor% - А захочешь в Брянск попасть - дай мне металл и грибы.
~
#85108
ВошлиКИванам~
2 e 70
~
wait 1s
wecho _Гостил поп Иван у Ивана княжьего сына во высоком терему.
wait 1s
wforce княжий г Эх, отец мой, батюшка Иван - гость дорогой!
wforce княжий эм налил в чарки зелена вина
wait 1s
wecho _Отвечал захмелевший поп Иван княжьему сыну:
wforce поп г Эх, Иван княжий сын, давай судить-рядить, как царству русскому быть!
wforce княжий эм заткнув ладонь за кушак, играл насыпным перстнем
wait 1s
wforce поп эм тихо пошевеливал торчащим из под рясы хвостом
wait 1s
wforce княжий ухм
wait 1s
wforce княжий г Приходи вчера!
wforce княжий ул
~
#85109
ВстречаетКузнец~
0 q 100
~
wait 1s
mecho _Тихо зазвенел колокольчик на двери, когда Вы вошли в кузню.
mecho _
эм поднял голову на шум
wait 1s
г Заходи, заходи! На товары гляди!
switch %random.8%
  case 1
    mecho - Вот мечи новехонькие! 
    mecho - Самим кладенцам вровень будут!
  break
  case 2
    mecho - Вот луки разрывчатые!
    mecho - В птицу в небесах из самой Нави попадешь!
  break
  case 3
    mecho - Вот шеломы знатные!
    mecho - В такой броне сам Кащей Бессмертный ходил!
  break
  case 4
    mecho - Вот кольчуги булатные!
    mecho - Ни один ворог ее не пробьет!
  break
  case 5
    mecho - Вот копья острые!
    mecho - В самое сердце ворога разят!
  break
  default 
  break 
done
wait 1s
mecho - А может какой заказ есть?
mecho - Все в срок сделаем!
~
#85110
ЗаказываемКузнецу~
0 d 0
*~
wait 1s
switch %speech%
  ************************************************
  case мешок
    if %actor.quested(85001)%
      say Какой мешок-то? Ты меня с портным не перепутал?
      wait 1s
      mforce %actor.name% нет
      mforce %actor.name% г Надобно большой мешок, чтоб медведь влез...
      mforce %actor.name% г Да чтоб из железных нитей был.
      wait 1s
      эм удивился
      wait 1s
      mecho - Странный заказ, ну да ничего - выполню!
      *квест стафина должна быть уникальна. 
      *если она уже есть в мире тормозим пока кто-то не доделает квест
      if %world.curobjs(85008)% > 0
        mecho - Только сейчас железа у меня нет. 
        mecho - Приходи попозже, завтра например - утречком.
      else
        mecho - Триста кун с тебя за мешок такой!
        attach 85111 %self.id%
      end
    else
      say Какой мешок еще? Я тебе что портной?!
    end
  break
  *************************************************
  case колпак
    if %actor.quested(85001)%
      эм удивился
      wait 1s
      mecho - Какой такой колпак? Ну-ка опиши мне его!
      wait 1s
      mforce %actor.name% г Ну такой...
      %send% %actor% Вы в раздумьях начали чесать затылок.
      %echoaround% %actor% %actor.name% нача%actor.q% чесать затылок.
      wait 1s
      mforce %actor.name% г Колпак из трех разных металлов - железа, меди и олова - все поровну.
      mforce %actor.name% г Спереди дырки надобно для глаз и носа.
      mforce %actor.name% г Да чтоб сидел плотно!
      wait 1s
      mecho - Да-а-а, непростую задачку-то ты зада%actor.q%...
      дум
      эм подозвал подмастерий и они начали о чем-то спорить
      wait 3s
      mecho _Наконец спор был окончен и кузнец выступил вперед.
      кив
      if %world.curobjs(85007)% > 3
        mecho - Только сейчас олова у меня нет. 
        mecho - Приходи попозже, завтра например - утречком.
      else
        mecho - Мы скуем тебе такой колпак, но стоить это будет тебе пятьсот кун.
        mecho - Металл нынче дорогой!
        attach 85112 %self.id%
      end
    else
      say Какой такой колпак? Незнаю никаких колпаков!
      хму
    end
  break
  case заказ
    say Ты говори что именно надобно!
  break
  case есть
    say Ты говори что именно надобно!
  break
  case да
    say Ты говори что именно надобно!
  break
done
~
#85111
ДалиКуныКузнецу1~
0 m 0
~
if (%amount% < 300)
  say Тебе же сказали тыщу кун!
  дать %amount% кун %actor.name%
  halt
end
if (%amount% > 300)
  say Больше чем сказал я не возьму!
  дать %amount% кун %actor.name%
  halt
end
if  (%amount% == 300)
  эм пересчитал куны
  wait 1s
  кив
  wait 1s
  say Теперь подожди, работа твоя непростая!
  эм созвал подмастерий и погрузился в работу
  wait 5s
  эм наконец закончил и вытер пот со лба
  wait 1s
  г Вот мешок твой, держи!
  mload obj 85008
  дать мешок %actor.name%
  detach 85111 %self.id%
~
#85112
ДалиКуныКузнецу2~
0 m 0
~
if (%amount% < 500)
  say Тебе же сказали две тыщи кун!
  дать %amount% кун %actor.name%
  halt
end
if (%amount% > 500)
  say Больше чем сказал я не возьму!
  дать %amount% кун %actor.name%
  halt
end
if  (%amount% == 500)
  эм пересчитал куны
  wait 1s
  кив
  wait 1s
  say Теперь подожди, работа твоя непростая!
  эм созвал подмастерий и погрузился в работу
  wait 5s
  эм наконец закончил и вытер пот со лба
  wait 1s
  г Вот колпак твой, держи!
  mload obj 85007
  дать колпак %actor.name%
  detach 85112 %self.id%
~
#85113
ВошлиККириллу~
0 r 100
~
wait 1s
эм тяжело вздохнул
switch %random.8%
  case 1
    г Господи, помоги рабу твоему Кириле скоро писать!
  break
  case 2
    эм потер рукавом и без того воспаленные глаза
    г Ох, уж очи спать хотят...
  break
  case 3
    эм закончил очередной документ и теперь с удовольствием его рассматривает
    wait 1s
    mecho Вдруг цепкий взгляд его наткнулся на ошибку.
    эм недовольно сморщился
    wait 1s
    г Эх, была не была - так отдам! 
    г Не клените за ошибки неволей сделаны...
  break
  case 4
    эм оторвался от бумаг
    wait 1s
    г Сести трапезничать что ли? Клюква с салом, да рыбьем жиром...
    эм жадно покосился на поднос на соседнем столе
  break
done
~
#85114
УбитьОнуфрию~
0 d 0
убить~
if %actor.religion% == 0
  *еслит язычник
  wait 1s 
  эм расхохотался
  wait 1s
  say И всего-то?!
  wait 1s
  mecho - Пугай сим служек своих поганых - истино верующему смерть не страшна!!!
  mecho - Тело лишь грешная тленная оболочка. Муки плоти - ничто!
  mecho - А дух мой крепче стены каменной!
  эм опять начал креститься
  wait 1s
  mecho - Да воскреснет Бог, да расточатся враги его!!!
  wait 1s
  %send% %actor% Священик пошел прямо на Вас выставив впереди себя тяжелый крест.
  %echoaround% %actor% Священик пошел прямо на %actor.rname% выставив впереди себя тяжелый медный крест.
  wait 1s
  mecho Но, спотыкнувшись о порог, выронил крест и неуклюже шлепнулся на пол.
  *лоад креста
  if (( %random.1000% < 310 ) && ( %world.curobjs(85118)% < 3 ))
    mload obj 85118
  end 
  бро крест
else
  руг %actor.name%
  mecho - Ты что же это богохульствуешь?!
  wait 1s
  mecho - А ну, живо, на колени - грехи замаливать, православный!
  mecho - А то, быстро научу уму-разуму!
  эм угрожающе поднял над головой тяжелый посох
end
detach 85114 %self%
~
#85115
ДалиДеньгиМедведю~
0 m 0
~
wait 1s
поклон
wait 1s
эм размахивая лапами прорычал что-то невразумительное и непереводимое
wait 1s
mforce мирошка г Это, он так благодарит тебя, %actor.name%!
mforce мирошка ул
~
#85116
ДалиДеньгиМирошке~
0 m 0
~
wait 1s
поклон
wait 1s
г Спасибо за награду трудов наших!
wait 1s
mforce медведь эм от всей души вылизал %actor.vname% от пят до носа
~
#85117
ПоютПевчие~
2 e 100
~
wait 1s
wecho _Едва войдя, Вы погрузились в тяжелую атмосферу чадящих свечей и торжественной тишины.
wecho _В церкви полным ходом шел какой-то обряд.
wait 1s
wecho _Вдруг разорвалась тишина и под купол церкви вознеслись высокие голоса церковных певчих.
wait 2s
switch %random.5%
  case 1
    *1 молитва
    wecho - Ангеле Божий, хранителю мой святый,
    wecho - Живот мой соблюди во страсе Христа Бога,
    wecho - Ум мой утверди во истиннем пути,
    wecho - И к любви горней уязви душу мою,
    wecho - Да тобою направляемь,
    wecho - Получу от Христа Бога велию милость.
  break
  case 2
    *2 молитва
    wecho - Благослови, душе моя, Господа,
    wecho - Благословен еси, Господи
    wait 1s
    wecho - Благослови, душе моя, Господа
    wecho - И вся внутренняя моя, Имя святое Его.
    wait 1s
    wecho - Благослови, душе моя, Господа
    wecho - И не забывай всех воздаяний Его:
    wecho - Очищающаго вся беззакония твоя,
    wecho - Исцеляющаго вся недуги твоя,
    wecho - Избавляющаго от истления живот твой,
    wecho - Венчающаго тя милостию и щедротами,
    wecho - Исполняющаго во благих желание твое:
    wecho - Обновится яко орля юность твоя.
    wait 1s
    wecho - Щедр и милостив, Господь.
    wecho - Долготерпелив и многомилостив.
    wecho - Благослови, душе моя, Господа
    wecho - И вся внутренняя моя Имя святое Его.
    wecho - Благословен еси Господи!
    
  break
  case 3
    *3 молитва
    wecho - Во царствии Твоем помяни нас, Господи,
    wait 1s
    wecho - Егда приидеши во Царствии Твоем.
    wecho - Блажени нищии духом, яко тех есть царство небесное.
    wait 1s
    wecho - Блажени плачущии, яко тии утешатся.
    wecho - Блажени кротции, яко тии наследят землю.
    wait 1s
    wecho - Блажени алчущии и жаждущии правды, яко тии насытятся.
    wecho - Блажени милостивии, яко тии помиловани будут.
    wait 1s
    wecho - Блажени чистии сердцем, яко тии Бога узрят.
    wecho - Блажени миротворцы, яко тии сынове Божии нарекутся.
    wecho - Блажени изгнани правды ради,
    wecho - Яко тех есть Царство небесное.
    wecho - Блажени есте, егда поносят вам,
    wecho - И ижденут и рекут всяк зол глагол,
    wecho - На вы лжуще Мене ради.
    wecho - Радуйтеся и веселитеся,
    wecho - Яко мзда ваша многа на Небеси.
    
  break
  case 4
    *4 молитва
    wecho - Днесь висит на Древе,
    wecho - Иже на водах землю повесивый: 
    wecho - Венцем от терния облагается, 
    wecho - Иже Ангелов Царь: 
    wait 1s
    wecho - В ложную багряницу облачается, 
    wait 1s
    wecho - Одеваяй небо облаки: заушение прият, 
    wecho - Иже во Иордане свободивый Адама: 
    wecho - Гвоздьми пригвоздися Жених Церковный: 
    wecho - Копием прободеся Сын Девы. 
    wait 1s
    wecho - Покланяемся Страстем Твоим, Христе, 
    wait 1s
    wecho - Покланяемся Страстем Твоим, Христе, 
    wait 1s
    wecho - Покланяемся Страстем Твоим, Христе. 
    wait 1s
    wecho - Покажи нам и славное Твое Воскресение. 
  break
  case 5
    *5 молитва
    wecho - Душа моя прегрешная, что не плачешься?
    wait 1s
    wecho - Ты плачь, душа, рыдай всегда: 
    wecho - Тем утешишься. 
    wait 1s
    wecho - Не сможешь ты тогда плакати, 
    wecho - Когда приидет смерть. 
    wait 1s
    wecho - Скинь одежду ты греховную в покаянии, 
    wecho - А не скинешь ты - грехи свои ада не минешь. 
    wecho - Страдалицы венцы носят на главах своих. 
    wecho - Они песни поют - ах - ангельские: 
    wait 1s
    wecho - Аллилуйа, аллилуйа, аллилуйа, аллилуйа. 
    
  break
  default 
    halt
  break 
done
wait 1s
wforce певчий эм бухнулся на колени в земном поклоне
wforce певчий эм бухнулся на колени, едва не разбив лоб об пол
wforce певчий эм бухнулся на колени, да так и остался
~
#85118
ДалиИнгридиентыТелепортеру~
0 j 100
~
wait 1
If (%object.vnum%==721)
  If (%object.val1%>5)
    say Отличный минерал!
    If  !(%self.haveobj(60064)%)
      mload obj 60064
    else 
      нет
      say Минерал у меня уже есть. Поищи другой ингридиент.
    End
  Else
    нет
    say Этот минерал со сколом, такой не подойдет.
  end 
End
If (%object.vnum%==722)
  If (%object.val1%>5)
    say Да, этот металл как раз подойдет.
    If  !(%self.haveobj(60065)%)
      mload obj 60065
    Else 
      нет
      say У меня уже есть метал! Куда мне еще?
    End
  Else
    эм ощупала металл, попробовала на зуб
    say  Нет, этот металл недостаточно прочен! Такой не подойдет.
  end 
End
If (%object.vnum%==700)
  If (%object.val1%>5)
    Say Уу-у! Замечательные грибы!
    If  !(%self.haveobj(60060)%)
      Mload obj 60060
    Else 
      say У меня уже есть гриб! А свой лучше в лесу посади!
    End
  Else
    say Да чтож такой червивый гриб? Не пойдет такой!
  end 
End    
mpurge %object%
if (%self.haveobj(60064)% && %self.haveobj(60065)%)
  say Отлично! Сейчас я сварю нужное зелье.
  say С его помощью я перенесу Вас в Корсунь
  mecho Ворожея положила ингридиенты в котел и что-то зашеплата над ним.
  %send% %actor% Пары из котла ударили Вам в голову...
  wait 1s
  %send% %actor% Все вокруг закрутилось в бешеном хороводе.
  %send% %actor% Города, селы, деревни - все мелькало перед Вашими глазами, 
  %send% %actor% Пока, наконец Вы не оказались во граде Корсунь.
  брос все
  mteleport all 27016
end
if (%self.haveobj(60064)% && %self.haveobj(60060)%)
  say Отлично! Сейчас я сварю нужное зелье.
  say С его помощью я перенесу Вас в Великий Новогород.
  mecho Ворожея положила ингридиенты в котел и что-то зашеплата над ним.
  %send% %actor% Пары из котла ударили Вам в голову...
  wait 1s
  %send% %actor% Все вокруг закрутилось в бешеном хороводе.
  %send% %actor% Города, селы, деревни - все мелькало перед Вашими глазами, 
  %send% %actor% Пока, наконец Вы не оказались во граде великом - Новогороде.
  брос все
  mteleport all 60085
end   
if (%self.haveobj(60065)% && %self.haveobj(60060)%)
  say Отлично! Сейчас я сварю нужное зелье.
  say С его помощью я перенесу Вас в Брянск.
  mecho Ворожея положила ингридиенты в котел и что-то зашеплата над ним.
  %send% %actor% Пары из котла ударили Вам в голову...
  wait 1s
  %send% %actor% Все вокруг закрутилось в бешеном хороводе.
  %send% %actor% Города, селы, деревни - все мелькало перед Вашими глазами, 
  %send% %actor% Пока, наконец Вы не оказались во граде Галиче.
  брос все
  mteleport all 66036
end
~
#85119
ТригСтражаЗападныхВорот~
0 q 100
~
%actor.wait(1)%
if ( %actor.class% == 4 )
  сказ %actor.name% Стой, путник!
  сказ %actor.name% Да будет тебе известно, что князь наш Всеволод решил все распри прекратить!
  сказ %actor.name% И всех бунтарей, да убийц вроде тебя на кол повестить! Или утопить!
  сказ %actor.name% Смотри, коли не внемлешь предупреждениям, утопит тебя стража в омуте черном!
  halt
end
*
wait 1s
*mecho %direction%
if %direction% == west
  *пришли из леса
  *mecho %direction% 
  хму
  wait 1s
  say Ты кто таков?
  mecho - Добрые люди по лесам глухим не шляются!
  эм посторонился, предоставляя путь
  wait 1s
  mecho - Проходи, да помни за чужаками у нас глаз, да глаз!
end
if %direction% == east
  *пришли из города
  *mecho %direction% 
  say Покидаешь нас, странник?
  вопрос %actor.name%
  wait 1s
  взд
  wait 1s
  эм посторонился, предоставляя путь
  wait 1s
  mecho - Смотри, соторожнее в лесах глухих зверье водится и нечисть всякая...
  mecho - Кабы не случилось ничего плохого...
end
~
#85120
ТригСтражаЮжныхВорот~
0 q 100
~
%actor.wait(1)%
if ( %actor.class% == 4 )
  сказ %actor.name% Стой, путник!
  сказ %actor.name% Да будет тебе известно, что князь наш Всеволод решил все распри прекратить!
  сказ %actor.name% И всех бунтарей, да убийц вроде тебя на кол повестить! Или утопить!
  сказ %actor.name% Смотри, коли не внемлешь предупреждениям, утопит тебя стража в омуте черном!
  halt
end
wait 1s
*mecho %direction%
if %direction% == south
  *пришли с озера
  *mecho %direction% 
  хму
  say Ты кто таков?
  wait 1s
  mecho - Добрые люди по местам глухим не шляются!
  mecho - А мож ты водяная нечисть какая с нашего озера выползшая?
  wait 1s
  %send% %actor% Стражник подозрительно осмотрел Вас и даже кое-где пощупал.
  %echoround% %actor% Стражник подозрительно осмотрел %actor.rname% и даже кое-где пощупал.
  *wait 1s
  mecho - Не похож что-то ты на нечисть...
  mecho - Ладно проходи!
  эм посторонился, предоставляя путь
end
if ( %direction% == north ) || ( %direction% == west ) || ( %direction% == east )
  *пришли из города
  *mecho %direction% 
  say Покидаешь нас, странник?
  вопрос %actor.name%
  wait 1s
  взд
  wait 1s
  эм посторонился, предоставляя путь
  wait 1s
  mecho - Смотри, соторожнее в озере не утони, а то водяной нынче лютует!
  mecho - Уж второй десяток утопленников за последнюю седмицу выловили.
end
~
#85121
ТригСтражаВосточныхВорот~
0 r 100
~
%actor.wait(1)%
*wait 1s
if ( %actor.class% == 4 )
  сказ %actor.name% Стой, путник!
  сказ %actor.name% Да будет тебе известно, что князь наш Всеволод решил все распри прекратить!
  сказ %actor.name% И всех бунтарей, да убийц вроде тебя на кол повестить! Или утопить!
  сказ %actor.name% Смотри, коли не внемлешь предупреждениям, утопит тебя стража в омуте черном!
  halt
end
wait 1s
*mecho %direction%
if %direction% == east
  *пришли из леса
  *mecho %direction% 
  хму
  say Ты кто таков?
  wait 1s
  mecho - Добрые люди по лесам глухим не шляются!
  эм посторонился, предоставляя путь
  wait 1s
  mecho - Проходи, да помни за чужаками у нас глаз, да глаз!
end
if ( %direction% == west ) || ( %direction% == north ) || ( %direction% == south )
  *пришли из города
  *mecho %direction% 
  say Покидаешь нас, странник?
  вопрос %actor.name%
  wait 1s
  взд
  wait 1s
  эм посторонился, предоставляя путь
  wait 1s
  mecho - Смотри, осторожнее в лесах глухих зверье водится и нечисть всякая...
  mecho - Кабы не случилось ничего плохого...
end
~
#85122
ВстречаетГриденьКрасныхПалат~
0 r 100
~
wait 1s
эм перестал клевать носом
wait 1s
say Здравствуй, гость незванный.
say По княжьему указу велено отдать Красные палаты под гостиный двор для важных господ.
say Чтобы могли приезжие гости отдохнуть с дороги, да делами своими заняться.
say Коль знатного ты роду племени - добро пожаловать!
~
#85123
ПытаемсяВойтиВКрасныеПалаты~
2 e 100
~
*если входящий не вор и не убийца и не бедный как церковная мышка
if (( %actor.class% == 4 ) || ( %actor.class% == 2 ) || ( %actor.bank% < 100000 ) )
  %send% %actor% _Пожилые гридни преградили путь перекрещенными мечами.
  %echoaround% %actor% Пожилые гридни скрестили мечи перед носом %actor.rname%.
  *wait 1s
  if ( %actor.class% == 4 )
    *наем
    %send% %actor% &CПожилой гридень сказал Вам: 'Не велено убийц позорных в палаты пускать!'
  end 
  if ( %actor.class% == 2 )
    *тать
    %send% %actor% &CПожилой гридень сказал Вам: 'Не велено воров позорных в палаты пускать!'
  end
  if ( %actor.bank% < 100000 ) 
    *бедный
    %send% %actor% &CПожилой гридень сказал Вам: 'Да ты беден, как церковная мышь!'
  end
  return 0
else
  *не наем и не мышь (не церковная) :)
  *
  return 1 
  halt
end 
~
#85124
ВстречаетКняжескийСтряпчий~
0 r 70
~
wait 1s
say Ну, что изволитте, господине?
say Все что в наличии на прилавках, смотри ничего другого нет!
~
#85125
ДалиКуныСмотрителю~
0 m 0
~
wait 1s
г Всегда рад услужить!
switch %amount%
  case 3025
    *набор первой помощи
    * всевиды
    mload obj 144
    mload obj 144
    mload obj 144
    mload obj 144
    mload obj 144
    mload obj 144
    mload obj 144
    mload obj 144
    mload obj 144
    mload obj 144
    *реколы
    mload obj 139       
    mload obj 139
    mload obj 139
    mload obj 139
    mload obj 139
    *серые
    mload obj 142
    mload obj 142
    mload obj 142
    mload obj 142
    mload obj 142
    *красные
    mload obj 141 
    mload obj 141
    mload obj 141
    mload obj 141
    mload obj 141
    mload obj 141
  break
  case 500
    dg_cast 'полет' %actor.name%
  break
  case 60
    dg_cast 'определение невидимости' %actor.name%
  break
  case 61
    dg_cast 'определение магии' %actor.name%
  break
  case 62
    dg_cast 'видение ночью' %actor.name%
  break
  case 1000
    dg_cast 'невидимость' %actor.name%
  break
  case 250
    dg_cast 'доблесть' %actor.name%
  break
  case 251
    dg_cast 'сила' %actor.name%
  break
  case 252
    dg_cast 'защита' %actor.name%
  break
  case 253
    dg_cast 'затуманивание' %actor.name%
  break
  case 1001
    dg_cast 'освящение' %actor.name%
  break
  case 1002
    dg_cast 'призматическая аура' %actor.name%
  break
  case 254
    dg_cast 'свет' %actor.name%
  break
  case 1683
    dg_cast 'полет' %actor.name%
    dg_cast 'определение невидимости' %actor.name%
    dg_cast 'определение магии' %actor.name%
    dg_cast 'видение ночью' %actor.name%
    dg_cast 'невидимость' %actor.name%
  break
  case 1006
    dg_cast 'доблесть' %actor.name%
    dg_cast 'сила' %actor.name%
    dg_cast 'защита' %actor.name%
    dg_cast 'затуманивание' %actor.name%
  break
  case 2257
    dg_cast 'освящение' %actor.name%
    dg_cast 'определение невидимости' %actor.name%
    dg_cast 'определение магии' %actor.name%
    dg_cast 'видение ночью' %actor.name%
    dg_cast 'невидимость' %actor.name%
  break
  case 315
    *набор еды в дорогу
    * квас
    mload obj 85112
    *гусь
    mload obj 85108
    mload obj 85108
    mload obj 85108
    mload obj 85108
    mload obj 85108
    mload obj 85108
    mload obj 85108
    mload obj 85108
    mload obj 85108
    *десерт
    mload obj 85117       
    mload obj 85117
    mload obj 85117
  break
  case 2050
    *набор брони
    mload obj 85121
    mload obj 85122
    mload obj 85123
    mload obj 85124
    mload obj 85125
    mload obj 85126
    mload obj 85127
    mload obj 85128
    mload obj 85129
  break
  default 
    нет
    say Еще раз прочитай расценки!
    дать %amount% кун %actor.name%
    halt
  break 
done
дать все %actor.name%
~
#85126
ВстретилиКуденю~
0 r 100
~
wait 1s
switch %random.8%
  case 1
    ул %actor.name%
  break
  case 2
    зд %actor.name%
  break
  case 3
    поклон %actor.name%
  break
done
~
#85127
ВстретилиКняжича~
0 r 70
~
if ( %actor.class% == 4 )
  halt
end
wait 1s
г Привет...
г Поиграй со мной прятки?
г А то с няньками скучно. Они толстые и я их быстро нахожу.
взд
attach 85128 %self%
~
#85128
ПоиграемКняжичу~
0 d 0
давай поиграю поиграем~
wait 1s
виз
wait 1s
switch %random.2%
  case 1
    г Давай ты будешь прятаться, а я тебя искать!
    хих
    эм зажмурил глаза
    г Давай, я считаю до тридцати - прячься!
    wait 30s
    mteleport %self% %actor.realroom%
    рад
    г Я нашел тебя, я нашел!!!
    г С тобой тоже неинтересно...
    грусти
    эм повернулся и понуро пошел прочь
    %purge% %self% 
  break
  case 2
    г Давай я спрячусь, а ты меня найдешь!
    хих
    г Давай, считай до тридцати и ищи меня!
    *атач трига нашли мальчика
    set q851 %actor% 
    remote q851 %self.id%
    detach 85127 %self%
    attach 85129 %self%
    switch %random.3%
      case 1
        mteleport %self% 85191
      break
      case 2
        mteleport %self% 85135
      break
      case 3
        mteleport %self% 85176
      break
    done
    detach 85128 %self%
  break
done
~
#85129
НашлиКняжича~
0 r 100
~
if ( %actor.class% == 4 )
  halt
end   
* * *mecho !!!!!!!!!!!!!!!!!! %q851.name%
if %q851% == %actor%
  wait 1s
  *mecho !!!!!!!!!!!!!!!!!! %q851.name%
  г Нашел меня, нашел!
  г А няньки ниразу не находили!
  взд
  wait 1s
  г Приходи, %q851.name%, завтра поиграем еще!
  
  switch %random.3%
    case 1
      if (( %random.1000% < 310 ) && ( %world.curobjs(85147)% < 5 ))
        mload obj 85147
        г А это тебе на память!
      end 
    break
    case 2
      f (( %random.1000% < 310 ) && ( %world.curobjs(85148)% < 5 ))
      mload obj 85148
      г А это тебе на память!
    end 
  break
  case 3
    
    if (( %random.1000% < 310 ) && ( %world.curobjs(85149)% < 5 ))
      г А это тебе на память!
      mload obj 85149
    end 
  break
done
ул
дать все %q851.name%
%purge% %self%
end
~
#85130
ПоетЗахмелевшийДетина~
0 b 40
~
wait 1s
mecho _Вдруг гнусавым голосом детина запел:
switch %random.4%
  case 1
    mecho __Вы сыграйте гам такого,
    mecho __Чтобы ноги дрыгали,
    mecho __Чтобы всяки соплиносы
    mecho __Перед нами прыгали!
  break
  case 2
    mecho __Нам хотели запретить
    mecho __По этой улице ходить!
    mecho __Наши запретители,
    mecho __По морде не хотители?!
  break
  case 3
    mecho __Скобари вы, скобари,
    mecho __Чаго вы скобаритися!
    mecho __Давно побить меня хотели -
    mecho __Да начать боитеся!
  break
  case 4
    mecho __У кинжала ручка ала,
    mecho __Ручка вьется, как змея.
    mecho __Заведу большую драку -
    mecho __Выручай, кинжал, меня!
  break
  default 
    halt
  break 
done
~
#85131
ВошлиКПьяномуПопу~
0 r 100
~
wait 1s
mecho _Вы распахнули дверь в баньку и отшатнулись.
wait 1s
mecho _В нос шибануло могучим хмельным духом.
wait 1s
mecho _На полу плескалась брага, и по скользким доскам туда-сюда скользил корабль.
mecho _В виде корабля выступало корыто, с восседающим на нем попом.
mecho _Кочергу он использовал вместо весла.
switch %random.3%
  case 1
    mecho - Пиво, не броди! Дурака не дразни! - пьяно потребовал поп.
  break
  case 2
    mecho - Ты кто таков будешь, смерд?! - смерил Вас нетрезвым взглядом поп.
  break
  case 3
    mecho - Проваливай отсюда,холоп! - пьяно промычал поп.
  break
  default 
    halt
  break 
done
~
#85132
ВстречаетКнязь~
0 r 100
~
*глобальный квест через 3 зоны (раз в морт) - 
* Марин Чертог
* Перунова крепь
* Край велеса
if ( %actor.class% == 4 )
  wait 1s
  дум
  wait 1s
  г Да будет ведомо тебе, %actor.name%, что желаю я все распри прекратить!
  г А бунтарей, да разбойников и убийц вроде тебя велю на кол повесить!
  г Шел бы подобру-поздоровому отсель.
  halt
end
* 
wait 1s 
if !%actor.quested(85021)%
  wait 1s
  эм озабоченно нахмурил брови
  wait 1s
  mecho - Распри княжеские прекращать пора, - угрюмо сказал князь.
  mecho - Слабеет Русь. На глазах слабеет!
  mecho - Ворог - глянь-ка! - уж на пороге стоит, железом бряцает.
  mecho - А князья знай друг друга за бороды таскают.
  взд
  wait 1s
  mecho - Брат с братом из-за клочка земли свару подымает, насмерть грызутся, точно стая паучья.
  mecho - А погань всякая этим разладом пользуется!
  mecho - Торговлю под корень извели, к Кавказу проход перекрыли...
  печ
  wait 1s
  mecho - Вот что, %actor.name%...
  mecho - Нашептала мне тут знахарка наша, что может любой князь Великую силу обрести, 
  mecho - Да распри эти прератить.
  mecho - Для того надо обратиться к силе богов древних.
  mecho - Рекут, что коли собрать три священных идола, то обретет смертный силы трех богов.
  mecho - Вот добыл%actor.g% бы ты их мне.
  вопрос
  attach 85133 %self%
  *detach 85132 %self%
else
  г Обещал%actor.u% ты уже помочь мне..
  wait 1s
  if  !%actor.quested(85022)%
    г Идол Морены-Смерти где?
  end
  if  !%actor.quested(85023)%
    г Идол Перуна-Громовержца где?
  end
  if  !%actor.quested(85024)%
    г Идол Велеса Волохатого бога где?
  end
end
~
#85133
ДаКнязю~
0 d 0
да конечно добуду~
if !%actor.quested(85021)%
  wait 1s
  кив
  wait 1s
  mecho - Непрост будет путь твой...
  mecho - Ведь идолы те находятся в чертогах самих богов.
  эм покачал головой
  wait 1s
  mecho - Иди же!
  mecho - И принеси мне три священных идола:
  mecho - Морены-Смерти,
  mecho - Перуна-Громовержца
  mecho - И Велеса-Волохатого бога!
  %actor.setquest(85021)%
  detach 85132 %self%
  detach 85133 %self%
else
  хмур
  г Ты кажется уже пытал%actor.u% мне помочь?
end
~
#85134
СдаемКвестКнязю~
0 j 100
~
if ( %actor.class% == 4 )
  wait 1s
  дум
  wait 1s
  г Ты все еще тут?
  г Как смеешь ты против воли княжесокой идти?!
  wait 1s
  mecho _&YЗакричал князь громким голосом: Стража! Вывезти %actor.rname% за пределы княжества!&n
  wait 1s
  mecho _Тут же сбежалась стража с мешками, да веревками.
  %send% %actor% В следующий миг Вы поняли, что Вас куда-то везут.
  %echoaround% %actor% Стража повалила %actor.vname%, одела мешок на голову, и куда-то поволокла.
  mteleport %actor.name% 85297
  halt
end
if ( ( %actor.quested(85021)% ) && ( !%actor.quested(85025)% ) )
  *
  switch %object.vnum% 
    case 85134
      кив
      wait 1s
      г Какой храбрец, саму Морену одолел!
      %actor.setquest(85022)%
      *идол морены
      %purge% %object%  
    break
    case 85135
      кив
      wait 1s
      г Какой храбрец, к самому Перуну дошел!
      %actor.setquest(85023)%
      *идол перуна
      %purge% %object%   
    break
    case 85136
      кив
      wait 1s
      г Какой храбрец, к самому Велесу в гости ходил!
      %actor.setquest(85024)%
      *идол велеса
      %purge% %object%  
    break
    default
      wait 1s
      хму
      wait 1s
      г Это еще зачем?
      бро все
    break
  done 
  if  ( %actor.quested(85022)%  &&  %actor.quested(85023)%  &&  %actor.quested(85024)%  )
    *if ( ( %actor.quested(850202)% )  ( %actor.quested(850203)% )  ( %actor.quested(850204)% )
    wait 1s
    *задание выполнено - награда
    эм расставил идолы по углам комнаты
    wait 1s
    mecho _Морена заняла почетное место у северного окна, овеваемая всеми ветрами.
    mecho _Перун расположился у восточного окна, взирая на восход ярого солнца.
    mecho _Велес задумчиво стоял в тени западной стены, укрытый сумраком Нави.
    wait 1s
    эм довольно потер руки
    wait 1s
    г Почти все готово для ритуала!
    wait 1s
    г Теперь пришло время тебя наградить!
    *
    switch %actor.class%
      *лекарь
      case 0
        if ( %random.1000% < 401 )
          mload obj 1757
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      *колдун
      case 1
        if ( %random.1000% < 401 )
          mload obj 1759
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      *тать
      case 2
        if ( %random.1000% < 401 )
          mload obj 1757
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      *богатырь
      case 3
        if ( %random.1000% < 401 )
          mload obj 1759
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      *наемник
      case 4
        *сюда не дойдет. Выше по тригу его стелепортит отсюда
      break
      *друж
      case 5
        if ( %random.1000% < 401 )
          mload obj 1757
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      *кудесник
      case 6
        if ( %random.1000% < 401 )
          mload obj 1759
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      *волшебники
      case 7
        if ( %random.1000% < 401 )
          mload obj 1757
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      case 8
        if ( %random.1000% < 401 )
          mload obj 1759
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      *витязь
      case 9
        if ( %random.1000% < 401 )
          mload obj 1757
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end 
      break
      *охот
      case 10
        if ( %random.1000% < 401 )
          mload obj 1759
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      *кузнец
      case 11
        if ( %random.1000% < 401 )
          mload obj 1757
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      *купец
      case 12
        if ( %random.1000% < 401 )
          mload obj 1759
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      *волхв
      case 13
        if ( %random.1000% < 401 )
          mload obj 241
          дать все %actor.name%
        else
          %self.gold(+4000)%
          дать 4000 кун %actor.name%
        end
      break
      default
        %self.gold(+4000)%
        дать 4000 кун %actor.name%
      break
    done
    г Да, и незабудь к купцу Маралису заглянуть!
    г Диковинный товар нынче продает! И только по моему распоряжению!
    %actor.setquest(85026)%
    %actor.unsetquest(85022)%
    %actor.unsetquest(85023)%
    %actor.unsetquest(85024)%
    %actor.unsetquest(85021)%
  end
  *
  * 
end
wait 1s
брос все
~
#85135
ДалиМышкеХлебушек~
0 j 100
~
*
if ( ( %object.vnum% == 101 ) || ( %object.vnum% == 125 ) )
  wait 1s
  эм завороженно уставилась на свежий белый хлеб
  wait 1s
  mecho _Кусок хлеба был явно больше мышки, что ничуть ее не смутило.
  mecho _Бросив сухарик, приложив все силы мышка потащила кусок хлеба в норку.
  wait 1s
  mecho _Мышка так увлеклась, что не заметила как из-за щеки выпала спрятанная бусинка.
  switch %random.3%
    case 1
      mload obj 85138
    break
    case 2
      mload obj 85139
    break
    case 3
      mload obj 85140
    break
  done
  %purge% %object%
  бро бус
  %purge% %self%
end
~
#85136
МышкаУбегает~
0 r 30
~
эм ухватила зубами сухарик и ринулась от Вас в ближайший темный угол
%purge% %self%
~
#85137
ЖалуетсяШвеяМастерица~
0 r 100
~
wait 1s
эм рассерженно взмахнула пяльцем
wait 1s
г Все бусины мыши растащили, никакого сладу с ними нет!
~
#85138
ДаемБусиныШвееМастерице~
0 j 100
~
switch %object.vnum% 
  case 85138
    *янтарная
    wait 1s
    эм радостно всплеснула руками
    wait 1s
    г Поймал таки воровку!
    %purge% %object%
    if %random.1000% < 400
      г Спасибо тебе большое...
      г Вот возьми на память от меня эту бусинку...
      эм достала с полки клубок шелковых нитей и нанизала янтарную бусинку.
      г Вот, держи!
      mload obj 85141
      дать нить %actor.name%
      wait 1s
      г Если хочешь, можешь сходить к бабушке моей, она у меня колдунья.
      г Может заклятье какое наложит.
      г Живет она за посадом во глухом лесу, в избушке на кошачьих лапах.
    else
      г Хотела я тебя отблагодарить, да нечем
      груст
    end
  break
  case 85139
    *изумрудная
    wait 1s
    эм радостно всплеснула руками
    wait 1s
    г Поймал таки воровку!
    %purge% %object%
    if %random.1000% < 400
      г Спасибо тебе большое...
      г Вот возьми на память от меня эту бусинку...
      эм достала с полки клубок шелковых нитей и нанизала изумрудную бусинку.
      г Вот, держи!
      mload obj 85142
      дать нить %actor.name%
      wait 1s
      г Если хочешь, можешь сходить к бабушке моей, она у меня колдунья.
      г Может заклятье какое наложит.
      г Живет она за посадом во глухом лесу, в избушке на кошачьих лапах.
    else
      г Хотела я тебя отблагодарить, да нечем
      груст
    end 
  break
  case 85140
    *рубиновая
    wait 1s
    эм радостно всплеснула руками
    wait 1s
    г Поймал таки воровку!
    %purge% %object%
    if %random.1000% < 400
      г Спасибо тебе большое...
      г Вот возьми на память от меня эту бусинку...
      эм достала с полки клубок шелковых нитей и нанизала янтарную бусинку.
      г Вот, держи!
      mload obj 85143
      дать нить %actor.name%
      wait 1s
      г Если хочешь, можешь сходить к бабушке моей, она у меня колдунья.
      г Может заклятье какое наложит.
      г Живет она за посадом во глухом лесу, в избушке на кошачьих лапах.
    else
      г Хотела я тебя отблагодарить, да нечем
      груст
    end 
  break
done
~
#85139
ВстречаетКонюх~
0 r 100
~
wait 1s
г Лошадками торгуем, по сто кун за копыто!
г У лошади 4 копыта - итого 400 кун!
~
#85140
ДалиКуныКонюху~
0 m 0
~
if (( %amount% < 400 ) || ( %amount% > 400 ) )
  нет
  wait 1s
  г Считать научись!
  дать %amount% кун %actor.name%
  halt
end
if %amount% == 400
  кив
  wait 1s
  г Держи лошадку!
  mload mob 85123
  mforce жеребец след .%actor.name%
  halt
end
~
#85141
ВстречаетНаемовВоевода~
0 r 100
~
if ( %actor.class% == 4 )
  wait 1s
  дум
  wait 1s
  г Как ты здесь оказался, убойца?
  г Как смеешь ты против воли княжесокой идти?!
  wait 1s
  mecho _&YЗакричал воевода громким голосом: Стража! Вывезти %actor.rname% за пределы княжества!&n
  wait 1s
  mecho _Тут же сбежалась стража с мешками, да веревками.
  %send% %actor% В следующий миг Вы поняли, что Вас куда-то везут.
  %echoaround% %actor% Стража повалила %actor.vname%, одела мешок на голову, и куда-то поволокла.
  mteleport %actor.name% 85297
  halt
end
~
#85142
ВстречаетНаемовГридень~
0 r 100
~
if ( %actor.class% == 4 )
  wait 1s
  дум
  wait 1s
  сказ %actor.name% По княжьему указу убийц велено вешать или утоплять!
  сказ %actor.name% Уходи пока цел, путник!
  halt
end
~
#85143
РезетЗоны~
2 fg 100
~
*атачим триг квеста князя
calcuid knaz 85106 mob
attach 85132 %knaz%
*онуфрия атачим
calcuid onuf 85116 mob
attach 85114 %onuf%
~
#85144
ЗапретВходаВМагазин~
0 g 100
~
if !%actor.quested(85026)%
  сказ %actor.name% Нет распоряжения тебя здесь обслуживать!
  сказ %actor.name% Коли княжьим благословением заручишься - тогда приходи.
  msend %actor% _И сию секунду купец выставил Вас вон.
  mechoaround %actor% Купец что-то недовольно заворчал и выкинул %actor.vname% вон.
  mteleport %actor% 85148
  mechoaround %actor% %actor.name% выскочил%actor.g% от купца Маралиса, словно ошпарен%actor.w%.
else
  %actor.unsetquest(85026)%
end
~
$~
